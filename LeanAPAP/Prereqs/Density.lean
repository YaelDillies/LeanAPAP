import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Tactic.Positivity.Basic
import Mathlib.Tactic.Positivity.Finset

/-!
# Density of a finite set

This defines the density of a `Finset` and provides induction principles for finsets.

## Main declarations

* `Finset.dens t`: `dens s : â„•` returns the density of `s : Finset Î±`.

## Notation

If you need to specify the field the density is valued in, `dens[ğ•œ] s` is notation for
`(dens s : ğ•œ)`. Note that no dot notation is allowed.
-/

open Function Multiset Nat

variable {ğ•œ Î± Î² : Type*} [Fintype Î±]

namespace Finset
section Semifield
variable [Semifield ğ•œ] {s t : Finset Î±} {a b : Î±}

/-- `dens s` is the number of elements of `s`, aka its density. -/
def dens (s : Finset Î±) : ğ•œ := s.card / Fintype.card Î±

notation "dens[" ğ•œ "]" => @dens ğ•œ

lemma card_div_card_eq_dens (s : Finset Î±) : dens[ğ•œ] s = s.card / Fintype.card Î± := rfl

@[simp] lemma dens_empty : dens[ğ•œ] (âˆ… : Finset Î±) = 0 := by simp [dens]

@[simp] lemma dens_singleton (a : Î±) : dens ({a} : Finset Î±) = (Fintype.card Î± : ğ•œ)â»Â¹ := by
  simp [dens]

@[simp]
lemma dens_cons (h : a âˆ‰ s) : (s.cons a h).dens = dens s + (Fintype.card Î± : ğ•œ)â»Â¹ := by
  simp [dens, add_div]

@[simp]
lemma dens_disjUnion (s t : Finset Î±) (h) : dens[ğ•œ] (s.disjUnion t h) = dens s + dens t := by
  simp_rw [dens, card_disjUnion, Nat.cast_add, add_div]

section Lattice
variable [DecidableEq Î±]

lemma dens_union_add_dens_inter (s t : Finset Î±) :
    dens[ğ•œ] (s âˆª t) + dens (s âˆ© t) = dens s + dens t := by
  simp_rw [dens, â† add_div, â† Nat.cast_add, card_union_add_card_inter]

lemma dens_inter_add_dens_union (s t : Finset Î±) :
    dens[ğ•œ] (s âˆ© t) + dens (s âˆª t) = dens s + dens t := by rw [add_comm, dens_union_add_dens_inter]

@[simp] lemma dens_union_of_disjoint (h : Disjoint s t) : dens[ğ•œ] (s âˆª t) = dens s + dens t := by
  rw [â† disjUnion_eq_union s t h, dens_disjUnion _ _ _]

lemma dens_sdiff_add_dens_eq_dens (h : s âŠ† t) :  dens[ğ•œ] (t \ s) + dens s = dens t := by
  simp [dens, â† card_sdiff_add_card_eq_card h, add_div]

lemma dens_sdiff_add_dens : dens[ğ•œ] (s \ t) + dens t = (s âˆª t).dens := by
  rw [â† dens_union_of_disjoint sdiff_disjoint, sdiff_union_self_eq_union]

lemma dens_sdiff_comm [IsCancelAdd ğ•œ] (h : card s = card t) : dens[ğ•œ] (s \ t) = dens (t \ s) :=
  add_left_injective (dens t) $ by
    simp_rw [dens_sdiff_add_dens, union_comm s, â† dens_sdiff_add_dens, dens, h]

@[simp]
lemma dens_sdiff_add_dens_inter (s t : Finset Î±) : dens[ğ•œ] (s \ t) + dens (s âˆ© t) = dens s := by
  rw [â† dens_union_of_disjoint (disjoint_sdiff_inter _ _), sdiff_union_inter]

@[simp]
lemma dens_inter_add_dens_sdiff (s t : Finset Î±) : dens[ğ•œ] (s âˆ© t) + dens (s \ t) = dens s := by
  rw [add_comm, dens_sdiff_add_dens_inter]

lemma dens_filter_add_dens_filter_not_eq_dens
    (p : Î± â†’ Prop) [DecidablePred p] [âˆ€ x, Decidable (Â¬p x)] :
    dens[ğ•œ] (s.filter p) + dens (s.filter fun a â†¦ Â¬ p a) = dens s := by
  classical
  rw [â† dens_union_of_disjoint (disjoint_filter_filter_neg ..), filter_union_filter_neg_eq]

end Lattice

section CharZero
variable [CharZero ğ•œ]

@[simp] lemma dens_eq_zero : dens[ğ•œ] s = 0 â†” s = âˆ… := by
  simp (config := { contextual := true }) [dens, Fintype.card_eq_zero_iff, eq_empty_of_isEmpty]

lemma dens_ne_zero : dens[ğ•œ] s â‰  0 â†” s.Nonempty := dens_eq_zero.not.trans nonempty_iff_ne_empty.symm

protected alias âŸ¨_, Nonempty.dens_ne_zeroâŸ© := dens_ne_zero

variable [Nonempty Î±]

@[simp] lemma dens_univ : dens[ğ•œ] (univ : Finset Î±) = 1 := by simp [dens, card_univ]

@[simp] lemma dens_eq_one : dens[ğ•œ] s = 1 â†” s = univ := by
  simp [dens, div_eq_one_iff_eq, card_eq_iff_eq_univ]

lemma dens_ne_one : dens[ğ•œ] s â‰  1 â†” s â‰  univ := dens_eq_one.not

end CharZero

end Semifield

section LinearOrderedSemifield
variable [LinearOrderedSemifield ğ•œ] {s t : Finset Î±} {a b : Î±}

@[simp] lemma dens_nonneg : 0 â‰¤ dens[ğ•œ] s := by unfold dens; positivity

lemma dens_le_dens (h : s âŠ† t) : dens[ğ•œ] s â‰¤ dens t :=
  div_le_div_of_nonneg_right (mod_cast card_mono h) $ by positivity

lemma dens_lt_dens (h : s âŠ‚ t) : dens[ğ•œ] s < dens t :=
  div_lt_div_of_pos_right (mod_cast card_strictMono h) $ by
    cases isEmpty_or_nonempty Î±
    Â· simp [Subsingleton.elim s t, ssubset_irrfl] at h
    Â· positivity

@[mono] lemma dens_mono : Monotone (dens : Finset Î± â†’ ğ•œ) := fun _ _ â†¦ dens_le_dens
@[mono] lemma dens_strictMono : StrictMono (dens : Finset Î± â†’ ğ•œ) := fun _ _ â†¦ dens_lt_dens

lemma dens_map_le [Fintype Î²] (f : Î± â†ª Î²) : dens[ğ•œ] (s.map f) â‰¤ dens s := by
  cases isEmpty_or_nonempty Î±
  Â· simp [Subsingleton.elim s âˆ…]
  simp_rw [dens, card_map]
  gcongr
  Â· positivity
  Â· positivity
  Â· exact Fintype.card_le_of_injective _ f.2

section Lattice
variable [DecidableEq Î±]

lemma dens_union_le (s t : Finset Î±) : dens[ğ•œ] (s âˆª t) â‰¤ dens s + dens t :=
  dens_union_add_dens_inter (ğ•œ := ğ•œ) s t â–¸ le_add_of_nonneg_right dens_nonneg

lemma dens_le_dens_sdiff_add_dens : dens[ğ•œ] s â‰¤ dens (s \ t) + dens t := by
  simp_rw [dens, â† add_div, â† Nat.cast_add]
  gcongr
  Â· positivity
  Â· exact card_le_card_sdiff_add_card

variable [Sub ğ•œ] [OrderedSub ğ•œ]

lemma dens_sdiff (h : s âŠ† t) : dens[ğ•œ] (t \ s) = dens t - dens s := by
  suffices dens (t \ s) = dens (t \ s âˆª s) - dens s by rwa [sdiff_union_of_subset h] at this
  rw [dens_union_of_disjoint sdiff_disjoint, add_tsub_cancel_right]

lemma le_dens_sdiff (s t : Finset Î±) : dens[ğ•œ] t - dens s â‰¤ dens (t \ s) :=
  calc
    _ â‰¤ dens t - dens (s âˆ© t) := tsub_le_tsub_left (dens_mono inter_subset_left) _
    _ = dens[ğ•œ] (t \ s) := by rw [â† dens_sdiff inter_subset_right, sdiff_inter_self_right t s]

end Lattice

@[simp]
lemma dens_pos [CharZero ğ•œ] : 0 < dens[ğ•œ] s â†” s.Nonempty := dens_nonneg.gt_iff_ne.trans dens_ne_zero

protected alias âŸ¨_, Nonempty.dens_posâŸ© := dens_pos

end LinearOrderedSemifield
end Finset

open Finset
namespace Mathlib.Meta.Positivity
open Qq Lean Meta

@[positivity Finset.dens _]
def evalFinsetDens : PositivityExt where eval {u ğ•œ} _ _ e := do
  match e with
  | ~q(@Finset.dens _ $Î± $instÎ± $instğ•œ $s) =>
    let p_pos : Option Q(0 < $e) := â† (do
      let some ps â† proveFinsetNonempty s | pure none
      let .some instğ•œordfield â† trySynthInstanceQ q(LinearOrderedSemifield $ğ•œ) | pure none
      let .some instğ•œchar â† trySynthInstanceQ q(CharZero $ğ•œ) | pure none
      assumeInstancesCommute
      return some q(@Nonempty.dens_pos $ğ•œ $Î± $instÎ± $instğ•œordfield $s $instğ•œchar $ps))
    -- Try to show that the density is positive
    if let some p_pos := p_pos then
      return .positive p_pos
    let p_nonneg : Option Q(0 â‰¤ $e) := â† (do
      let .some instğ•œordfield â† trySynthInstanceQ q(LinearOrderedSemifield $ğ•œ) | pure none
      assumeInstancesCommute
      return some q(@dens_nonneg $ğ•œ $Î± $instÎ± $instğ•œordfield $s))
    -- Try to show that the density is nonnegative
    if let some p_nonneg := p_nonneg then
      return .nonnegative p_nonneg
    -- Fall back to showing that the density is nonzero
    let instğ•œchar â† synthInstanceQ q(CharZero $ğ•œ)
    let some ps â† proveFinsetNonempty s | return .none
    assertInstancesCommute
    return .nonzero q(@Nonempty.dens_ne_zero $ğ•œ $Î± $instÎ± $instğ•œ $s $instğ•œchar $ps)
  | _ => throwError "not Finset.dens"

variable {ğ•œ Î± : Type*} [Fintype Î±] {s : Finset Î±}

example [LinearOrderedSemifield ğ•œ] : 0 â‰¤ dens[ğ•œ] s := by positivity
example [LinearOrderedSemifield ğ•œ] {s : Finset Î±} (hs : s.Nonempty) : 0 < dens[ğ•œ] s := by positivity
example [LinearOrderedSemifield ğ•œ] [Nonempty Î±] : 0 < dens[ğ•œ] (univ : Finset Î±) := by positivity
example [PartialOrder ğ•œ] [Semifield ğ•œ] [CharZero ğ•œ] {s : Finset Î±} (hs : s.Nonempty) :
    dens[ğ•œ] s â‰  0 := by positivity
example [PartialOrder ğ•œ] [Semifield ğ•œ] [CharZero ğ•œ] [Nonempty Î±] :
    dens[ğ•œ] (univ : Finset Î±) â‰  0 := by positivity

end Mathlib.Meta.Positivity
