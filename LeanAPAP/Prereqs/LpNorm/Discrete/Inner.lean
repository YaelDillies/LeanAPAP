import Mathlib.Analysis.InnerProductSpace.PiL2
import LeanAPAP.Prereqs.LpNorm.Discrete.Defs

/-! # Inner product -/

open Finset Function Real
open scoped ComplexConjugate ENNReal NNReal NNRat

variable {Î¹ R S : Type*} [Fintype Î¹]

namespace MeasureTheory
section CommSemiring
variable [CommSemiring R] [StarRing R] [DistribSMul S R]

/-- Inner product giving rise to the L2 norm. -/
def dL2Inner (f g : Î¹ â†’ R) : R := âˆ‘ i, conj (f i) * g i

notation "âŸª" f ", " g "âŸ«_[" S "]" => dL2Inner (R := S) f g

lemma dL2Inner_eq_sum (f g : Î¹ â†’ R) : âŸªf, gâŸ«_[R] = âˆ‘ i, conj (f i) * g i := rfl

@[simp] lemma conj_dL2Inner (f g : Î¹ â†’ R) : conj âŸªf, gâŸ«_[R] = âŸªconj f, conj gâŸ«_[R] := by
  simp [dL2Inner_eq_sum, map_sum]

lemma dL2Inner_anticomm (f g : Î¹ â†’ R) : âŸªf, gâŸ«_[R] = âŸªconj g, conj fâŸ«_[R] := by
  simp [dL2Inner_eq_sum, map_sum, mul_comm]

@[simp] lemma dL2Inner_zero_left (g : Î¹ â†’ R) : âŸª0, gâŸ«_[R] = 0 := by simp [dL2Inner_eq_sum]
@[simp] lemma dL2Inner_zero_right (f : Î¹ â†’ R) : âŸªf, 0âŸ«_[R] = 0 := by simp [dL2Inner_eq_sum]

@[simp] lemma dL2Inner_of_isEmpty [IsEmpty Î¹] (f g : Î¹ â†’ R) : âŸªf, gâŸ«_[R] = 0 := by
  simp [Subsingleton.elim f 0]

@[simp] lemma dL2Inner_const_left (a : R) (f : Î¹ â†’ R) : âŸªconst _ a, fâŸ«_[R] = conj a * âˆ‘ x, f x := by
  simp only [dL2Inner_eq_sum, const_apply, mul_sum]

@[simp]
lemma dL2Inner_const_right (f : Î¹ â†’ R) (a : R) : âŸªf, const _ aâŸ«_[R] = (âˆ‘ x, conj (f x)) * a := by
  simp only [dL2Inner_eq_sum, const_apply, sum_mul]

lemma dL2Inner_add_left (fâ‚ fâ‚‚ g : Î¹ â†’ R) : âŸªfâ‚ + fâ‚‚, gâŸ«_[R] = âŸªfâ‚, gâŸ«_[R] + âŸªfâ‚‚, gâŸ«_[R] := by
  simp_rw [dL2Inner_eq_sum, Pi.add_apply, map_add, add_mul, sum_add_distrib]

lemma dL2Inner_add_right (f gâ‚ gâ‚‚ : Î¹ â†’ R) : âŸªf, gâ‚ + gâ‚‚âŸ«_[R] = âŸªf, gâ‚âŸ«_[R] + âŸªf, gâ‚‚âŸ«_[R] := by
  simp_rw [dL2Inner_eq_sum, Pi.add_apply, mul_add, sum_add_distrib]

lemma dL2Inner_smul_left [Star S] [StarModule S R] [IsScalarTower S R R] (c : S) (f g : Î¹ â†’ R) :
    âŸªc â€¢ f, gâŸ«_[R] = star c â€¢ âŸªf, gâŸ«_[R] := by
  simp only [dL2Inner_eq_sum, Pi.smul_apply, smul_mul_assoc, smul_sum, starRingEnd_apply, star_smul]

lemma dL2Inner_smul_right [Star S] [StarModule S R] [SMulCommClass S R R] (c : S) (f g : Î¹ â†’ R) :
    âŸªf, c â€¢ gâŸ«_[R] = c â€¢ âŸªf, gâŸ«_[R] := by
  simp only [dL2Inner_eq_sum, Pi.smul_apply, mul_smul_comm, smul_sum, starRingEnd_apply, star_smul]

lemma smul_dL2Inner_left [InvolutiveStar S] [StarModule S R] [IsScalarTower S R R] (c : S)
    (f g : Î¹ â†’ R) : c â€¢ âŸªf, gâŸ«_[R] = âŸªstar c â€¢ f, gâŸ«_[R] := by rw [dL2Inner_smul_left, star_star]

end CommSemiring

section CommRing
variable [CommRing R] [StarRing R]

@[simp]
lemma dL2Inner_neg_left (f g : Î¹ â†’ R) : âŸª-f, gâŸ«_[R] = -âŸªf, gâŸ«_[R] := by simp [dL2Inner_eq_sum]

@[simp]
lemma dL2Inner_neg_right (f g : Î¹ â†’ R) : âŸªf, -gâŸ«_[R] = -âŸªf, gâŸ«_[R] := by simp [dL2Inner_eq_sum]

lemma dL2Inner_sub_left (fâ‚ fâ‚‚ g : Î¹ â†’ R) : âŸªfâ‚ - fâ‚‚, gâŸ«_[R] = âŸªfâ‚, gâŸ«_[R] - âŸªfâ‚‚, gâŸ«_[R] := by
  simp_rw [sub_eq_add_neg, dL2Inner_add_left, dL2Inner_neg_left]

lemma dL2Inner_sub_right (f gâ‚ gâ‚‚ : Î¹ â†’ R) : âŸªf, gâ‚ - gâ‚‚âŸ«_[R] = âŸªf, gâ‚âŸ«_[R] - âŸªf, gâ‚‚âŸ«_[R] := by
  simp_rw [sub_eq_add_neg, dL2Inner_add_right, dL2Inner_neg_right]

end CommRing

section OrderedCommSemiring
variable [OrderedCommSemiring R] [StarRing R] [StarOrderedRing R] {f g : Î¹ â†’ R}

lemma dL2Inner_nonneg (hf : 0 â‰¤ f) (hg : 0 â‰¤ g) : 0 â‰¤ âŸªf, gâŸ«_[R] :=
  sum_nonneg fun _ _ â†¦ mul_nonneg (star_nonneg_iff.2 $ hf _) $ hg _

end OrderedCommSemiring

section LinearOrderedCommRing
variable [LinearOrderedCommRing R] [StarRing R] [TrivialStar R] {f g : Î¹ â†’ R}

--TODO: Can we remove the `TrivialStar` assumption?
lemma abs_dL2Inner_le_dL2Inner_abs : |âŸªf, gâŸ«_[R]| â‰¤ âŸª|f|, |g|âŸ«_[R] :=
  (abs_sum_le_sum_abs _ _).trans_eq $
    sum_congr rfl fun i _ â†¦ by simp only [abs_mul, conj_trivial, Pi.abs_apply]

end LinearOrderedCommRing

section RCLike
variable {Îº : Type*} [RCLike R] {f : Î¹ â†’ R}

lemma dL2Inner_eq_inner (f g : Î¹ â†’ R) :
    âŸªf, gâŸ«_[R] = inner ((WithLp.equiv 2 _).symm f) ((WithLp.equiv 2 _).symm g) := rfl

lemma inner_eq_dL2Inner (f g : PiLp 2 fun _i : Î¹ â†¦ R) :
    inner f g = âŸªWithLp.equiv 2 _ f, WithLp.equiv 2 _ gâŸ«_[R] := rfl

@[simp] lemma dL2Inner_self {_ : MeasurableSpace Î¹} [DiscreteMeasurableSpace Î¹] (f : Î¹ â†’ R) :
    âŸªf, fâŸ«_[R] = ((â€–fâ€–_[2] : â„) : R) ^ 2 := by
  simp_rw [â† algebraMap.coe_pow, â† NNReal.coe_pow]
  simp [dL2Norm_sq_eq_sum_nnnorm, dL2Inner_eq_sum, RCLike.conj_mul]

lemma dL2Inner_self_of_norm_eq_one (hf : âˆ€ x, â€–f xâ€– = 1) : âŸªf, fâŸ«_[R] = Fintype.card Î¹ := by
  simp [-dL2Inner_self, dL2Inner_eq_sum, RCLike.conj_mul, hf, card_univ]

lemma linearIndependent_of_ne_zero_of_dL2Inner_eq_zero {v : Îº â†’ Î¹ â†’ R} (hz : âˆ€ k, v k â‰  0)
    (ho : Pairwise fun k l â†¦ âŸªv k, v lâŸ«_[R] = 0) : LinearIndependent R v := by
  simp_rw [dL2Inner_eq_inner] at ho
  have := linearIndependent_of_ne_zero_of_inner_eq_zero ?_ ho
  exacts [this, hz]

variable {mÎ¹ : MeasurableSpace Î¹} [DiscreteMeasurableSpace Î¹]

lemma dL1Norm_mul (f g : Î¹ â†’ R) : â€–f * gâ€–_[1] = âŸªfun i â†¦ â€–f iâ€–, fun i â†¦ â€–g iâ€–âŸ«_[â„] := by
  simp [dL2Inner_eq_sum, dL1Norm_eq_sum_nnnorm]

end RCLike

variable {mÎ¹ : MeasurableSpace Î¹} [DiscreteMeasurableSpace Î¹]

/-- **Cauchy-Schwarz inequality** -/
lemma dL2Inner_le_dL2Norm_mul_dL2Norm (f g : Î¹ â†’ â„) : âŸªf, gâŸ«_[â„] â‰¤ â€–fâ€–_[2] * â€–gâ€–_[2] := by
  simpa [dL2Norm_eq_sum_nnnorm, PiLp.norm_eq_of_L2, sqrt_eq_rpow]
    using real_inner_le_norm ((WithLp.equiv 2 _).symm f) _

end MeasureTheory

namespace Mathlib.Meta.Positivity
open Lean Meta Qq Function MeasureTheory

section OrderedCommSemiring
variable [OrderedCommSemiring R] [StarRing R] [StarOrderedRing R] {f g : Î¹ â†’ R}

private lemma dL2Inner_nonneg_of_pos_of_nonneg (hf : 0 < f) (hg : 0 â‰¤ g) : 0 â‰¤ âŸªf, gâŸ«_[R] :=
  dL2Inner_nonneg hf.le hg

private lemma dL2Inner_nonneg_of_nonneg_of_pos (hf : 0 â‰¤ f) (hg : 0 < g) : 0 â‰¤ âŸªf, gâŸ«_[R] :=
  dL2Inner_nonneg hf hg.le

private lemma dL2Inner_nonneg_of_pos_of_pos (hf : 0 < f) (hg : 0 < g) : 0 â‰¤ âŸªf, gâŸ«_[R] :=
  dL2Inner_nonneg hf.le hg.le

end OrderedCommSemiring

/-- The `positivity` extension which identifies expressions of the form `âŸªf, gâŸ«_[R]`. -/
@[positivity âŸª_, _âŸ«_[_]] def evalL2Inner : PositivityExt where eval {u R} _ _ e := do
  match e with
  | ~q(@dL2Inner $Î¹ _ $instÎ¹ $instring $inststar $f $g) =>
      let _pğ•œ â† synthInstanceQ q(OrderedCommSemiring $R)
      let _pğ•œ â† synthInstanceQ q(StarOrderedRing $R)
      assumeInstancesCommute
      match â† core q(inferInstance) q(inferInstance) f,
        â† core q(inferInstance) q(inferInstance) g with
      | .positive pf, .positive pg => return .nonnegative q(dL2Inner_nonneg_of_pos_of_pos $pf $pg)
      | .positive pf, .nonnegative pg =>
        return .nonnegative q(dL2Inner_nonneg_of_pos_of_nonneg $pf $pg)
      | .nonnegative pf, .positive pg =>
        return .nonnegative q(dL2Inner_nonneg_of_nonneg_of_pos $pf $pg)
      | .nonnegative pf, .nonnegative pg => return .nonnegative q(dL2Inner_nonneg $pf $pg)
      | _, _ => return .none
  | _ => throwError "not dL2Inner"

section OrderedCommSemiring
variable [OrderedCommSemiring R] [StarRing R] [StarOrderedRing R] {f g : Î¹ â†’ R}

example (hf : 0 < f) (hg : 0 < g) : 0 â‰¤ âŸªf, gâŸ«_[R] := by positivity
example (hf : 0 < f) (hg : 0 â‰¤ g) : 0 â‰¤ âŸªf, gâŸ«_[R] := by positivity
example (hf : 0 â‰¤ f) (hg : 0 < g) : 0 â‰¤ âŸªf, gâŸ«_[R] := by positivity
example (hf : 0 â‰¤ f) (hg : 0 â‰¤ g) : 0 â‰¤ âŸªf, gâŸ«_[R] := by positivity

end OrderedCommSemiring
end Mathlib.Meta.Positivity

/-! ### HÃ¶lder inequality -/

namespace MeasureTheory
section Real
variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} [DiscreteMeasurableSpace Î±] [Fintype Î±] {p q : â„â‰¥0}
  {f g : Î± â†’ â„}

lemma dL1Norm_mul_of_nonneg (hf : 0 â‰¤ f) (hg : 0 â‰¤ g) : â€–f * gâ€–_[1] = âŸªf, gâŸ«_[â„] := by
  convert dL1Norm_mul f g using 2 <;> ext a <;> refine (norm_of_nonneg ?_).symm; exacts [hf _, hg _]

/-- **HÃ¶lder's inequality**, binary case. -/
lemma dL2Inner_le_dLpNorm_mul_dLpNorm (hpq : p.IsConjExponent q) (f g : Î± â†’ â„) :
    âŸªf, gâŸ«_[â„] â‰¤ â€–fâ€–_[p] * â€–gâ€–_[q] := by
  have hp := hpq.ne_zero
  have hq := hpq.symm.ne_zero
  norm_cast at hp hq
  simpa [dL2Inner_eq_sum, dLpNorm_eq_sum_nnnorm, *] using inner_le_Lp_mul_Lq _ f g hpq.coe

/-- **HÃ¶lder's inequality**, binary case. -/
lemma abs_dL2Inner_le_dLpNorm_mul_dLpNorm (hpq : p.IsConjExponent q) (f g : Î± â†’ â„) :
    |âŸªf, gâŸ«_[â„]| â‰¤ â€–fâ€–_[p] * â€–gâ€–_[q] :=
  abs_dL2Inner_le_dL2Inner_abs.trans $
    (dL2Inner_le_dLpNorm_mul_dLpNorm hpq _ _).trans_eq $ by simp_rw [dLpNorm_abs]

end Real

section Hoelder
variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} [DiscreteMeasurableSpace Î±] [Fintype Î±] [RCLike R]
  {p q : â„â‰¥0} {f g : Î± â†’ R}

lemma norm_dL2Inner_le (f g : Î± â†’ R) : â€–âŸªf, gâŸ«_[R]â€–â‚Š â‰¤ âŸªfun a â†¦ â€–f aâ€–, fun a â†¦ â€–g aâ€–âŸ«_[â„] :=
  (norm_sum_le _ _).trans $ by simp [dL2Inner]

/-- **HÃ¶lder's inequality**, binary case. -/
lemma nnnorm_dL2Inner_le_dLpNorm_mul_dLpNorm (hpq : p.IsConjExponent q) (f g : Î± â†’ R) :
    â€–âŸªf, gâŸ«_[R]â€–â‚Š â‰¤ â€–fâ€–_[p] * â€–gâ€–_[q] :=
  calc
    _ â‰¤ âŸªfun a â†¦ â€–f aâ€–, fun a â†¦ â€–g aâ€–âŸ«_[â„] := norm_dL2Inner_le _ _
    _ â‰¤ â€–fun a â†¦ â€–f aâ€–â€–_[p] * â€–fun a â†¦ â€–g aâ€–â€–_[q] := dL2Inner_le_dLpNorm_mul_dLpNorm hpq _ _
    _ = â€–fâ€–_[p] * â€–gâ€–_[q] := by simp_rw [dLpNorm_norm]

/-- **HÃ¶lder's inequality**, binary case. -/
lemma dLpNorm_mul_le (hp : p â‰  0) (hq : q â‰  0) (r : â„â‰¥0) (hpqr : pâ»Â¹ + qâ»Â¹ = râ»Â¹) (f g : Î± â†’ R) :
    â€–f * gâ€–_[r] â‰¤ â€–fâ€–_[p] * â€–gâ€–_[q] := by
  have hr : r â‰  0 := by
    rintro rfl
    simp [hp] at hpqr
  have : (â€–(f * g) Â·â€– ^ (r : â„)) = (â€–f Â·â€– ^ (r : â„)) * (â€–g Â·â€– ^ (r : â„)) := by
    ext; simp [mul_rpow, abs_mul]
  rw [dLpNorm_eq_dL1Norm_rpow, NNReal.rpow_inv_le_iff_of_pos, this, â† NNReal.coe_le_coe]
  push_cast
  rw [dL1Norm_mul_of_nonneg, mul_rpow, â† NNReal.coe_rpow, â† NNReal.coe_rpow, dLpNorm_rpow',
    dLpNorm_rpow', â† ENNReal.coe_div, â† ENNReal.coe_div]
  refine dL2Inner_le_dLpNorm_mul_dLpNorm âŸ¨?_, ?_âŸ© _ _
  Â· norm_cast
    rw [div_eq_mul_inv, â† hpqr, mul_add, mul_inv_cancelâ‚€ hp]
    exact lt_add_of_pos_right _ (by positivity)
  Â· norm_cast
    simp [div_eq_mul_inv, hpqr, â† mul_add, hr]
  any_goals intro a; dsimp
  all_goals positivity

/-- **HÃ¶lder's inequality**, binary case. -/
lemma dL1Norm_mul_le (hpq : p.IsConjExponent q) (f g : Î± â†’ R) :
    â€–f * gâ€–_[1] â‰¤ â€–fâ€–_[p] * â€–gâ€–_[q] :=
  dLpNorm_mul_le (mod_cast hpq.ne_zero) (mod_cast hpq.symm.ne_zero) _
    (by simpa using hpq.inv_add_inv_conj) _ _

/-- **HÃ¶lder's inequality**, finitary case. -/
lemma dLpNorm_prod_le {Î¹ : Type*} {s : Finset Î¹} (hs : s.Nonempty) {p : Î¹ â†’ â„â‰¥0} (hp : âˆ€ i, p i â‰  0)
    (q : â„â‰¥0) (hpq : âˆ‘ i âˆˆ s, (p i)â»Â¹ = qâ»Â¹) (f : Î¹ â†’ Î± â†’ R) :
    â€–âˆ i âˆˆ s, f iâ€–_[q] â‰¤ âˆ i âˆˆ s, â€–f iâ€–_[p i] := by
  induction' s using Finset.cons_induction with i s hi ih generalizing q
  Â· cases not_nonempty_empty hs
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· simp only [sum_cons, sum_empty, add_zero, inv_inj] at hpq
    simp [â† hpq]
  simp_rw [prod_cons]
  rw [sum_cons, â† inv_inv (âˆ‘ _ âˆˆ _, _ : â„â‰¥0)] at hpq
  refine (dLpNorm_mul_le (hp _) (inv_ne_zero (sum_pos (fun _ _ â†¦ ?_) hs).ne') _ hpq _ _).trans
    (mul_le_mul_left' (ih hs _ (inv_inv _).symm) _)
  exact pos_iff_ne_zero.2 (inv_ne_zero $ hp _)

end Hoelder
end MeasureTheory
