import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Series
import Mathlib.Combinatorics.Additive.Randomisation
import LeanAPAP.Prereqs.FourierTransform.Compact

/-!
# Rudin's inequality
-/

open Finset hiding card
open Fintype (card)
open Function Real MeasureTheory
open Complex (I re im)
open scoped BigOperators Nat NNReal ENNReal ComplexConjugate ComplexOrder

variable {G : Type*} [Fintype G] [AddCommGroup G] {p : â„•}

variable [MeasurableSpace G] [DiscreteMeasurableSpace G]

/-- **Rudin's inequality**, exponential form. -/
lemma rudin_exp_ineq (f : G â†’ â„‚) (hf : AddDissociated <| support <| cft f) :
    ğ”¼ a, exp (f a).re â‰¤ exp (â€–fâ€–â‚™_[2] ^ 2 / 2) := by
  have (z : â„‚) : exp (re z) â‰¤ cosh â€–zâ€– + re (z / â€–zâ€–) * sinh â€–zâ€– :=
    calc
      _ = _ := by obtain rfl | hz := eq_or_ne z 0 <;> simp [*]
      _ â‰¤ _ := exp_mul_le_cosh_add_mul_sinh (by simpa using z.abs_re_div_norm_le_one) _
  choose c hc hcf using fun Ïˆ â†¦ Complex.exists_norm_mul_eq_self (cft f Ïˆ)
  have hcâ‚€ (Ïˆ) : c Ïˆ â‰  0 := fun h â†¦ by simpa [h] using hc Ïˆ
  have (a : G) :
    exp (f a).re â‰¤ âˆ Ïˆ, (cosh â€–cft f Ïˆâ€– + (c Ïˆ * sinh â€–cft f Ïˆâ€– * Ïˆ a).re) :=
    calc
      _ = âˆ Ïˆ, exp ((cft f Ïˆ * Ïˆ a).re) := by simp_rw [â† exp_sum, â† Complex.re_sum, cft_inversion]
      _ â‰¤ _ := prod_le_prod (fun _ _ â†¦ by positivity) fun _ _ â†¦ this _
      _ = âˆ Ïˆ, (cosh â€–cft f Ïˆâ€– + (c Ïˆ * (cft f Ïˆ * Ïˆ a)
            / (c Ïˆ * â†‘â€–cft f Ïˆâ€–)).re * sinh â€–cft f Ïˆâ€–) := by
          simp_rw [norm_mul, AddChar.norm_apply, mul_one, mul_div_mul_left _ _ (hcâ‚€ _)]
      _ = _ := by
          congr with Ïˆ
          obtain hÏˆ | hÏˆ := eq_or_ne (cft f Ïˆ) 0
          Â· simp [hÏˆ]
          Â· simp only [hcf, mul_left_comm (c _), mul_div_cancel_leftâ‚€ _ hÏˆ, â† Complex.re_mul_ofReal,
              mul_right_comm]
  calc
    _ â‰¤ ğ”¼ a, âˆ Ïˆ, (cosh â€–cft f Ïˆâ€– + (c Ïˆ * sinh â€–cft f Ïˆâ€– * Ïˆ a).re) :=
        expect_le_expect fun _ _ â†¦ this _
    _ = âˆ Ïˆ, cosh â€–cft f Ïˆâ€– :=
        AddDissociated.randomisation _ _ <| by simpa [-Complex.ofReal_sinh, hcâ‚€]
    _ â‰¤ âˆ Ïˆ, exp (â€–cft f Ïˆâ€– ^ 2 / 2) :=
        prod_le_prod (fun _ _ â†¦ by positivity) fun _ _ â†¦ cosh_le_exp_half_sq _
    _ = _ := by simp_rw [â† exp_sum, â† sum_div, â† dL2Norm_cft, dL2Norm_sq_eq_sum_norm]

/-- **Rudin's inequality**, exponential form with absolute values. -/
lemma rudin_exp_abs_ineq (f : G â†’ â„‚) (hf : AddDissociated <| support <| cft f) :
    ğ”¼ a, exp |(f a).re| â‰¤ 2 * exp (â€–fâ€–â‚™_[2] ^ 2 / 2) := by
  calc
    _ â‰¤ ğ”¼ a, (exp (f a).re + exp (-f a).re) := expect_le_expect fun _ _ â†¦ exp_abs_le _
    _ = ğ”¼ a, exp (f a).re + ğ”¼ a, exp ((-f) a).re := by simp [expect_add_distrib]
    _ â‰¤ exp (â€–fâ€–â‚™_[2] ^ 2 / 2) + exp (â€–-fâ€–â‚™_[2] ^ 2 / 2) :=
        add_le_add (rudin_exp_ineq f hf) (rudin_exp_ineq (-f) <| by simpa using hf)
    _ = _ := by simp [two_mul]

private lemma rudin_ineq_aux (hp : 2 â‰¤ p) (f : G â†’ â„‚) (hf : AddDissociated <| support <| cft f) :
    â€–re âˆ˜ fâ€–â‚™_[p] â‰¤ 2 * exp 2â»Â¹ * sqrt p * â€–fâ€–â‚™_[2] := by
  wlog hfp : â€–fâ€–â‚™_[2] = sqrt p with H
  Â· obtain rfl | hf := eq_or_ne f 0
    Â· simp
    specialize H hp ((sqrt p / â€–fâ€–â‚™_[2]) â€¢ f) ?_
    Â· rwa [cft_smul, support_const_smul_of_ne_zero]
      positivity
    have : 0 < â€–fâ€–â‚™_[2] := (cLpNorm_pos two_ne_zero).2 hf
    have : 0 < |âˆš p| := by positivity
    simp_rw [Function.comp_def, Pi.smul_apply, Complex.smul_re, â† Pi.smul_def] at H
    simpa [cLpNorm_const_smul, nnnorm, sqrt_nonneg, â† mul_div_right_comm, mul_comm |âˆš_|,
      div_le_iffâ‚€, mul_right_comm, *] using H
  have hpâ‚€ : p â‰  0 := by positivity
  have : (â€–re âˆ˜ fâ€–â‚™_[â†‘p] / p) ^ p â‰¤ (2 * exp 2â»Â¹) ^ p := by
    calc
      _ = ğ”¼ a, |(f a).re| ^ p / p ^ p := by
          simp [div_pow, cLpNorm_pow_eq_expect_norm hpâ‚€]; rw [expect_div]
      _ â‰¤ ğ”¼ a, |(f a).re| ^ p / p ! := by gcongr; norm_cast; exact p.factorial_le_pow
      _ â‰¤ ğ”¼ a, exp |(f a).re| := by gcongr; exact pow_div_factorial_le_exp _ (abs_nonneg _) _
      _ â‰¤ _ := rudin_exp_abs_ineq f hf
      _ â‰¤ 2 ^ p * exp (â€–fâ€–â‚™_[2] ^ 2 / 2) := by gcongr; exact le_self_powâ‚€ one_le_two hpâ‚€
      _ = (2 * exp 2â»Â¹) ^ p := by
          rw [hfp, sq_sqrt, mul_pow, â† exp_nsmul, nsmul_eq_mul, div_eq_mul_inv]; positivity
  refine le_of_pow_le_pow_leftâ‚€ hpâ‚€ (by positivity) ?_
  rwa [hfp, mul_assoc, mul_self_sqrt, mul_pow, â† div_le_iffâ‚€, â† div_pow]
  all_goals positivity

/-- **Rudin's inequality**, usual form. -/
lemma rudin_ineq (hp : 2 â‰¤ p) (f : G â†’ â„‚) (hf : AddDissociated <| support <| cft f) :
    â€–fâ€–â‚™_[p] â‰¤ 4 * exp 2â»Â¹ * sqrt p * â€–fâ€–â‚™_[2] := by
  have hpâ‚ : (1 : â„â‰¥0âˆ) â‰¤ p := by exact_mod_cast one_le_two.trans hp
  calc
    (â€–fâ€–â‚™_[p] : â„) = â€–(fun a â†¦ ((f a).re : â„‚)) + I â€¢ (fun a â†¦ ((f a).im : â„‚))â€–â‚™_[p]
      := by congr with a; simp [mul_comm I]
    _ â‰¤ â€–fun a â†¦ ((f a).re : â„‚)â€–â‚™_[p] + â€–I â€¢ (fun a â†¦ ((f a).im : â„‚))â€–â‚™_[p]
      := cLpNorm_add_le hpâ‚
    _ = â€–re âˆ˜ fâ€–â‚™_[p] + â€–re âˆ˜ ((-I) â€¢ f)â€–â‚™_[p] := by
        rw [cLpNorm_const_smul, Complex.nnnorm_I, one_mul, â† Complex.cLpNorm_coe_comp,
          â† Complex.cLpNorm_coe_comp]
        congr
        ext a : 1
        simp
    _ â‰¤ 2 * exp 2â»Â¹ * sqrt p * â€–fâ€–â‚™_[2] + 2 * exp 2â»Â¹ * sqrt p * â€–(-I) â€¢ fâ€–â‚™_[2]
      := add_le_add (rudin_ineq_aux hp _ hf) <| rudin_ineq_aux hp _ <| by
        rwa [cft_smul, support_const_smul_of_ne_zero]; simp
    _ = 4 * exp 2â»Â¹ * sqrt p * â€–fâ€–â‚™_[2] := by
        rw [cLpNorm_const_smul, nnnorm_neg, Complex.nnnorm_I, one_mul]; ring
