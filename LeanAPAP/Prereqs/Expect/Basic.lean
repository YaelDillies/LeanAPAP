import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Tactic.Positivity.Finset
import LeanAPAP.Mathlib.Algebra.Algebra.Basic
import LeanAPAP.Mathlib.Algebra.Order.Module.Defs
import LeanAPAP.Mathlib.Data.Fintype.Pi

/-!
# Average over a finset

This file defines `Finset.expect`, the average (aka expectation) of a function over a finset.

## Notation

* `ğ”¼ i âˆˆ s, f i` is notation for `Finset.expect s f`. It is the expectation of `f i` where `i`
  ranges over the finite set `s` (either a `Finset` or a `Set` with a `Fintype` instance).
* `ğ”¼ x, f x` is notation for `Finset.expect Finset.univ f`. It is the expectation of `f i` where `i`
  ranges over the finite domain of `f`.
* `ğ”¼ i âˆˆ s with p i, f i` is notation for `Finset.expect (Finset.filter p s) f`. This is referred to
  as `expectWith` in lemma names.
* `ğ”¼ (i âˆˆ s) (j âˆˆ t), f i j` is notation for `Finset.expect (s Ã—Ë¢ t) (fun âŸ¨i, jâŸ© â†¦ f i j)`.
-/

open Function
open Fintype (card)
open scoped Pointwise NNRat NNReal

variable {Î¹ Îº Î± Î² : Type*}

-- TODO: Localise
notation a " /â„š " q => (q : â„šâ‰¥0)â»Â¹ â€¢ a

/-- Average of a function over a finset. If the finset is empty, this is equal to zero. -/
def Finset.expect [AddCommMonoid Î±] [Module â„šâ‰¥0 Î±] (s : Finset Î¹) (f : Î¹ â†’ Î±) : Î± :=
  (s.card : â„šâ‰¥0)â»Â¹ â€¢ s.sum f

namespace BigOperators
open Batteries.ExtendedBinder Lean Meta

/--
* `ğ”¼ i âˆˆ s, f i` is notation for `Finset.expect s f`. It is the expectation of `f i` where `i`
  ranges over the finite set `s` (either a `Finset` or a `Set` with a `Fintype` instance).
* `ğ”¼ x, f x` is notation for `Finset.expect Finset.univ f`. It is the expectation of `f i` where `i`
  ranges over the finite domain of `f`.
* `ğ”¼ i âˆˆ s with p i, f i` is notation for `Finset.expect (Finset.filter p s) f`.
* `ğ”¼ (i âˆˆ s) (j âˆˆ t), f i j` is notation for `Finset.expect (s Ã—Ë¢ t) (fun âŸ¨i, jâŸ© â†¦ f i j)`.

These support destructuring, for example `ğ”¼ âŸ¨i, jâŸ© âˆˆ s Ã—Ë¢ t, f i j`.

Notation: `"ğ”¼" bigOpBinders* ("with" term)? "," term` -/
scoped syntax (name := bigexpect) "ğ”¼ " bigOpBinders ("with " term)? ", " term:67 : term

scoped macro_rules (kind := bigexpect)
  | `(ğ”¼ $bs:bigOpBinders $[with $p?]?, $v) => do
    let processed â† processBigOpBinders bs
    let x â† bigOpBindersPattern processed
    let s â† bigOpBindersProd processed
    match p? with
    | some p => `(Finset.expect (Finset.filter (fun $x â†¦ $p) $s) (fun $x â†¦ $v))
    | none => `(Finset.expect $s (fun $x â†¦ $v))

open Lean Meta Parser.Term PrettyPrinter.Delaborator SubExpr
open Batteries.ExtendedBinder

/-- Delaborator for `Finset.expect`. The `pp.piBinderTypes` option controls whether
to show the domain type when the expect is over `Finset.univ`. -/
@[scoped delab app.Finset.expect] def delabFinsetexpect : Delab := whenPPOption getPPNotation do
  let #[_, _, _, s, f] := (â† getExpr).getAppArgs | failure
  guard $ f.isLambda
  let ppDomain â† getPPOption getPPPiBinderTypes
  let (i, body) â† withAppArg $ withBindingBodyUnusedName fun i => do
    return (i, â† delab)
  if s.isAppOfArity ``Finset.univ 2 then
    let binder â†
      if ppDomain then
        let ty â† withNaryArg 0 delab
        `(bigOpBinder| $(.mk i):ident : $ty)
      else
        `(bigOpBinder| $(.mk i):ident)
    `(ğ”¼ $binder:bigOpBinder, $body)
  else
    let ss â† withNaryArg 3 $ delab
    `(ğ”¼ $(.mk i):ident âˆˆ $ss, $body)

end BigOperators

open scoped BigOperators

namespace Finset
section AddCommMonoid
variable [AddCommMonoid Î±] [Module â„šâ‰¥0 Î±] [AddCommMonoid Î²] [Module â„šâ‰¥0 Î²] {s : Finset Î¹}
  {f g : Î¹ â†’ Î±} {m : Î² â†’ Î±}

lemma expect_univ [Fintype Î¹] : ğ”¼ x, f x = (âˆ‘ x, f x) /â„š Fintype.card Î¹ := by
  rw [expect, card_univ]

@[simp] lemma expect_empty (f : Î¹ â†’ Î±) : expect âˆ… f = 0 := by simp [expect]
@[simp] lemma expect_singleton (f : Î¹ â†’ Î±) (i : Î¹) : expect {i} f = f i := by simp [expect]
@[simp] lemma expect_const_zero (s : Finset Î¹) : ğ”¼ _i âˆˆ s, (0 : Î±) = 0 := by simp [expect]

@[congr]
lemma expect_congr {t : Finset Î¹} (hst : s = t) (h : âˆ€ x âˆˆ t, f x = g x) :
    ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by rw [expect, expect, sum_congr hst h, hst]

lemma expectWith_congr (p : Î¹ â†’ Prop) [DecidablePred p] (h : âˆ€ x âˆˆ s, p x â†’ f x = g x) :
    ğ”¼ i âˆˆ s with p i, f i = ğ”¼ i âˆˆ s with p i, g i :=
  expect_congr rfl $ by simpa using h

lemma expect_sum_comm (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Îº â†’ Î±) :
    ğ”¼ i âˆˆ s, âˆ‘ j âˆˆ t, f i j = âˆ‘ j âˆˆ t, ğ”¼ i âˆˆ s, f i j := by
  simpa only [expect, smul_sum] using sum_comm

lemma expect_comm (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Îº â†’ Î±) :
    ğ”¼ i âˆˆ s, ğ”¼ j âˆˆ t, f i j = ğ”¼ j âˆˆ t, ğ”¼ i âˆˆ s, f i j := by
  rw [expect, expect, â†expect_sum_comm, â†expect_sum_comm, expect, expect, smul_comm, sum_comm]

lemma expect_eq_zero (h : âˆ€ i âˆˆ s, f i = 0) : ğ”¼ i âˆˆ s, f i = 0 :=
  (expect_congr rfl h).trans s.expect_const_zero

-- TODO: Golf `exists_ne_zero_of_sum_ne_zero`
lemma exists_ne_zero_of_expect_ne_zero (h : ğ”¼ i âˆˆ s, f i â‰  0) : âˆƒ i âˆˆ s, f i â‰  0 := by
  contrapose! h; exact expect_eq_zero h

lemma expect_add_distrib (s : Finset Î¹) (f g : Î¹ â†’ Î±) :
    ğ”¼ i âˆˆ s, (f i + g i) = ğ”¼ i âˆˆ s, f i + ğ”¼ i âˆˆ s, g i := by
  simp [expect, sum_add_distrib]

lemma expect_add_expect_comm (fâ‚ fâ‚‚ gâ‚ gâ‚‚ : Î¹ â†’ Î±) :
    ğ”¼ i âˆˆ s, (fâ‚ i + fâ‚‚ i) + ğ”¼ i âˆˆ s, (gâ‚ i + gâ‚‚ i) =
      ğ”¼ i âˆˆ s, (fâ‚ i + gâ‚ i) + ğ”¼ i âˆˆ s, (fâ‚‚ i + gâ‚‚ i) := by
  simp_rw [expect_add_distrib, add_add_add_comm]

lemma expect_eq_single_of_mem (i : Î¹) (hi : i âˆˆ s) (h : âˆ€ j âˆˆ s, j â‰  i â†’ f j = 0) :
    ğ”¼ i âˆˆ s, f i = f i /â„š s.card := by rw [expect, sum_eq_single_of_mem _ hi h]

/-- See also `Finset.expect_boole`. -/
lemma expect_ite_zero (s : Finset Î¹) (p : Î¹ â†’ Prop) [DecidablePred p]
    (h : âˆ€ i âˆˆ s, âˆ€ j âˆˆ s, p i â†’ p j â†’ i = j) (a : Î±) :
    ğ”¼ i âˆˆ s, ite (p i) a 0 = ite (âˆƒ i âˆˆ s, p i) (a /â„š s.card) 0 := by
  split_ifs <;> simp [expect, sum_ite_zero _ _ h, *]

section DecidableEq
variable [DecidableEq Î¹]

@[simp] lemma expect_dite_eq (i : Î¹) (f : âˆ€ j, i = j â†’ Î±) :
    ğ”¼ j âˆˆ s, (if h : i = j then f j h else 0) = if i âˆˆ s then f i rfl /â„š s.card else 0 := by
  split_ifs <;> simp [expect, *]

@[simp] lemma expect_dite_eq' (i : Î¹) (f : âˆ€ j, j = i â†’ Î±) :
    ğ”¼ j âˆˆ s, (if h : j = i then f j h else 0) = if i âˆˆ s then f i rfl /â„š s.card else 0 := by
  split_ifs <;> simp [expect, *]

@[simp] lemma expect_ite_eq (i : Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ j âˆˆ s, (if i = j then f j else 0) = if i âˆˆ s then f i /â„š s.card else 0 := by
  split_ifs <;> simp [expect, *]

@[simp] lemma expect_ite_eq' (i : Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ j âˆˆ s, (if j = i then f j else 0) = if i âˆˆ s then f i /â„š s.card else 0 := by
  split_ifs <;> simp [expect, *]

end DecidableEq

section bij
variable {t : Finset Îº} {g : Îº â†’ Î±}

-- TODO: Backport arguments changes to `card_congr` and `prod_bij`
lemma expect_bij (i : âˆ€ a âˆˆ s, Îº) (hi : âˆ€ a ha, i a ha âˆˆ t) (h : âˆ€ a ha, f a = g (i a ha))
    (i_inj : âˆ€ aâ‚ haâ‚ aâ‚‚ haâ‚‚, i aâ‚ haâ‚ = i aâ‚‚ haâ‚‚ â†’ aâ‚ = aâ‚‚)
    (i_surj : âˆ€ b âˆˆ t, âˆƒ a ha, i a ha = b) : ğ”¼ x âˆˆ s, f x = ğ”¼ x âˆˆ t, g x := by
  rw [expect, expect, card_congr i hi (fun _ _ _ â†¦ i_inj _ _ _) (by simpa using i_surj),
    sum_bij i hi (fun _ _ _ â†¦ i_inj _ _ _) (by simpa using i_surj) h]

lemma expect_nbij (i : Î¹ â†’ Îº) (hi : âˆ€ a âˆˆ s, i a âˆˆ t) (h : âˆ€ a âˆˆ s, f a = g (i a))
    (i_inj : (s : Set Î¹).InjOn i) (i_surj : (s : Set Î¹).SurjOn i t) :
    ğ”¼ x âˆˆ s, f x = ğ”¼ x âˆˆ t, g x :=
  expect_bij (fun a _ â†¦ i a) hi h i_inj $ by simpa [Set.SurjOn, Set.subset_def] using i_surj

lemma expect_bij' (i : âˆ€ a âˆˆ s, Îº) (j : âˆ€ a âˆˆ t, Î¹) (hi : âˆ€ a ha, i a ha âˆˆ t)
    (hj : âˆ€ a ha, j a ha âˆˆ s) (left_inv : âˆ€ a ha, j (i a ha) (hi a ha) = a)
    (right_inv : âˆ€ a ha, i (j a ha) (hj a ha) = a) (h : âˆ€ a ha, f a = g (i a ha)) :
    ğ”¼ x âˆˆ s, f x = ğ”¼ x âˆˆ t, g x := by
  rw [expect, expect, sum_bij' i j hi hj left_inv right_inv h, card_congr i hi]
  Â· intro a b ha hb z
    rw [â†left_inv a ha, â†left_inv b hb]
    congr 1
  Â· exact fun b hb â†¦ âŸ¨j b hb, hj _ _, right_inv _ _âŸ©

lemma expect_nbij' (i : Î¹ â†’ Îº) (j : Îº â†’ Î¹) (hi : âˆ€ a âˆˆ s, i a âˆˆ t) (hj : âˆ€ a âˆˆ t, j a âˆˆ s)
    (left_inv : âˆ€ a âˆˆ s, j (i a) = a) (right_inv : âˆ€ a âˆˆ t, i (j a) = a)
    (h : âˆ€ a âˆˆ s, f a = g (i a)) : ğ”¼ x âˆˆ s, f x = ğ”¼ x âˆˆ t, g x :=
  expect_bij' (fun a _ â†¦ i a) (fun b _ â†¦ j b) hi hj left_inv right_inv h

/-- `Finset.expect_equiv` is a specialization of `Finset.expect_bij` that automatically fills in
most arguments. -/
lemma expect_equiv (e : Î¹ â‰ƒ Îº) (hst : âˆ€ i, i âˆˆ s â†” e i âˆˆ t) (hfg : âˆ€ i âˆˆ s, f i = g (e i)) :
    ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i :=
  expect_nbij e (fun i â†¦ (hst _).1) hfg (e.injective.injOn _) fun i hi â†¦ âŸ¨e.symm i, by simpa [hst]âŸ©

lemma expect_product' (f : Î¹ â†’ Îº â†’ Î±) : ğ”¼ x âˆˆ s Ã—Ë¢ t, f x.1 x.2 = ğ”¼ x âˆˆ s, ğ”¼ y âˆˆ t, f x y := by
  simp only [expect, card_product, sum_product', smul_sum, mul_inv, mul_smul, Nat.cast_mul]

-- TODO: Change assumption of `prod_image` to `Set.InjOn`
@[simp]
lemma expect_image [DecidableEq Î¹] {m : Îº â†’ Î¹} (hm : (t : Set Îº).InjOn m) :
    ğ”¼ i âˆˆ t.image m, f i = ğ”¼ i âˆˆ t, f (m i) := by
  simp_rw [expect, card_image_of_injOn hm, sum_image hm]

end bij

@[simp] lemma expect_inv_index [DecidableEq Î¹] [InvolutiveInv Î¹] (s : Finset Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ i âˆˆ sâ»Â¹, f i = ğ”¼ i âˆˆ s, f iâ»Â¹ := expect_image $ inv_injective.injOn _

@[simp] lemma expect_neg_index [DecidableEq Î¹] [InvolutiveNeg Î¹] (s : Finset Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ i âˆˆ -s, f i = ğ”¼ i âˆˆ s, f (-i) := expect_image $ neg_injective.injOn _

lemma _root_.map_expect {F : Type*} [FunLike F Î± Î²] [LinearMapClass F â„šâ‰¥0 Î± Î²]
    (g : F) (f : Î¹ â†’ Î±) (s : Finset Î¹) :
    g (ğ”¼ x âˆˆ s, f x) = ğ”¼ x âˆˆ s, g (f x) := by simp only [expect, map_smul, map_natCast, map_sum]

@[simp]
lemma card_smul_expect (s : Finset Î¹) (f : Î¹ â†’ Î±) : s.card â€¢ ğ”¼ i âˆˆ s, f i = âˆ‘ i âˆˆ s, f i := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· simp
  Â· rw [expect, nsmul_eq_smul_cast â„šâ‰¥0, smul_inv_smulâ‚€]
    positivity

@[simp] nonrec lemma _root_.Fintype.card_smul_expect [Fintype Î¹] (f : Î¹ â†’ Î±) :
    Fintype.card Î¹ â€¢ ğ”¼ i, f i = âˆ‘ i, f i := card_smul_expect _ _

@[simp] lemma expect_const (hs : s.Nonempty) (a : Î±) : ğ”¼ _i âˆˆ s, a = a := by
  rw [expect, sum_const, nsmul_eq_smul_cast â„šâ‰¥0, inv_smul_smulâ‚€]; positivity

lemma smul_expect {G : Type*} [DistribSMul G Î±] [SMulCommClass G â„šâ‰¥0 Î±] (a : G)
    (s : Finset Î¹) (f : Î¹ â†’ Î±) : a â€¢ ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ s, a â€¢ f i := by
  simp only [expect, smul_sum, smul_comm]

end AddCommMonoid

section AddCommGroup
variable [AddCommGroup Î±] [Module â„šâ‰¥0 Î±] [Field Î²] [Module â„šâ‰¥0 Î²] {s : Finset Î¹}

lemma expect_sub_distrib (s : Finset Î¹) (f g : Î¹ â†’ Î±) :
    ğ”¼ i âˆˆ s, (f i - g i) = ğ”¼ i âˆˆ s, f i - ğ”¼ i âˆˆ s, g i := by
  simp only [expect, sum_sub_distrib, smul_sub]

@[simp]
lemma expect_neg_distrib (s : Finset Î¹) (f : Î¹ â†’ Î±) : ğ”¼ i âˆˆ s, -f i = -ğ”¼ i âˆˆ s, f i := by
  simp [expect]

variable [Fintype Î¹]

def balance (f : Î¹ â†’ Î±) : Î¹ â†’ Î± := f - Function.const _ (ğ”¼ y, f y)

lemma balance_apply (f : Î¹ â†’ Î±) (x : Î¹) : balance f x = f x - ğ”¼ y, f y := rfl

@[simp] lemma balance_zero : balance (0 : Î¹ â†’ Î±) = 0 := by simp [balance]

@[simp] lemma balance_add (f g : Î¹ â†’ Î±) : balance (f + g) = balance f + balance g := by
  simp only [balance, expect_add_distrib, â† const_add, add_sub_add_comm, Pi.add_apply]

@[simp] lemma balance_sub (f g : Î¹ â†’ Î±) : balance (f - g) = balance f - balance g := by
  simp only [balance, expect_sub_distrib, const_sub, sub_sub_sub_comm, Pi.sub_apply]

@[simp] lemma balance_neg (f : Î¹ â†’ Î±) : balance (-f) = -balance f := by
  simp only [balance, expect_neg_distrib, const_neg, neg_sub', Pi.neg_apply]

@[simp] lemma sum_balance (f : Î¹ â†’ Î±) : âˆ‘ x, balance f x = 0 := by
  cases isEmpty_or_nonempty Î¹ <;> simp [balance_apply]

@[simp] lemma expect_balance (f : Î¹ â†’ Î±) : ğ”¼ x, balance f x = 0 := by simp [expect]

@[simp] lemma balance_idem (f : Î¹ â†’ Î±) : balance (balance f) = balance f := by
  cases isEmpty_or_nonempty Î¹ <;> ext x <;> simp [balance, expect_sub_distrib, univ_nonempty]

@[simp] lemma map_balance {F : Type*} [FunLike F Î± Î²] [LinearMapClass F â„šâ‰¥0 Î± Î²] (g : F) (f : Î¹ â†’ Î±)
    (a : Î¹) : g (balance f a) = balance (g âˆ˜ f) a := by simp [balance, map_expect]

end AddCommGroup

section Semiring
variable [Semiring Î±] [Module â„šâ‰¥0 Î±] {s : Finset Î¹} {f g : Î¹ â†’ Î±} {m : Î² â†’ Î±}

@[simp] lemma card_mul_expect (s : Finset Î¹) (f : Î¹ â†’ Î±) :
    s.card * ğ”¼ i âˆˆ s, f i = âˆ‘ i âˆˆ s, f i := by rw [â†nsmul_eq_mul, card_smul_expect]

@[simp] nonrec lemma _root_.Fintype.card_mul_expect [Fintype Î¹] (f : Î¹ â†’ Î±) :
    Fintype.card Î¹ * ğ”¼ i, f i = âˆ‘ i, f i := card_mul_expect _ _

lemma expect_mul [IsScalarTower â„šâ‰¥0 Î± Î±] (s : Finset Î¹) (f : Î¹ â†’ Î±) (a : Î±) :
    (ğ”¼ i âˆˆ s, f i) * a = ğ”¼ i âˆˆ s, f i * a := by rw [expect, expect, smul_mul_assoc, sum_mul]

lemma mul_expect [SMulCommClass â„šâ‰¥0 Î± Î±] (s : Finset Î¹) (f : Î¹ â†’ Î±) (a : Î±) :
    a * ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ s, a * f i := by rw [expect, expect, mul_smul_comm, mul_sum]

-- TODO: Change `sum_mul_sum` to match?
lemma expect_mul_expect [IsScalarTower â„šâ‰¥0 Î± Î±] [SMulCommClass â„šâ‰¥0 Î± Î±] (s : Finset Î¹)
    (t : Finset Îº) (f : Î¹ â†’ Î±) (g : Îº â†’ Î±) :
    (ğ”¼ i âˆˆ s, f i) * ğ”¼ j âˆˆ t, g j = ğ”¼ i âˆˆ s, ğ”¼ j âˆˆ t, f i * g j := by
  simp_rw [expect_mul, mul_expect]

end Semiring

section CommSemiring
variable [CommSemiring Î±] [Module â„šâ‰¥0 Î±] [IsScalarTower â„šâ‰¥0 Î± Î±] [SMulCommClass â„šâ‰¥0 Î± Î±]

lemma expect_pow (s : Finset Î¹) (f : Î¹ â†’ Î±) (n : â„•) :
    (ğ”¼ i âˆˆ s, f i) ^ n = ğ”¼ p âˆˆ s ^^ n, âˆ i, f (p i) := by
  classical
  rw [expect, smul_pow, sum_pow', expect, Fintype.card_piFinsetConst, inv_pow, Nat.cast_pow]

end CommSemiring

section Semifield
variable [Semifield Î±] [CharZero Î±] [SMul â„šâ‰¥0 Î±] {s : Finset Î¹} {f g : Î¹ â†’ Î±}
  {m : Î² â†’ Î±}

lemma expect_indicate_eq [Fintype Î¹] [Nonempty Î¹] [DecidableEq Î¹] (f : Î¹ â†’ Î±) (x : Î¹) :
    ğ”¼ i, ite (x = i) (Fintype.card Î¹ : Î±) 0 * f i = f x := by
  simp_rw [expect_univ, ite_mul, zero_mul, sum_ite_eq, if_pos (mem_univ _)]
  rw [â†@NNRat.cast_natCast Î±, â†NNRat.smul_def, inv_smul_smulâ‚€]
  simp [Fintype.card_ne_zero]

lemma expect_indicate_eq' [Fintype Î¹] [Nonempty Î¹] [DecidableEq Î¹] (f : Î¹ â†’ Î±) (x : Î¹) :
    ğ”¼ i, ite (i = x) (Fintype.card Î¹ : Î±) 0 * f i = f x := by
  simp_rw [@eq_comm _ _ x, expect_indicate_eq]

lemma expect_eq_sum_div_card (s : Finset Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ i âˆˆ s, f i = (âˆ‘ i âˆˆ s, f i) / s.card := by
  rw [expect, NNRat.smul_def, div_eq_inv_mul, NNRat.cast_inv, NNRat.cast_natCast]

nonrec lemma _root_.Fintype.expect_eq_sum_div_card [Fintype Î¹] (f : Î¹ â†’ Î±) :
    ğ”¼ i, f i = (âˆ‘ i, f i) / Fintype.card Î¹ := Finset.expect_eq_sum_div_card _ _

lemma expect_div (s : Finset Î¹) (f : Î¹ â†’ Î±) (a : Î±) : (ğ”¼ i âˆˆ s, f i) / a = ğ”¼ i âˆˆ s, f i / a := by
  simp_rw [div_eq_mul_inv, expect_mul]

end Semifield

/-! ### Order -/

section OrderedAddCommMonoid
variable [OrderedAddCommMonoid Î±] [Module â„šâ‰¥0 Î±] [OrderedAddCommMonoid Î²] [Module â„šâ‰¥0 Î²]
  {s : Finset Î¹} {f g : Î¹ â†’ Î±}

lemma expect_eq_zero_iff_of_nonneg (hs : s.Nonempty) (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) :
    ğ”¼ i âˆˆ s, f i = 0 â†” âˆ€ i âˆˆ s, f i = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonneg hf, hs.ne_empty]

lemma expect_eq_zero_iff_of_nonpos (hs : s.Nonempty) (hf : âˆ€ i âˆˆ s, f i â‰¤ 0) :
    ğ”¼ i âˆˆ s, f i = 0 â†” âˆ€ i âˆˆ s, f i = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonpos hf, hs.ne_empty]

section PosSMulMono
variable [PosSMulMono â„šâ‰¥0 Î±] [PosSMulMono â„šâ‰¥0 Î²]

lemma expect_le_expect (hfg : âˆ€ i âˆˆ s, f i â‰¤ g i) : ğ”¼ i âˆˆ s, f i â‰¤ ğ”¼ i âˆˆ s, g i :=
  smul_le_smul_of_nonneg_left (sum_le_sum hfg) $ by positivity

/-- This is a (beta-reduced) version of the standard lemma `Finset.expect_le_expect`,
convenient for the `gcongr` tactic. -/
@[gcongr]
lemma _root_.GCongr.expect_le_expect (h : âˆ€ i âˆˆ s, f i â‰¤ g i) : s.expect f â‰¤ s.expect g :=
  Finset.expect_le_expect h

lemma expect_le (hs : s.Nonempty) (f : Î¹ â†’ Î±) (a : Î±) (h : âˆ€ x âˆˆ s, f x â‰¤ a) : ğ”¼ i âˆˆ s, f i â‰¤ a :=
  (inv_smul_le_iff_of_pos $ by positivity).2 $ by
    rw [â†nsmul_eq_smul_cast]; exact sum_le_card_nsmul _ _ _ h

lemma le_expect (hs : s.Nonempty) (f : Î¹ â†’ Î±) (a : Î±) (h : âˆ€ x âˆˆ s, a â‰¤ f x) : a â‰¤ ğ”¼ i âˆˆ s, f i :=
  (le_inv_smul_iff_of_pos $ by positivity).2 $ by
    rw [â†nsmul_eq_smul_cast]; exact card_nsmul_le_sum _ _ _ h

lemma expect_nonneg (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) : 0 â‰¤ ğ”¼ i âˆˆ s, f i :=
  smul_nonneg (by positivity) $ sum_nonneg hf

-- TODO: Contribute back better docstring to `le_prod_of_submultiplicative`
/-- If `m` is a subadditive function (`m (x + y) â‰¤ m x + m y`, `f 1 = 1`), and `f i`,
`i âˆˆ s`, is a finite family of elements, then `m (ğ”¼ i in s, f i) â‰¤ ğ”¼ i in s, m (f i)`. -/
lemma le_expect_of_subadditive (m : Î± â†’ Î²) (h_zero : m 0 = 0)
    (h_add : âˆ€ a b, m (a + b) â‰¤ m a + m b) (h_div : âˆ€ a (n : â„•), m (a /â„š n) = m a /â„š n)
    (s : Finset Î¹) (f : Î¹ â†’ Î±) : m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i) := by
  simp only [expect, h_div]
  exact smul_le_smul_of_nonneg_left (le_sum_of_subadditive _ h_zero h_add _ _) $ by positivity

end PosSMulMono
end OrderedAddCommMonoid

section OrderedCancelAddCommMonoid
variable [OrderedCancelAddCommMonoid Î±] [Module â„šâ‰¥0 Î±] {s : Finset Î¹} {f g : Î¹ â†’ Î±}
section PosSMulStrictMono
variable [PosSMulStrictMono â„šâ‰¥0 Î±]

lemma expect_pos (hf : âˆ€ i âˆˆ s, 0 < f i) (hs : s.Nonempty) : 0 < ğ”¼ i âˆˆ s, f i :=
  smul_pos (by positivity) $ sum_pos hf hs

end PosSMulStrictMono
end OrderedCancelAddCommMonoid

section LinearOrderedAddCommGroup
variable [LinearOrderedAddCommGroup Î±] [Module â„šâ‰¥0 Î±] [PosSMulMono â„šâ‰¥0 Î±]

lemma abs_expect_le_expect_abs (s : Finset Î¹) (f : Î¹ â†’ Î±) : |ğ”¼ i âˆˆ s, f i| â‰¤ ğ”¼ i âˆˆ s, |f i| :=
  le_expect_of_subadditive _ abs_zero abs_add (fun _ _ â†¦ abs_nnqsmul _ _) _ _

end LinearOrderedAddCommGroup
end Finset

namespace algebraMap
variable [Semifield Î±] [CharZero Î±] [SMul â„šâ‰¥0 Î±] [Semifield Î²] [CharZero Î²]
  [SMul â„šâ‰¥0 Î²] [Algebra Î± Î²]

@[simp, norm_cast]
lemma coe_expect (s : Finset Î¹) (f : Î¹ â†’ Î±) : ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ s, (f i : Î²) :=
  map_expect (algebraMap _ _) _ _

end algebraMap

open Finset

namespace Fintype
variable [Fintype Î¹] [Fintype Îº]

section AddCommMonoid
variable [AddCommMonoid Î±] [Module â„šâ‰¥0 Î±] {f : Î¹ â†’ Î±}

/-- `Fintype.expect_bijective` is a variant of `Finset.expect_bij` that accepts
`Function.Bijective`.

See `Function.Bijective.expect_comp` for a version without `h`. -/
lemma expect_bijective (e : Î¹ â†’ Îº) (he : Bijective e) (f : Î¹ â†’ Î±) (g : Îº â†’ Î±)
    (h : âˆ€ x, f x = g (e x)) : ğ”¼ i, f i = ğ”¼ i, g i :=
  expect_nbij (fun _ â†¦ e _) (fun _ _ â†¦ mem_univ _) (fun x _ â†¦ h x) (he.injective.injOn _) $ by
    simpa using he.surjective.surjOn _

/-- `Fintype.expect_equiv` is a specialization of `Finset.expect_bij` that automatically fills in
most arguments.

See `Equiv.expect_comp` for a version without `h`. -/
lemma expect_equiv (e : Î¹ â‰ƒ Îº) (f : Î¹ â†’ Î±) (g : Îº â†’ Î±) (h : âˆ€ x, f x = g (e x)) :
    ğ”¼ i, f i = ğ”¼ i, g i :=
  expect_bijective _ e.bijective f g h

@[simp] lemma expect_const [Nonempty Î¹] (a : Î±) : ğ”¼ _i : Î¹, a = a :=
  Finset.expect_const univ_nonempty _

lemma expect_ite_zero (p : Î¹ â†’ Prop) [DecidablePred p] (h : âˆ€ i j, p i â†’ p j â†’ i = j) (a : Î±) :
    ğ”¼ i, ite (p i) a 0 = ite (âˆƒ i, p i) (a /â„š Fintype.card Î¹) 0 := by
  simp [univ.expect_ite_zero p (by simpa using h), card_univ]

variable [DecidableEq Î¹]

@[simp] lemma expect_dite_eq (i : Î¹) (f : âˆ€ j, i = j â†’ Î±) :
    ğ”¼ j, (if h : i = j then f j h else 0) = f i rfl /â„š card Î¹ := by simp [card_univ]

@[simp] lemma expect_dite_eq' (i : Î¹) (f : âˆ€ j, j = i â†’ Î±) :
    ğ”¼ j, (if h : j = i then f j h else 0) = f i rfl /â„š card Î¹ := by simp [card_univ]

@[simp] lemma expect_ite_eq (i : Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ j, (if i = j then f j else 0) = f i /â„š card Î¹ := by simp [card_univ]

@[simp] lemma expect_ite_eq' (i : Î¹) (f : Î¹ â†’ Î±) :
    ğ”¼ j, (if j = i then f j else 0) = f i /â„š card Î¹ := by simp [card_univ]

end AddCommMonoid

section Semiring
variable [Semiring Î±] [Module â„šâ‰¥0 Î±]

@[simp] lemma expect_one [Nonempty Î¹] : ğ”¼ _i : Î¹, (1 : Î±) = 1 := expect_const _

end Semiring

section OrderedAddCommMonoid
variable [OrderedAddCommMonoid Î±] [Module â„šâ‰¥0 Î±] {f : Î¹ â†’ Î±}

lemma expect_eq_zero_iff_of_nonneg [Nonempty Î¹] (hf : 0 â‰¤ f) : ğ”¼ i, f i = 0 â†” f = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonneg hf, univ_nonempty.ne_empty]

lemma expect_eq_zero_iff_of_nonpos [Nonempty Î¹] (hf : f â‰¤ 0) : ğ”¼ i, f i = 0 â†” f = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonpos hf, univ_nonempty.ne_empty]

end OrderedAddCommMonoid
end Fintype

namespace RCLike
variable [RCLike Î±] [Fintype Î¹] (f : Î¹ â†’ â„) (a : Î¹)

@[simp, norm_cast]
lemma coe_balance : (â†‘(balance f a) : Î±) = balance ((â†‘) âˆ˜ f) a := map_balance (algebraMap â„ Î±) _ _

@[simp] lemma coe_comp_balance : ((â†‘) : â„ â†’ Î±) âˆ˜ balance f = balance ((â†‘) âˆ˜ f) :=
  funext $ coe_balance _

end RCLike

open Finset

instance [Preorder Î±] [MulAction â„š Î±] [PosSMulMono â„š Î±] : PosSMulMono â„šâ‰¥0 Î± where
  elim a _ _bâ‚ _bâ‚‚ hb := (smul_le_smul_of_nonneg_left hb a.2 :)

instance LinearOrderedSemifield.toPosSMulStrictMono [LinearOrderedSemifield Î±] :
    PosSMulStrictMono â„šâ‰¥0 Î± where
  elim a ha bâ‚ bâ‚‚ hb := by
    simp_rw [NNRat.smul_def]; exact mul_lt_mul_of_pos_left hb (NNRat.cast_pos.2 ha)

namespace Mathlib.Meta.Positivity
open Qq Lean Meta

@[positivity Finset.expect _ _]
def evalFinsetExpect : PositivityExt where eval {u Î±} zÎ± pÎ± e := do
  match e with
  | ~q(@Finset.expect $Î¹ _ $instÎ± $instmod $s $f) =>
    let (lhs, _, (rhs : Q($Î±))) â† lambdaMetaTelescope f
    let so : Option Q(Finset.Nonempty $s) â† do -- TODO: It doesn't complain if we make a typo?
      try
        let _fi â† synthInstanceQ q(Fintype $Î¹)
        let _no â† synthInstanceQ q(Nonempty $Î¹)
        match s with
        | ~q(@univ _ $fi) => pure (some q(Finset.univ_nonempty (Î± := $Î¹)))
        | _ => pure none
      catch _ => do
        let .some fv â† findLocalDeclWithType? q(Finset.Nonempty $s) | pure none
        pure (some (.fvar fv))
    match â† core zÎ± pÎ± rhs, so with
    | .nonnegative pb, _ => do
      let instÎ±ordmon â† synthInstanceQ q(OrderedAddCommMonoid $Î±)
      let instÎ±ordsmul â† synthInstanceQ q(PosSMulMono â„šâ‰¥0 $Î±)
      assumeInstancesCommute
      let pr : Q(âˆ€ i, 0 â‰¤ $f i) â† mkLambdaFVars lhs pb
      return .nonnegative q(@expect_nonneg $Î¹ $Î± $instÎ±ordmon $instmod $s $f $instÎ±ordsmul
        fun i _ â†¦ $pr i)
    | .positive pb, .some (fi : Q(Finset.Nonempty $s)) => do
      let instÎ±ordmon â† synthInstanceQ q(OrderedCancelAddCommMonoid $Î±)
      let instÎ±ordsmul â† synthInstanceQ q(PosSMulStrictMono â„šâ‰¥0 $Î±)
      assumeInstancesCommute
      let pr : Q(âˆ€ i, 0 < $f i) â† mkLambdaFVars lhs pb
      return .positive q(@expect_pos $Î¹ $Î± $instÎ±ordmon $instmod $s $f $instÎ±ordsmul
        (fun i _ â†¦ $pr i) $fi)
    | _, _ => pure .none
  | _ => throwError "not Finset.expect"

example (n : â„•) (a : â„• â†’ â„) : 0 â‰¤ ğ”¼ j âˆˆ range n, a j^2 := by positivity
example (a : ULift.{2} â„• â†’ â„) (s : Finset (ULift.{2} â„•)) : 0 â‰¤ ğ”¼ j âˆˆ s, a j^2 := by positivity
example (n : â„•) (a : â„• â†’ â„) : 0 â‰¤ ğ”¼ j : Fin 8, ğ”¼ i âˆˆ range n, (a j^2 + i ^ 2) := by positivity
example (n : â„•) (a : â„• â†’ â„) : 0 < ğ”¼ j : Fin (n + 1), (a j^2 + 1) := by positivity
example (a : â„• â†’ â„) : 0 < ğ”¼ j âˆˆ ({1} : Finset â„•), (a j^2 + 1) := by
  have : Finset.Nonempty {1} := singleton_nonempty 1
  positivity

end Mathlib.Meta.Positivity
