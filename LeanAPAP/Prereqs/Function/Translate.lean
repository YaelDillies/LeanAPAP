import Mathlib.Algebra.BigOperators.Pi
import Mathlib.Algebra.Order.Pi
import Mathlib.Algebra.Star.Order
import Mathlib.Data.ZMod.Basic

/-!
# Precomposition operators
-/

open Function Set
open scoped ComplexConjugate Pointwise

/-! ### Translation operator -/

section translate
variable {Î¹ Î± Î² Î³ : Type*} [AddCommGroup Î±]

def translate (a : Î±) (f : Î± â†’ Î²) : Î± â†’ Î² := fun x â†¦ f (x - a)

notation "Ï„ " => translate

@[simp] lemma translate_apply (a : Î±) (f : Î± â†’ Î²) (x : Î±) : Ï„ a f x = f (x - a) := rfl
@[simp] lemma translate_zero (f : Î± â†’ Î²) : Ï„ 0 f = f := by ext; simp

lemma translate_translate (a b : Î±) (f : Î± â†’ Î²) : Ï„ a (Ï„ b f) = Ï„ (a + b) f := by
  ext; simp [sub_sub]

lemma translate_add (a b : Î±) (f : Î± â†’ Î²) : Ï„ (a + b) f = Ï„ a (Ï„ b f) := by ext; simp [sub_sub]

lemma translate_add' (a b : Î±) (f : Î± â†’ Î²) : Ï„ (a + b) f = Ï„ b (Ï„ a f) := by
  rw [add_comm, translate_add]

lemma translate_comm (a b : Î±) (f : Î± â†’ Î²) : Ï„ a (Ï„ b f) = Ï„ b (Ï„ a f) := by
  rw [â† translate_add, translate_add']

@[simp] lemma comp_translate (a : Î±) (f : Î± â†’ Î²) (g : Î² â†’ Î³) : g âˆ˜ Ï„ a f = Ï„ a (g âˆ˜ f) := rfl

@[simp]
lemma translate_smul_right [SMul Î³ Î²] (a : Î±) (f : Î± â†’ Î²) (c : Î³) : Ï„ a (c â€¢ f) = c â€¢ Ï„ a f := rfl

section AddCommGroup
variable [AddCommGroup Î²]

@[simp] lemma translate_zero_right (a : Î±) : Ï„ a (0 : Î± â†’ Î²) = 0 := rfl

lemma translate_add_right (a : Î±) (f g : Î± â†’ Î²) : Ï„ a (f + g) = Ï„ a f + Ï„ a g := rfl
lemma translate_sub_right (a : Î±) (f g : Î± â†’ Î²) : Ï„ a (f - g) = Ï„ a f - Ï„ a g := rfl
lemma translate_neg_right (a : Î±) (f : Î± â†’ Î²) : Ï„ a (-f) = -Ï„ a f := rfl

lemma translate_sum_right (a : Î±) (f : Î¹ â†’ Î± â†’ Î²) (s : Finset Î¹) :
    Ï„ a (âˆ‘ i in s, f i) = âˆ‘ i in s, Ï„ a (f i) := by ext; simp

lemma sum_translate [Fintype Î±] (a : Î±) (f : Î± â†’ Î²) : âˆ‘ b, Ï„ a f b = âˆ‘ b, f b :=
  Fintype.sum_equiv (Equiv.subRight _) _ _ fun _ â†¦ rfl

@[simp] lemma support_translate (a : Î±) (f : Î± â†’ Î²) : support (Ï„ a f) = a +áµ¥ support f := by
  ext; simp [mem_vadd_set_iff_neg_vadd_mem, sub_eq_neg_add]

end AddCommGroup

variable [CommRing Î²]

lemma translate_prod_right (a : Î±) (f : Î¹ â†’ Î± â†’ Î²) (s : Finset Î¹) :
    Ï„ a (âˆ i in s, f i) = âˆ i in s, Ï„ a (f i) := by ext; simp

end translate

/-! ### Conjugation negation operator -/

section conjneg
variable {Î¹ Î± Î² Î³ : Type*} [AddGroup Î±]

section CommSemiring
variable [CommSemiring Î²] [StarRing Î²] {f g : Î± â†’ Î²}

def conjneg (f : Î± â†’ Î²) : Î± â†’ Î² := conj fun x â†¦ f (-x)

@[simp] lemma conjneg_apply (f : Î± â†’ Î²) (x : Î±) : conjneg f x = conj (f (-x)) := rfl
@[simp] lemma conjneg_conjneg (f : Î± â†’ Î²) : conjneg (conjneg f) = f := by ext; simp

lemma conjneg_injective : Injective (conjneg : (Î± â†’ Î²) â†’ Î± â†’ Î²) :=
  Involutive.injective conjneg_conjneg

@[simp] lemma conjneg_inj : conjneg f = conjneg g â†” f = g := conjneg_injective.eq_iff
lemma conjneg_ne_conjneg : conjneg f â‰  conjneg g â†” f â‰  g := conjneg_injective.ne_iff

@[simp] lemma conjneg_conj (f : Î± â†’ Î²) : conjneg (conj f) = conj (conjneg f) := rfl

@[simp] lemma conjneg_zero : conjneg (0 : Î± â†’ Î²) = 0 := by ext; simp
@[simp] lemma conjneg_one : conjneg (1 : Î± â†’ Î²) = 1 := by ext; simp
@[simp] lemma conjneg_add (f g : Î± â†’ Î²) : conjneg (f + g) = conjneg f + conjneg g := by ext; simp

@[simp] lemma conjneg_sum (s : Finset Î¹) (f : Î¹ â†’ Î± â†’ Î²) :
    conjneg (âˆ‘ i in s, f i) = âˆ‘ i in s, conjneg (f i) := by
  ext; simp only [map_sum, conjneg_apply, Finset.sum_apply]

@[simp] lemma conjneg_prod (s : Finset Î¹) (f : Î¹ â†’ Î± â†’ Î²) :
    conjneg (âˆ i in s, f i) = âˆ i in s, conjneg (f i) := by
  ext; simp only [map_prod, conjneg_apply, Finset.prod_apply]

@[simp] lemma conjneg_eq_zero : conjneg f = 0 â†” f = 0 := by
  rw [â† conjneg_inj, conjneg_conjneg, conjneg_zero]

@[simp]
lemma conjneg_eq_one : conjneg f = 1 â†” f = 1 := by rw [â† conjneg_inj, conjneg_conjneg, conjneg_one]

lemma conjneg_ne_zero : conjneg f â‰  0 â†” f â‰  0 := conjneg_eq_zero.not
lemma conjneg_ne_one : conjneg f â‰  1 â†” f â‰  1 := conjneg_eq_one.not

lemma sum_conjneg [Fintype Î±] (f : Î± â†’ Î²) : âˆ‘ a, conjneg f a = âˆ‘ a, conj (f a) :=
  Fintype.sum_equiv (Equiv.neg _) _ _ fun _ â†¦ rfl

@[simp] lemma support_conjneg (f : Î± â†’ Î²) : support (conjneg f) = -support f := by
  ext; simp [starRingEnd_apply]

end CommSemiring

section CommRing
variable [CommRing Î²] [StarRing Î²]

@[simp] lemma conjneg_sub (f g : Î± â†’ Î²) : conjneg (f - g) = conjneg f - conjneg g := by ext; simp
@[simp] lemma conjneg_neg (f : Î± â†’ Î²) : conjneg (-f) = -conjneg f := by ext; simp

end CommRing

section OrderedCommSemiring
variable [OrderedCommSemiring Î²] [StarRing Î²] [StarOrderedRing Î²] {f : Î± â†’ Î²}

@[simp] lemma conjneg_nonneg : 0 â‰¤ conjneg f â†” 0 â‰¤ f :=
  (Equiv.neg _).forall_congr' $ by simp [starRingEnd_apply]

@[simp] lemma conjneg_pos : 0 < conjneg f â†” 0 < f := by
  simp_rw [lt_iff_le_and_ne, @ne_comm (Î± â†’ Î²) 0, conjneg_nonneg, conjneg_ne_zero]

end OrderedCommSemiring

section OrderedCommRing
variable [OrderedCommRing Î²] [StarRing Î²] [StarOrderedRing Î²] {f : Î± â†’ Î²}

@[simp] lemma conjneg_nonpos : conjneg f â‰¤ 0 â†” f â‰¤ 0 := by
  simp_rw [â† neg_nonneg, â† conjneg_neg, conjneg_nonneg]

@[simp]
lemma conjneg_neg' : conjneg f < 0 â†” f < 0 := by simp_rw [â† neg_pos, â† conjneg_neg, conjneg_pos]

end OrderedCommRing

end conjneg

open Fintype

variable {Î± Î² G ğ•œ : Type*} [AddCommGroup G]

noncomputable def dilate (f : G â†’ ğ•œ) (n : â„•) : G â†’ ğ•œ :=
  fun a â†¦ f ((nâ»Â¹ : ZMod (Nat.card G)).val â€¢ a)

variable [Star ğ•œ] {f : G â†’ ğ•œ}

protected lemma IsSelfAdjoint.dilate (hf : IsSelfAdjoint f) (n : â„•) : IsSelfAdjoint (dilate f n) :=
  Pi.isSelfAdjoint.2 fun _g â†¦ hf.apply _
