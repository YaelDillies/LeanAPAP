import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.MeanInequalities
import LeanAPAP.Prereqs.Curlog
import LeanAPAP.Prereqs.Energy
import LeanAPAP.Prereqs.LargeSpec
import LeanAPAP.Prereqs.Rudin

/-!
# Chang's lemma
-/

open Finset Fintype Function Real
open scoped ComplexConjugate ComplexOrder NNReal

  variable {G : Type*} [AddCommGroup G] [Fintype G] {f : G â†’ â„‚} {Î· : â„} {Ïˆ : AddChar G â„‚}
  {Î” : Finset (AddChar G â„‚)} {m : â„•}

local notation:70 s:70 " ^^ " n:71 => Fintype.piFinset fun _ : Fin n â†¦ s

private noncomputable def Î± (f : G â†’ â„‚) := â€–fâ€–_[1] ^ 2 / â€–fâ€–_[2] ^ 2 / card G

lemma Î±_nonneg (f : G â†’ â„‚) : 0 â‰¤ Î± f := by unfold Î±; positivity
lemma Î±_pos (hf : f â‰  0) : 0 < Î± f := by unfold Î±; positivity

lemma Î±_le_one (f : G â†’ â„‚) : Î± f â‰¤ 1 := by
  refine div_le_one_of_le (div_le_of_nonneg_of_le_mul ?_ ?_ ?_) ?_
  any_goals positivity
  rw [l1Norm_eq_sum, l2Norm_sq_eq_sum]
  exact sq_sum_le_card_mul_sum_sq

lemma general_hoelder (hÎ· : 0 â‰¤ Î·) (Î½ : G â†’ â„â‰¥0) (hfÎ½ : âˆ€ x, f x â‰  0 â†’ 1 â‰¤ Î½ x)
    (hÎ” : Î” âŠ† largeSpec f Î·) (hm : m â‰  0) :
    â†‘Î”.card ^ (2 * m) * (Î· ^ (2 * m) * (â€–fâ€–_[1] ^ 2 / â€–fâ€–_[2] ^ 2)) â‰¤
      energy m Î” (dft fun a â†¦ Î½ a) := by
  obtain rfl | hf := eq_or_ne f 0
  Â· simp
  choose c norm_c hc using fun Î³ â†¦ RCLike.exists_norm_eq_mul_self (dft f Î³)
  have :=
    calc
      Î· * â€–fâ€–_[1] * Î”.card â‰¤ âˆ‘ Î³ in Î”, â€–dft f Î³â€– := ?_
      _ â‰¤ â€–âˆ‘ x, f x * âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– := ?_
      _ â‰¤ âˆ‘ x, â€–f x * âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– := (norm_sum_le _ _)
      _ = âˆ‘ x, â€–f xâ€– * â€–âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– := by simp_rw [norm_mul]
      _ â‰¤ _ := inner_le_weight_mul_Lp_of_nonneg _ (p := m) ?_ _ _ (fun _ â†¦ norm_nonneg _)
            fun _ â†¦ norm_nonneg _
      _ = â€–fâ€–_[1] ^ (1 - (m : â„)â»Â¹) * (âˆ‘ x, â€–f xâ€– * â€–âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ (mâ»Â¹ : â„) :=
        by simp_rw [l1Norm_eq_sum, rpow_natCast]
  rotate_left
  Â· rw [â† nsmul_eq_mul']
    exact card_nsmul_le_sum _ _ _ fun x hx â†¦ mem_largeSpec.1 $ hÎ” hx
  Â· simp_rw [mul_sum, mul_comm (f _), mul_assoc (c _), @sum_comm _ _ G, â† mul_sum, â† l2Inner_eq_sum,
      â† dft_apply, â† hc, â† RCLike.ofReal_sum, RCLike.norm_ofReal]
    exact le_abs_self _
  Â· norm_cast
    exact hm.bot_lt
  replace this := pow_le_pow_left (by positivity) this m
  simp_rw [mul_pow] at this
  rw [rpow_inv_natCast_pow _ hm, â† rpow_mul_natCast, one_sub_mul,
    inv_mul_cancelâ‚€, â† Nat.cast_pred, rpow_natCast, mul_assoc, mul_left_comm, â† pow_sub_one_mul,
    mul_assoc, mul_le_mul_left] at this
  any_goals positivity
  replace hfÎ½ : âˆ€ x, â€–f xâ€– â‰¤ â€–f xâ€– * sqrt (Î½ x) := by
    rintro x
    obtain hfx | hfx := eq_or_ne (f x) 0
    Â· simp [hfx]
    Â· exact le_mul_of_one_le_right (norm_nonneg _) $ one_le_sqrt.2 $ NNReal.one_le_coe.2 $
        hfÎ½ _ hfx
  replace this :=
    calc
      _ â‰¤ (âˆ‘ x, â€–f xâ€– * sqrt (Î½ x) * â€–âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ 2 :=
          pow_le_pow_left (by positivity)
            (this.trans $ sum_le_sum fun x _ â†¦ mul_le_mul_of_nonneg_right (hfÎ½ _) $ by positivity) _
      _ = (âˆ‘ x, â€–f xâ€– * (sqrt (Î½ x) * â€–âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– ^ m)) ^ 2 := by
        simp_rw [mul_assoc]
      _ â‰¤ (âˆ‘ x, â€–f xâ€– ^ 2) * âˆ‘ x, (sqrt (Î½ x) * â€–âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ 2 :=
        sum_mul_sq_le_sq_mul_sq _ _ _
      _ â‰¤ â€–fâ€–_[2] ^ 2 * âˆ‘ x, Î½ x * (â€–âˆ‘ Î³ in Î”, c Î³ * conj (Î³ x)â€– ^ 2) ^ m := by
        simp_rw [l2Norm_sq_eq_sum, mul_pow, sq_sqrt (NNReal.coe_nonneg _), pow_right_comm]; rfl
  rw [mul_rotate', mul_left_comm, mul_pow, mul_pow, â† pow_mul', â† pow_mul', â† div_le_iff',
    mul_div_assoc, mul_div_assoc] at this
  calc
    _ â‰¤ _ := this
    _ = â€–(_ : â„‚)â€– := Eq.symm $ RCLike.norm_of_nonneg $ sum_nonneg fun _ _ â†¦ by positivity
    _ = â€–âˆ‘ Î³ in Î” ^^ m, âˆ‘ Î´ in Î” ^^ m,
          (âˆ i, conj (c (Î³ i)) * c (Î´ i)) * conj (dft (fun a â†¦ Î½ a) (âˆ‘ i, Î³ i - âˆ‘ i, Î´ i))â€– := ?_
    _ â‰¤ âˆ‘ Î³ in Î” ^^ m, âˆ‘ Î´ in Î” ^^ m,
          â€–(âˆ i, conj (c (Î³ i)) * c (Î´ i)) * conj (dft (fun a â†¦ Î½ a) (âˆ‘ i, Î³ i - âˆ‘ i, Î´ i))â€– :=
      (norm_sum_le _ _).trans $ sum_le_sum fun _ _ â†¦ norm_sum_le _ _
    _ = _ := by simp [energy, norm_c, -Complex.norm_eq_abs, norm_prod]
  Â· push_cast
    simp_rw [â† RCLike.conj_mul, dft_apply, l2Inner_eq_sum, map_sum, map_mul, RCLike.conj_conj,
      mul_pow, sum_pow', sum_mul, mul_sum, @sum_comm _ _ G, â† AddChar.inv_apply_eq_conj, â†
      AddChar.neg_apply', prod_mul_prod_comm, â† AddChar.add_apply, â† AddChar.sum_apply,
      mul_left_comm (Algebra.cast (Î½ _ : â„) : â„‚), â† mul_sum, â† sub_eq_add_neg, sum_sub_distrib,
      Complex.conj_ofReal, mul_comm (Algebra.cast (Î½ _ : â„) : â„‚)]
    rfl
  positivity

lemma spec_hoelder (hÎ· : 0 â‰¤ Î·) (hÎ” : Î” âŠ† largeSpec f Î·) (hm : m â‰  0) :
    â†‘Î”.card ^ (2 * m) * (Î· ^ (2 * m) * Î± f) â‰¤ boringEnergy m Î” := by
  have hG : (0 : â„) < card G := by positivity
  simpa [boringEnergy, Î±, mul_assoc, â† Pi.one_def, â† mul_div_right_comm, â† mul_div_assoc,
    div_le_iff hG, energy_nsmul, -nsmul_eq_mul, â† nsmul_eq_mul'] using
    general_hoelder hÎ· 1 (fun (_ : G) _ â†¦ le_rfl) hÎ” hm

noncomputable def changConst : â„ := 32 * exp 1

lemma one_lt_changConst : 1 < changConst := one_lt_mul (by norm_num) $ one_lt_exp_iff.2 one_pos

lemma changConst_pos : 0 < changConst := zero_lt_one.trans one_lt_changConst

namespace Mathlib.Meta.Positivity
open Lean.Meta Qq

/-- Extension for the `positivity` tactic: `changConst` is positive. -/
@[positivity changConst] def evalChangConst : PositivityExt where eval _ _ _ := do
  return .positive (q(changConst_pos) : Lean.Expr)

example : 0 < changConst := by positivity

end Mathlib.Meta.Positivity

lemma AddDissociated.boringEnergy_le [DecidableEq G] {s : Finset G}
    (hs : AddDissociated (s : Set G)) (n : â„•) :
    boringEnergy n s â‰¤ changConst ^ n * n ^ n * s.card ^ n := by
  obtain rfl | hn := eq_or_ne n 0
  Â· simp
  calc
    _ = â€–dft (ðŸ­ s)â€–â‚™_[â†‘(2 * n)] ^ (2 * n) := by rw [nlpNorm_dft_indicate_pow]
    _ â‰¤ (4 * rexp 2â»Â¹ * sqrt â†‘(2 * n) * â€–dft (ðŸ­ s)â€–â‚™_[2]) ^ (2 * n) := by
        gcongr
        refine rudin_ineq (le_mul_of_one_le_right zero_le_two $ Nat.one_le_iff_ne_zero.2 hn)
          (dft (ðŸ­_[â„‚] s)) ?_
        rwa [cft_dft, support_comp_eq_preimage, support_indicate, Set.preimage_comp,
          Set.neg_preimage, addDissociated_neg, AddEquiv.addDissociated_preimage]
    _ = _ := by
        simp_rw [mul_pow, pow_mul, nl2Norm_dft_indicate]
        rw [â† exp_nsmul, sq_sqrt, sq_sqrt]
        simp_rw [â† mul_pow]
        simp [changConst]
        ring_nf
        all_goals positivity

/-- **Chang's lemma**. -/
lemma chang (hf : f â‰  0) (hÎ· : 0 < Î·) :
    âˆƒ Î”, Î” âŠ† largeSpec f Î· âˆ§
      Î”.card â‰¤ âŒˆchangConst * exp 1 * âŒˆcurlog (Î± f)âŒ‰â‚Š / Î· ^ 2âŒ‰â‚Š âˆ§ largeSpec f Î· âŠ† Î”.addSpan := by
  refine exists_subset_addSpan_card_le_of_forall_addDissociated fun Î” hÎ”Î· hÎ” â†¦ ?_
  obtain hÎ”' | hÎ”' := @eq_zero_or_pos _ _ Î”.card
  Â· simp [hÎ”']
  have : 0 < Î± f := Î±_pos hf
  set Î² := âŒˆcurlog (Î± f)âŒ‰â‚Š
  have hÎ² : 0 < Î² := Nat.ceil_pos.2 (curlog_pos (Î±_pos hf) $ Î±_le_one _)
  refine le_of_pow_le_pow_left hÎ².ne' zero_le' $ Nat.cast_le.1 $ le_of_mul_le_mul_right ?_
    (by positivity : 0 < â†‘Î”.card ^ Î² * (Î· ^ (2 * Î²) * Î± f))
  push_cast
  rw [â† mul_assoc, â† pow_add, â† two_mul]
  refine ((spec_hoelder hÎ·.le hÎ”Î· hÎ².ne').trans $ hÎ”.boringEnergy_le _).trans ?_
  refine le_trans ?_ $ mul_le_mul_of_nonneg_right (pow_le_pow_left ?_ (Nat.le_ceil _) _) ?_
  rw [mul_right_comm, div_pow, mul_pow, mul_pow, exp_one_pow, â† pow_mul, mul_div_assoc]
  calc
    _ = (changConst * Î”.card * Î²) ^ Î² := by ring
    _ â‰¤ (changConst * Î”.card * Î²) ^ Î² * (Î± f * exp Î²) := ?_
    _ â‰¤ (changConst * Î”.card * Î²) ^ Î² * ((Î· / Î·) ^ (2 * Î²) * Î± f * exp Î²) := by
        rw [div_self hÎ·.ne', one_pow, one_mul]
    _ = _ := by ring
  refine le_mul_of_one_le_right (by positivity) ?_
  rw [â† inv_pos_le_iff_one_le_mul']
  exact inv_le_exp_curlog.trans $ exp_monotone $ Nat.le_ceil _
  all_goals positivity
