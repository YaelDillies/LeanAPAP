import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.MeanInequalities
import LeanAPAP.Prereqs.Curlog
import LeanAPAP.Prereqs.Energy
import LeanAPAP.Prereqs.LargeSpec
import LeanAPAP.Prereqs.Rudin

/-!
# Chang's lemma
-/

open Finset Fintype Function Real
open scoped BigOperators ComplexConjugate ComplexOrder NNReal

variable {G : Type*} [AddCommGroup G] [Fintype G] {f : G ‚Üí ‚ÑÇ} {Œ∑ : ‚Ñù} {œà : AddChar G ‚ÑÇ}
  {Œî : Finset (AddChar G ‚ÑÇ)} {m : ‚Ñï}

private noncomputable def Œ± (f : G ‚Üí ‚ÑÇ) := ‚Äñf‚Äñ_[1] ^ 2 / ‚Äñf‚Äñ_[2] ^ 2 / card G

lemma Œ±_nonneg (f : G ‚Üí ‚ÑÇ) : 0 ‚â§ Œ± f := by unfold Œ±; positivity
lemma Œ±_pos (hf : f ‚â† 0) : 0 < Œ± f := by unfold Œ±; positivity

lemma Œ±_le_one (f : G ‚Üí ‚ÑÇ) : Œ± f ‚â§ 1 := by
  refine' div_le_one_of_le (div_le_of_nonneg_of_le_mul _ _ _) _
  any_goals positivity
  rw [l1Norm_eq_sum, l2Norm_sq_eq_sum]
  exact sq_sum_le_card_mul_sum_sq

lemma general_hoelder (hŒ∑ : 0 ‚â§ Œ∑) (ŒΩ : G ‚Üí ‚Ñù‚â•0) (hfŒΩ : ‚àÄ x, f x ‚â† 0 ‚Üí 1 ‚â§ ŒΩ x)
    (hŒî : Œî ‚äÜ largeSpec f Œ∑) (hm : m ‚â† 0) :
    ‚ÜëŒî.card ^ (2 * m) * (Œ∑ ^ (2 * m) * (‚Äñf‚Äñ_[1] ^ 2 / ‚Äñf‚Äñ_[2] ^ 2)) ‚â§
      energy m Œî (dft fun a ‚Ü¶ ŒΩ a) := by
  obtain rfl | hf := eq_or_ne f 0
  ¬∑ simp
  choose c norm_c hc using fun Œ≥ ‚Ü¶ RCLike.exists_norm_eq_mul_self (dft f Œ≥)
  have :=
    calc
      Œ∑ * ‚Äñf‚Äñ_[1] * Œî.card ‚â§ ‚àë Œ≥ in Œî, ‚Äñdft f Œ≥‚Äñ := ?_
      _ ‚â§ ‚Äñ‚àë x, f x * ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ := ?_
      _ ‚â§ ‚àë x, ‚Äñf x * ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ := (norm_sum_le _ _)
      _ = ‚àë x, ‚Äñf x‚Äñ * ‚Äñ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ := by simp_rw [norm_mul]
      _ ‚â§ _ := inner_le_weight_mul_Lp_of_nonneg _ (p := m) ?_ _ _ (fun _ ‚Ü¶ norm_nonneg _)
            fun _ ‚Ü¶ norm_nonneg _
      _ = ‚Äñf‚Äñ_[1] ^ (1 - (m : ‚Ñù)‚Åª¬π) * (‚àë x, ‚Äñf x‚Äñ * ‚Äñ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ ^ m) ^ (m‚Åª¬π : ‚Ñù) :=
        by push_cast; simp_rw [l1Norm_eq_sum, rpow_nat_cast]
  rotate_left
  ¬∑ rw [‚Üênsmul_eq_mul']
    exact card_nsmul_le_sum _ _ _ fun x hx ‚Ü¶ mem_largeSpec.1 $ hŒî hx
  ¬∑ simp_rw [mul_sum, mul_comm (f _), mul_assoc (c _), @sum_comm _ _ G, ‚Üêmul_sum, ‚Üêl2Inner_eq_sum,
      ‚Üêdft_apply, ‚Üêhc, ‚ÜêRCLike.ofReal_sum, RCLike.norm_ofReal]
    exact le_abs_self _
  ¬∑ norm_cast
    exact hm.bot_lt
  replace this := pow_le_pow_left (by positivity) this m
  simp_rw [mul_pow] at this
  rw [rpow_inv_natCast_pow _ hm, ‚Üêrpow_mul_natCast, one_sub_mul,
    inv_mul_cancel, ‚ÜêNat.cast_pred, rpow_nat_cast, mul_assoc, mul_left_comm, ‚Üêpow_sub_one_mul,
    mul_assoc, mul_le_mul_left] at this
  any_goals positivity
  replace hfŒΩ : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ ‚Äñf x‚Äñ * sqrt (ŒΩ x) := by
    rintro x
    obtain hfx | hfx := eq_or_ne (f x) 0
    ¬∑ simp [hfx]
    ¬∑ exact le_mul_of_one_le_right (norm_nonneg _) $ one_le_sqrt.2 $ NNReal.one_le_coe.2 $
        hfŒΩ _ hfx
  replace this :=
    calc
      _ ‚â§ (‚àë x, ‚Äñf x‚Äñ * sqrt (ŒΩ x) * ‚Äñ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ ^ m) ^ 2 :=
          pow_le_pow_left (by positivity)
            (this.trans $ sum_le_sum fun x _ ‚Ü¶ mul_le_mul_of_nonneg_right (hfŒΩ _) $ by positivity) _
      _ = (‚àë x, ‚Äñf x‚Äñ * (sqrt (ŒΩ x) * ‚Äñ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ ^ m)) ^ 2 := by
        simp_rw [mul_assoc]
      _ ‚â§ (‚àë x, ‚Äñf x‚Äñ ^ 2) * ‚àë x, (sqrt (ŒΩ x) * ‚Äñ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ ^ m) ^ 2 :=
        sum_mul_sq_le_sq_mul_sq _ _ _
      _ ‚â§ ‚Äñf‚Äñ_[2] ^ 2 * ‚àë x, ŒΩ x * (‚Äñ‚àë Œ≥ in Œî, c Œ≥ * conj (Œ≥ x)‚Äñ ^ 2) ^ m := by
        simp_rw [l2Norm_sq_eq_sum, mul_pow, sq_sqrt (NNReal.coe_nonneg _), pow_right_comm]; rfl
  rw [mul_rotate', mul_left_comm, mul_pow, mul_pow, ‚Üêpow_mul', ‚Üêpow_mul', ‚Üêdiv_le_iff',
    mul_div_assoc, mul_div_assoc] at this
  calc
    _ ‚â§ _ := this
    _ = ‚Äñ(_ : ‚ÑÇ)‚Äñ := Eq.symm $ RCLike.norm_of_nonneg $ sum_nonneg fun _ _ ‚Ü¶ by positivity
    _ = ‚Äñ‚àë Œ≥ in Œî ^^ m, ‚àë Œ¥ in Œî ^^ m,
          (‚àè i, conj (c (Œ≥ i)) * c (Œ¥ i)) * conj (dft (fun a ‚Ü¶ ŒΩ a) (‚àë i, Œ≥ i - ‚àë i, Œ¥ i))‚Äñ := ?_
    _ ‚â§ ‚àë Œ≥ in Œî ^^ m, ‚àë Œ¥ in Œî ^^ m,
          ‚Äñ(‚àè i, conj (c (Œ≥ i)) * c (Œ¥ i)) * conj (dft (fun a ‚Ü¶ ŒΩ a) (‚àë i, Œ≥ i - ‚àë i, Œ¥ i))‚Äñ :=
      (norm_sum_le _ _).trans $ sum_le_sum fun _ _ ‚Ü¶ norm_sum_le _ _
    _ = _ := by simp [energy, norm_c, -Complex.norm_eq_abs, norm_prod]
  ¬∑ push_cast
    simp_rw [‚ÜêRCLike.conj_mul, dft_apply, l2Inner_eq_sum, map_sum, map_mul, RCLike.conj_conj,
      mul_pow, sum_pow', sum_mul, mul_sum, @sum_comm _ _ G, ‚ÜêAddChar.inv_apply_eq_conj, ‚Üê
      AddChar.neg_apply', prod_mul_prod_comm, ‚ÜêAddChar.add_apply, ‚ÜêAddChar.sum_apply,
      mul_left_comm (Algebra.cast (ŒΩ _ : ‚Ñù) : ‚ÑÇ), ‚Üêmul_sum, ‚Üêsub_eq_add_neg, sum_sub_distrib,
      Complex.conj_ofReal, mul_comm]
    rfl
  positivity

lemma spec_hoelder (hŒ∑ : 0 ‚â§ Œ∑) (hŒî : Œî ‚äÜ largeSpec f Œ∑) (hm : m ‚â† 0) :
    ‚ÜëŒî.card ^ (2 * m) * (Œ∑ ^ (2 * m) * Œ± f) ‚â§ boringEnergy m Œî := by
  have hG : (0 : ‚Ñù) < card G := by positivity
  simpa [boringEnergy, Œ±, mul_assoc, ‚ÜêPi.one_def, ‚Üêmul_div_right_comm, ‚Üêmul_div_assoc,
    div_le_iff hG, energy_nsmul, -nsmul_eq_mul, ‚Üênsmul_eq_mul'] using
    general_hoelder hŒ∑ 1 (fun (_ : G) _ ‚Ü¶ le_rfl) hŒî hm

noncomputable def changConst : ‚Ñù := 32 * exp 1

lemma one_lt_changConst : 1 < changConst := one_lt_mul (by norm_num) $ one_lt_exp_iff.2 one_pos

lemma changConst_pos : 0 < changConst := zero_lt_one.trans one_lt_changConst

namespace Mathlib.Meta.Positivity
open Lean.Meta Qq

/-- Extension for the `positivity` tactic: `changConst` is positive. -/
@[positivity changConst] def evalChangConst : PositivityExt where eval _ _ _ := do
  return .positive (q(changConst_pos) : Lean.Expr)

example : 0 < changConst := by positivity

end Mathlib.Meta.Positivity

lemma AddDissociated.boringEnergy_le [DecidableEq G] {s : Finset G}
    (hs : AddDissociated (s : Set G)) (n : ‚Ñï) :
    boringEnergy n s ‚â§ changConst ^ n * n ^ n * s.card ^ n := by
  obtain rfl | hn := eq_or_ne n 0
  ¬∑ simp
  calc
    _ = ‚Äñdft (ùü≠ s)‚Äñ‚Çô_[‚Üë(2 * n)] ^ (2 * n) := by rw [nlpNorm_dft_indicate_pow]
    _ ‚â§ (4 * rexp 2‚Åª¬π * sqrt ‚Üë(2 * n) * ‚Äñdft (ùü≠ s)‚Äñ‚Çô_[2]) ^ (2 * n) := by
        gcongr
        refine rudin_ineq (le_mul_of_one_le_right zero_le_two $ Nat.one_le_iff_ne_zero.2 hn)
          (dft (ùü≠_[‚ÑÇ] s)) ?_
        rwa [cft_dft, support_comp_eq_preimage, support_indicate, Set.preimage_comp,
          Set.neg_preimage, addDissociated_neg, AddEquiv.addDissociated_preimage]
    _ = _ := by
        simp_rw [mul_pow, pow_mul, nl2Norm_dft_indicate]
        rw [‚Üê exp_nsmul, sq_sqrt, sq_sqrt]
        simp_rw [‚Üêmul_pow]
        simp [changConst]
        ring_nf
        all_goals positivity

/-- **Chang's lemma**. -/
lemma chang (hf : f ‚â† 0) (hŒ∑ : 0 < Œ∑) :
    ‚àÉ Œî, Œî ‚äÜ largeSpec f Œ∑ ‚àß
      Œî.card ‚â§ ‚åàchangConst * exp 1 * ‚åàcurlog (Œ± f)‚åâ‚Çä / Œ∑ ^ 2‚åâ‚Çä ‚àß largeSpec f Œ∑ ‚äÜ Œî.addSpan := by
  refine diss_addSpan fun Œî hŒîŒ∑ hŒî ‚Ü¶ ?_
  obtain hŒî' | hŒî' := @eq_zero_or_pos _ _ Œî.card
  ¬∑ simp [hŒî']
  have : 0 < Œ± f := Œ±_pos hf
  set Œ≤ := ‚åàcurlog (Œ± f)‚åâ‚Çä
  have hŒ≤ : 0 < Œ≤ := Nat.ceil_pos.2 (curlog_pos (Œ±_pos hf) $ Œ±_le_one _)
  refine' le_of_pow_le_pow_left hŒ≤.ne' zero_le' $ Nat.cast_le.1 $ le_of_mul_le_mul_right _
    (by positivity : 0 < ‚ÜëŒî.card ^ Œ≤ * (Œ∑ ^ (2 * Œ≤) * Œ± f))
  push_cast
  rw [‚Üêmul_assoc, ‚Üêpow_add, ‚Üêtwo_mul]
  refine' ((spec_hoelder hŒ∑.le hŒîŒ∑ hŒ≤.ne').trans $ hŒî.boringEnergy_le _).trans _
  refine' le_trans _ $ mul_le_mul_of_nonneg_right (pow_le_pow_left _ (Nat.le_ceil _) _) _
  rw [mul_right_comm, div_pow, mul_pow, mul_pow, exp_one_pow, ‚Üêpow_mul, mul_div_assoc]
  calc
    _ = (changConst * Œî.card * Œ≤) ^ Œ≤ := by ring
    _ ‚â§ (changConst * Œî.card * Œ≤) ^ Œ≤ * (Œ± f * exp Œ≤) := ?_
    _ ‚â§ (changConst * Œî.card * Œ≤) ^ Œ≤ * ((Œ∑ / Œ∑) ^ (2 * Œ≤) * Œ± f * exp Œ≤) := by
        rw [div_self hŒ∑.ne', one_pow, one_mul]
    _ = _ := by ring
  refine' le_mul_of_one_le_right (by positivity) _
  rw [‚Üêinv_pos_le_iff_one_le_mul']
  exact inv_le_exp_curlog.trans $ exp_monotone $ Nat.le_ceil _
  all_goals positivity
