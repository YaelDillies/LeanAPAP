import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.MeanInequalities
import Mathlib.Tactic.Bound
import LeanAPAP.Prereqs.Energy
import LeanAPAP.Prereqs.LargeSpec
import LeanAPAP.Prereqs.Rudin

/-!
# Chang's lemma
-/

open Finset Fintype Function MeasureTheory RCLike Real
open scoped BigOperators ComplexConjugate ComplexOrder NNReal

variable {G : Type*} [AddCommGroup G] [Fintype G] {f : G â†’ â„‚} {x Î· : â„} {Ïˆ : AddChar G â„‚}
  {Î” : Finset (AddChar G â„‚)} {m : â„•}

local notation "ğ“›" x:arg => 1 + log xâ»Â¹

private lemma curlog_pos (hxâ‚€ : 0 â‰¤ x) (hxâ‚ : x â‰¤ 1) : 0 < ğ“› x := by
  obtain rfl | hxâ‚€ := hxâ‚€.eq_or_lt
  Â· simp
  have : 0 â‰¤ log xâ»Â¹ := by bound
  positivity

private lemma rpow_inv_neg_curlog_le (hxâ‚€ : 0 â‰¤ x) (hxâ‚ : x â‰¤ 1) : xâ»Â¹ ^ (ğ“› x)â»Â¹ â‰¤ exp 1 := by
  obtain rfl | hxâ‚€ := hxâ‚€.eq_or_lt
  Â· simp; positivity
  obtain rfl | hxâ‚ := hxâ‚.eq_or_lt
  Â· simp
  have hx := (one_lt_invâ‚€ hxâ‚€).2 hxâ‚
  calc
    xâ»Â¹ ^ (ğ“› x)â»Â¹ â‰¤ xâ»Â¹ ^ (log xâ»Â¹)â»Â¹ := by
      gcongr
      Â· exact hx.le
      Â· exact log_pos hx
      Â· simp
    _ â‰¤ exp 1 := xâ»Â¹.rpow_inv_log_le_exp_one

noncomputable def changConst : â„ := 32 * exp 1

lemma one_lt_changConst : 1 < changConst := by unfold changConst; bound

lemma changConst_pos : 0 < changConst := zero_lt_one.trans one_lt_changConst

namespace Mathlib.Meta.Positivity
open Lean.Meta Qq

/-- Extension for the `positivity` tactic: `changConst` is positive. -/
@[positivity changConst] def evalChangConst : PositivityExt where eval _ _ _ := do
  return .positive (q(changConst_pos) : Lean.Expr)

example : 0 < changConst := by positivity

end Mathlib.Meta.Positivity

lemma AddDissociated.boringEnergy_le [MeasurableSpace G] [DiscreteMeasurableSpace G] [DecidableEq G]
    {s : Finset G} (hs : AddDissociated (s : Set G)) (n : â„•) :
    boringEnergy n s â‰¤ changConst ^ n * n ^ n * #s ^ n := by
  obtain rfl | hn := eq_or_ne n 0
  Â· simp
  calc
    _ = (â€–dft (ğŸ­ s)â€–â‚™_[â†‘(2 * n)] ^ (2 * n) : â„) := by rw [cLpNorm_dft_indicate_pow]
    _ â‰¤ (4 * rexp 2â»Â¹ * sqrt â†‘(2 * n) * â€–dft (ğŸ­ s)â€–â‚™_[2]) ^ (2 * n) := by
        gcongr
        refine rudin_ineq (le_mul_of_one_le_right zero_le_two <| Nat.one_le_iff_ne_zero.2 hn)
          (dft (ğŸ­_[â„‚] s)) ?_
        rwa [cft_dft, support_comp_eq_preimage, support_indicate, Set.preimage_comp,
          Set.neg_preimage, addDissociated_neg, AddEquiv.addDissociated_preimage]
    _ = _ := by
        simp_rw [mul_pow, pow_mul, cL2Norm_dft_indicate]
        rw [â† exp_nsmul, sq_sqrt (by positivity), sq_sqrt (by positivity)]
        simp_rw [â† mul_pow]
        simp [changConst]
        ring_nf

local notation:70 s:70 " ^^ " n:71 => Fintype.piFinset fun _ : Fin n â†¦ s

variable [MeasurableSpace G] [DiscreteMeasurableSpace G]

lemma general_hoelder (hÎ· : 0 â‰¤ Î·) (Î½ : G â†’ â„â‰¥0) (hfÎ½ : âˆ€ x, f x â‰  0 â†’ 1 â‰¤ Î½ x)
    (hÎ” : Î” âŠ† largeSpec f Î·) (hm : m â‰  0) :
    #Î” ^ (2 * m) * (Î· ^ (2 * m) * (â€–fâ€–â‚™_[1] ^ 2 / â€–fâ€–â‚™_[2] ^ 2)) â‰¤
      energy m Î” (cft fun a â†¦ Î½ a) := by
  obtain rfl | hf := eq_or_ne f 0
  Â· simp
  choose c norm_c hc using fun Î³ â†¦ RCLike.exists_norm_eq_mul_self (cft f Î³)
  have :=
    calc
      Î· * â€–fâ€–â‚™_[1] * Î”.card â‰¤ âˆ‘ Î³ âˆˆ Î”, â€–cft f Î³â€– := by
        rw [â† nsmul_eq_mul']
        exact card_nsmul_le_sum _ _ _ fun x hx â†¦ mem_largeSpec_iff_cft.1 <| hÎ” hx
      _ â‰¤ |âˆ‘ i âˆˆ Î”, â€–cft f iâ€–| := le_abs_self _
      _ = â€–ğ”¼ x, f x * âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– := by
        simp_rw [mul_sum, mul_comm (f _), mul_assoc (c _), expect_sum_comm, â† mul_expect,
          â† cL2Inner_eq_expect, â† cft_apply, â† hc, â† RCLike.ofReal_sum, RCLike.norm_ofReal]
      _ â‰¤ ğ”¼ x, â€–f x * âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– := norm_expect_le (K := â„‚)
      _ = ğ”¼ x, â€–f xâ€– * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– := by simp_rw [norm_mul]
      _ â‰¤ _ :=
        compact_inner_le_weight_mul_Lp_of_nonneg _ (p := m) (mod_cast hm.bot_lt)
          (fun _ â†¦ norm_nonneg _) fun _ â†¦ norm_nonneg _
      _ = â€–fâ€–â‚™_[1] ^ (1 - (m : â„)â»Â¹) * (ğ”¼ x, â€–f xâ€– * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ (mâ»Â¹ : â„) :=
        by simp_rw [cL1Norm_eq_expect_norm, rpow_natCast]
  replace hfÎ½ : âˆ€ x, â€–f xâ€– â‰¤ â€–f xâ€– * sqrt (Î½ x) := by
    rintro x
    obtain hfx | hfx := eq_or_ne (f x) 0
    Â· simp [hfx]
    Â· exact le_mul_of_one_le_right (norm_nonneg _) <| one_le_sqrt.2 <| NNReal.one_le_coe.2 <|
        hfÎ½ _ hfx
  have :=
    calc
      (â€–fâ€–â‚™_[1] * (Î· ^ m * Î”.card ^ m)) ^ 2
        = (â€–fâ€–â‚™_[1] / â€–fâ€–â‚™_[1] ^ m * â€–fâ€–â‚™_[1] ^ m * (Î· ^ m * Î”.card ^ m)) ^ 2 := by
        rw [div_mul_cancelâ‚€]; positivity
      _ = (â€–fâ€–â‚™_[1] ^ (1 - m : â„¤) * (Î· * â€–fâ€–â‚™_[1] * Î”.card) ^ m) ^ 2 := by
        rw [zpow_one_sub_natCastâ‚€]; ring; positivity
      _ â‰¤ (â€–fâ€–â‚™_[1] ^ (1 - m : â„¤) * (â€–fâ€–â‚™_[1] ^ (1 - (m : â„)â»Â¹) *
            (ğ”¼ x, â€–f xâ€– * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ (mâ»Â¹ : â„)) ^ m) ^ 2 := by gcongr
      _ = (â€–fâ€–â‚™_[1] ^ (1 - m : â„¤) * (â€–fâ€–â‚™_[1] ^ (m - 1 : â„¤) *
            ğ”¼ x, â€–f xâ€– * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m)) ^ 2 := by
        rw [mul_pow _ _ m, â† rpow_mul_natCast, â† rpow_mul_natCast, one_sub_mul, inv_mul_cancelâ‚€]
        norm_cast
        ring
        all_goals positivity
      _ = (ğ”¼ x, â€–f xâ€– * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ 2 := by
        rw [â† mul_assoc, â† zpow_addâ‚€]; simp; positivity
      _ â‰¤ (ğ”¼ x, â€–f xâ€– * sqrt (Î½ x) * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ 2 := by gcongr; exact hfÎ½ _
      _ = (ğ”¼ x, â€–f xâ€– * (sqrt (Î½ x) * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m)) ^ 2 := by
        simp_rw [mul_assoc]
      _ â‰¤ (ğ”¼ x, â€–f xâ€– ^ 2) * ğ”¼ x, (sqrt (Î½ x) * â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ m) ^ 2 :=
        expect_mul_sq_le_sq_mul_sq ..
      _ â‰¤ â€–fâ€–â‚™_[2] ^ 2 * ğ”¼ x, Î½ x * (â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ 2) ^ m := by
        simp_rw [cL2Norm_sq_eq_expect_norm, mul_pow, sq_sqrt (NNReal.coe_nonneg _), pow_right_comm]
        rfl
  rw [mul_rotate', mul_left_comm, mul_pow, mul_pow, â† pow_mul', â† pow_mul', â† div_le_iffâ‚€',
    mul_div_assoc, mul_div_assoc] at this
  calc
    Î”.card ^ (2 * m) * (Î· ^ (2 * m) * (â€–fâ€–â‚™_[1] ^ 2 / â€–fâ€–â‚™_[2] ^ 2))
      â‰¤ ğ”¼ x, Î½ x * (â€–âˆ‘ Î³ âˆˆ Î”, c Î³ * conj (Î³ x)â€– ^ 2) ^ m := this
    _ = â€–(_ : â„‚)â€– := Eq.symm <| RCLike.norm_of_nonneg <| by positivity
    _ = â€–âˆ‘ Î³ âˆˆ Î” ^^ m, âˆ‘ Î´ âˆˆ Î” ^^ m,
          (âˆ i, conj (c (Î³ i)) * c (Î´ i)) * conj (cft (fun a â†¦ Î½ a) (âˆ‘ i, Î³ i - âˆ‘ i, Î´ i))â€– := ?_
    _ â‰¤ âˆ‘ Î³ âˆˆ Î” ^^ m, âˆ‘ Î´ âˆˆ Î” ^^ m,
          â€–(âˆ i, conj (c (Î³ i)) * c (Î´ i)) * conj (cft (fun a â†¦ Î½ a) (âˆ‘ i, Î³ i - âˆ‘ i, Î´ i))â€– :=
      (norm_sum_le _ _).trans <| sum_le_sum fun _ _ â†¦ norm_sum_le _ _
    _ = _ := by simp [energy, norm_c, norm_prod]
  Â· push_cast
    simp_rw [â† RCLike.conj_mul, cft_apply, cL2Inner_eq_expect', map_sum, map_mul, RCLike.conj_conj,
      mul_pow, sum_pow', sum_mul, mul_sum, expect_sum_comm, â† AddChar.inv_apply_eq_conj,
      â† AddChar.neg_apply', prod_mul_prod_comm, â† AddChar.add_apply, â† AddChar.sum_apply,
      map_expect, mul_left_comm (Algebra.cast (Î½ _ : â„) : â„‚), â† mul_expect, â† sub_eq_add_neg,
      sum_sub_distrib, map_mul, Complex.conj_ofReal, mul_comm (Algebra.cast (Î½ _ : â„) : â„‚),
      â† AddChar.map_neg_eq_conj, AddChar.neg_apply, neg_neg]
    rfl

open scoped ComplexOrder

lemma spec_hoelder (hÎ· : 0 â‰¤ Î·) (hÎ” : Î” âŠ† largeSpec f Î·) (hm : m â‰  0) :
    #Î” ^ (2 * m) * (Î· ^ (2 * m) * (â€–fâ€–â‚™_[1] ^ 2 / â€–fâ€–â‚™_[2] ^ 2)) â‰¤ boringEnergy m Î” := by
  have hG : (0 : â„) < card G := by positivity
  simpa [boringEnergy, mul_assoc, â† Pi.one_def, â† mul_div_right_comm, â† mul_div_assoc,
    div_le_iffâ‚€ hG, energy_nsmul, -nsmul_eq_mul, â† nsmul_eq_mul'] using
    general_hoelder hÎ· 1 (fun (_ : G) _ â†¦ le_rfl) hÎ” hm

/-- **Chang's lemma**. -/
lemma chang (hf : f â‰  0) (hÎ· : 0 < Î·) :
    âˆƒ Î”, Î” âŠ† largeSpec f Î· âˆ§ largeSpec f Î· âŠ† Î”.addSpan âˆ§
      #Î” â‰¤ âŒˆchangConst * exp 1 * âŒˆğ“› â†‘(â€–fâ€–â‚™_[1] ^ 2 / â€–fâ€–â‚™_[2] ^ 2)âŒ‰â‚Š / Î· ^ 2âŒ‰â‚Š := by
  simp_rw [and_comm (a := largeSpec f Î· âŠ† _)]
  refine exists_subset_addSpan_card_le_of_forall_addDissociated fun Î” hÎ”Î· hÎ” â†¦ ?_
  obtain hÎ”' | hÎ”' := eq_zero_or_pos #Î”
  Â· simp [hÎ”']
  let Î± := â€–fâ€–â‚™_[1] ^ 2 / â€–fâ€–â‚™_[2] ^ 2
  have : 0 < Î± := by positivity
  set Î² := âŒˆğ“› Î±âŒ‰â‚Š
  have hÎ² : 0 < Î² := Nat.ceil_pos.2 <| curlog_pos (by positivity) <|
    div_le_one_of_leâ‚€ (by dsimp; gcongr; exact one_le_two) (by dsimp; positivity)
  have : 0 < â€–fâ€–â‚™_[1] := by positivity
  refine le_of_pow_le_pow_leftâ‚€ hÎ².ne' zero_le' <| Nat.cast_le.1 <| le_of_mul_le_mul_right ?_
    (by positivity : 0 < Î”.card ^ Î² * (Î· ^ (2 * Î²) * Î±))
  push_cast
  rw [â† mul_assoc, â† pow_add, â† two_mul]
  refine ((spec_hoelder hÎ·.le hÎ”Î· hÎ².ne').trans <| hÎ”.boringEnergy_le _).trans ?_
  refine le_trans ?_ <| mul_le_mul_of_nonneg_right (pow_le_pow_leftâ‚€ ?_ (Nat.le_ceil _) _) ?_
  any_goals positivity
  rw [mul_right_comm, div_pow, mul_pow, mul_pow, exp_one_pow, â† pow_mul, mul_div_assoc]
  calc
    _ = (changConst * #Î” * Î²) ^ Î² := by ring
    _ â‰¤ (changConst * #Î” * Î²) ^ Î² * (Î± * exp Î²) := ?_
    _ â‰¤ (changConst * #Î” * Î²) ^ Î² * ((Î· / Î·) ^ (2 * Î²) * Î± * exp Î²) := by
      rw [div_self hÎ·.ne', one_pow, one_mul]
    _ = _ := by ring
  refine le_mul_of_one_le_right (by positivity) ?_
  rw [â† inv_le_iff_one_le_mulâ‚€' (by positivity)]
  calc
    Î±â»Â¹ = exp (0 + log Î±â»Â¹) := by
      rw [zero_add, exp_log]
      Â· norm_cast
      Â· positivity
    _ â‰¤ exp âŒˆ0 + log Î±â»Â¹âŒ‰â‚Š := by gcongr; exact Nat.le_ceil _
    _ â‰¤ exp Î² := by unfold Î²; gcongr; exact zero_le_one
