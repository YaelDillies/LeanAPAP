import Mathlib.NumberTheory.LegendreSymbol.AddCharacter
import LeanAPAP.Prereqs.Discrete.Convolution.Basic
import LeanAPAP.Prereqs.Discrete.LpNorm.Basic

/-!
### TODO

Rename
* `map_add_mul` â†’ `map_add_eq_mul`
* `map_zero_one` â†’ `map_zero_eq_one`
* `map_nsmul_pow` â†’ `map_nsmul_eq_pow`
-/

open Finset hiding card
open Fintype (card)
open Function
open scoped BigOps ComplexConjugate DirectSum NNRat

variable {G H R : Type*}

namespace AddChar
section AddMonoid
variable [AddMonoid G] [AddMonoid H] [CommMonoid R] {Ïˆ : AddChar G R}

instance instAddCommMonoid : AddCommMonoid (AddChar G R) := Additive.addCommMonoid

attribute [simp, norm_cast] mul_apply one_apply MonoidHom.coe_compAddChar coe_compAddMonoidHom

-- TODO: Replace `AddChar.toMonoidHomEquiv`
/-- Interpret an additive character as a monoid homomorphism. -/
def toMonoidHomEquiv' : AddChar G R â‰ƒ (Multiplicative G â†’* R) := toMonoidHomEquiv _ _

@[simp, norm_cast]
lemma coe_toMonoidHomEquiv' (Ïˆ : AddChar G R) : â‡‘(toMonoidHomEquiv' Ïˆ) = Ïˆ âˆ˜ Multiplicative.toAdd := rfl

@[simp, norm_cast] lemma coe_toMonoidHomEquiv'_symm (Ïˆ : Multiplicative G â†’* R) :
    â‡‘(toMonoidHomEquiv'.symm Ïˆ) = Ïˆ âˆ˜ Multiplicative.ofAdd := rfl

@[simp] lemma toMonoidHomEquiv'_apply (Ïˆ : AddChar G R) (a : Multiplicative G) :
    toMonoidHomEquiv' Ïˆ a = Ïˆ (Multiplicative.toAdd a) := rfl

@[simp] lemma toMonoidHomEquiv'_symm_apply (Ïˆ : Multiplicative G â†’* R) (a : G) :
    toMonoidHomEquiv'.symm Ïˆ a = Ïˆ (Multiplicative.ofAdd a) := rfl

@[simp] lemma toMonoidHomEquiv'_zero : toMonoidHomEquiv' (0 : AddChar G R) = 1 := rfl
@[simp] lemma toMonoidHomEquiv'_symm_one : toMonoidHomEquiv'.symm (1 : Multiplicative G â†’* R) = 0 := rfl

@[simp] lemma toMonoidHomEquiv'_add (Ïˆ Ï† : AddChar G R) :
    toMonoidHomEquiv' (Ïˆ + Ï†) = toMonoidHomEquiv' Ïˆ * toMonoidHomEquiv' Ï† := rfl
@[simp] lemma toMonoidHomEquiv'_symm_mul (Ïˆ Ï† : Multiplicative G â†’* R) :
  toMonoidHomEquiv'.symm (Ïˆ * Ï†) = toMonoidHomEquiv'.symm Ïˆ + toMonoidHomEquiv'.symm Ï† := rfl

/-- Interpret an additive character as a monoid homomorphism. -/
def toAddMonoidHomEquiv' : AddChar G R â‰ƒ (G â†’+ Additive R) := toAddMonoidHomEquiv _ _

@[simp, norm_cast]
lemma coe_toAddMonoidHomEquiv' (Ïˆ : AddChar G R) : â‡‘(toAddMonoidHomEquiv' Ïˆ) = Additive.ofMul âˆ˜ Ïˆ := rfl

@[simp, norm_cast] lemma coe_toAddMonoidHomEquiv'_symm (Ïˆ : G â†’+ Additive R) :
    â‡‘(toAddMonoidHomEquiv'.symm Ïˆ) = Additive.toMul âˆ˜ Ïˆ := rfl

@[simp] lemma toAddMonoidHomEquiv'_apply (Ïˆ : AddChar G R) (a : G) :
    toAddMonoidHomEquiv' Ïˆ a = Additive.ofMul (Ïˆ a) := rfl

@[simp] lemma toAddMonoidHomEquiv'_symm_apply (Ïˆ : G â†’+ Additive R) (a : G) :
    toAddMonoidHomEquiv'.symm Ïˆ a = Additive.toMul (Ïˆ a) := rfl

lemma eq_one_iff : Ïˆ = 0 â†” âˆ€ x, Ïˆ x = 1 := DFunLike.ext_iff
lemma ne_one_iff : Ïˆ â‰  0 â†” âˆƒ x, Ïˆ x â‰  1 := DFunLike.ne_iff

@[simp, norm_cast] lemma coe_one : â‡‘(1 : AddChar G R) = 1 := rfl
@[simp, norm_cast] lemma coe_mul (Ïˆ Ï‡ : AddChar G R) : â‡‘(Ïˆ * Ï‡) = Ïˆ * Ï‡ := rfl

@[simp, norm_cast] lemma coe_pow (n : â„•) (Ïˆ : AddChar G R) : â‡‘(Ïˆ ^ n) = Ïˆ ^ n := rfl

lemma eq_zero_iff : Ïˆ = 0 â†” âˆ€ x, Ïˆ x = 1 := DFunLike.ext_iff
lemma ne_zero_iff : Ïˆ â‰  0 â†” âˆƒ x, Ïˆ x â‰  1 := DFunLike.ne_iff

@[simp, norm_cast] lemma coe_zero : â‡‘(0 : AddChar G R) = 1 := rfl

lemma zero_apply (a : G) : (0 : AddChar G R) a = 1 := rfl

@[simp, norm_cast] lemma coe_eq_zero : â‡‘Ïˆ = 1 â†” Ïˆ = 0 := by rw [â† coe_zero, DFunLike.coe_fn_eq]
@[simp, norm_cast] lemma coe_add (Ïˆ Ï‡ : AddChar G R) : â‡‘(Ïˆ + Ï‡) = Ïˆ * Ï‡ := rfl

lemma add_apply (Ïˆ Ï‡ : AddChar G R) (a : G) : (Ïˆ + Ï‡) a = Ïˆ a * Ï‡ a := rfl

@[simp, norm_cast] lemma coe_nsmul (n : â„•) (Ïˆ : AddChar G R) : â‡‘(n â€¢ Ïˆ) = Ïˆ ^ n := rfl

lemma nsmul_apply (n : â„•) (Ïˆ : AddChar G R) (a : G) : (Ïˆ ^ n) a = Ïˆ a ^ n := rfl

variable {Î¹ : Type*}

@[simp, norm_cast]
lemma coe_sum (s : Finset Î¹) (Ïˆ : Î¹ â†’ AddChar G R) : âˆ‘ i in s, Ïˆ i = âˆ i in s, â‡‘(Ïˆ i) := by
  induction s using Finset.cons_induction <;> simp [*]

lemma sum_apply (s : Finset Î¹) (Ïˆ : Î¹ â†’ AddChar G R) (a : G) :
    (âˆ‘ i in s, Ïˆ i) a = âˆ i in s, Ïˆ i a := by rw [coe_sum, Finset.prod_apply]

noncomputable instance : DecidableEq (AddChar G R) := Classical.decEq _

@[simp] lemma compAddMonoidHom_apply (Ïˆ : AddChar H R) (f : G â†’+ H) (a : G) :
    (Ïˆ.compAddMonoidHom f) a = Ïˆ (f a) := rfl

lemma compAddMonoidHom_injective_left (f : G â†’+ H) (hf : Surjective f) :
    Injective fun Ïˆ : AddChar H R â†¦ Ïˆ.compAddMonoidHom f := by
  rintro Ïˆ Ï‡ h
  rw [DFunLike.ext'_iff] at h âŠ¢
  exact hf.injective_comp_right h

lemma compAddMonoidHom_injective_right (Ïˆ : AddChar H R) (hÏˆ : Injective Ïˆ) :
    Injective fun f : G â†’+ H â†¦ Ïˆ.compAddMonoidHom f := by
  rintro f g h
  rw [DFunLike.ext'_iff] at h âŠ¢
  exact hÏˆ.comp_left h

/-- The double dual embedding. -/
def doubleDualEmb : G â†’+ AddChar (AddChar G R) R where
  toFun a := { toFun := fun Ïˆ â†¦ Ïˆ a
               map_zero_one' := by simp
               map_add_mul' := by simp }
  map_zero' := by ext; simp
  map_add' _ _ := by ext; simp [map_add_mul]

@[simp] lemma doubleDualEmb_apply (a : G) (Ïˆ : AddChar G R) : doubleDualEmb a Ïˆ = Ïˆ a := rfl

end AddMonoid

section AddGroup
variable [AddGroup G]

section DivisionCommMonoid
variable [DivisionCommMonoid R]

lemma map_sub_eq_div (Ïˆ : AddChar G R) (x y : G) : Ïˆ (x - y) = Ïˆ x / Ïˆ y :=
  Ïˆ.toMonoidHom.map_div _ _

lemma injective_iff {Ïˆ : AddChar G R} : Injective Ïˆ â†” âˆ€ â¦ƒxâ¦„, Ïˆ x = 1 â†’ x = 0 :=
  Ïˆ.toMonoidHom.ker_eq_bot_iff.symm.trans eq_bot_iff

end DivisionCommMonoid

section NormedField
variable [Finite G] [NormedField R]

@[simp] lemma norm_apply (Ïˆ : AddChar G R) (x : G) : â€–Ïˆ xâ€– = 1 :=
  (Ïˆ.toMonoidHom.isOfFinOrder $ isOfFinOrder_of_finite _).norm_eq_one

@[simp] lemma coe_ne_zero (Ïˆ : AddChar G R) : (Ïˆ : G â†’ R) â‰  0 :=
  Function.ne_iff.2 âŸ¨0, fun h â†¦ by simpa only [h, Pi.zero_apply, zero_ne_one] using map_zero_one ÏˆâŸ©

end NormedField

section IsROrC
variable [IsROrC R]

lemma inv_apply_eq_conj [Finite G] (Ïˆ : AddChar G R) (x : G) : (Ïˆ x)â»Â¹ = conj (Ïˆ x) :=
  IsROrC.inv_eq_conj $ norm_apply _ _

protected lemma l2Inner_self [Fintype G] (Ïˆ : AddChar G R) :
    âŸª(Ïˆ : G â†’ R), ÏˆâŸ«_[R] = Fintype.card G := l2Inner_self_of_norm_eq_one Ïˆ.norm_apply

end IsROrC

section CommSemiring
variable [Fintype G] [CommSemiring R] [IsDomain R] [CharZero R] {Ïˆ : AddChar G R}

lemma sum_eq_ite (Ïˆ : AddChar G R) : âˆ‘ a, Ïˆ a = if Ïˆ = 0 then â†‘(card G) else 0 := by
  split_ifs with h
  Â· simp [h, card_univ]
  obtain âŸ¨x, hxâŸ© := ne_one_iff.1 h
  refine' eq_zero_of_mul_eq_self_left hx _
  rw [Finset.mul_sum]
  exact Fintype.sum_equiv (Equiv.addLeft x) _ _ fun y â†¦ (map_add_mul _ _ _).symm

lemma sum_eq_zero_iff_ne_zero : âˆ‘ x, Ïˆ x = 0 â†” Ïˆ â‰  0 := by
  rw [sum_eq_ite, Ne.ite_eq_right_iff]
  exact Nat.cast_ne_zero.2 Fintype.card_ne_zero

lemma sum_ne_zero_iff_eq_zero : âˆ‘ x, Ïˆ x â‰  0 â†” Ïˆ = 0 :=
  sum_eq_zero_iff_ne_zero.not_left

end CommSemiring

section Semifield
variable [Fintype G] [Semifield R] [IsDomain R] [CharZero R] [SMul â„šâ‰¥0 R] [CompAction R]
  {Ïˆ : AddChar G R}

lemma expect_eq_ite (Ïˆ : AddChar G R) : ğ”¼ a, Ïˆ a = if Ïˆ = 0 then 1 else 0 := by
  split_ifs with h
  Â· simp [h, card_univ, univ_nonempty]
  obtain âŸ¨x, hxâŸ© := ne_one_iff.1 h
  refine' eq_zero_of_mul_eq_self_left hx _
  rw [Finset.mul_expect]
  exact Fintype.expect_equiv (Equiv.addLeft x) _ _ fun y â†¦ (map_add_mul _ _ _).symm

lemma expect_eq_zero_iff_ne_zero : ğ”¼ x, Ïˆ x = 0 â†” Ïˆ â‰  0 := by
  rw [expect_eq_ite, one_ne_zero.ite_eq_right_iff]

lemma expect_ne_zero_iff_eq_zero : ğ”¼ x, Ïˆ x â‰  0 â†” Ïˆ = 0 := expect_eq_zero_iff_ne_zero.not_left

end Semifield
end AddGroup

section AddCommGroup
variable [AddCommGroup G]

section CommMonoid
variable [CommMonoid R]

/-- The additive characters on a commutative additive group form a commutative group. -/
instance : AddCommGroup (AddChar G R) :=
  @Additive.addCommGroup (AddChar G R) _

@[simp]
lemma neg_apply (Ïˆ : AddChar G R) (a : G) : (-Ïˆ) a = Ïˆ (-a) := rfl

@[simp]
lemma sub_apply (Ïˆ Ï‡ : AddChar G R) (a : G) : (Ïˆ - Ï‡) a = Ïˆ a * Ï‡ (-a) := rfl

end CommMonoid

section DivisionCommMonoid
variable [DivisionCommMonoid R]

-- TODO: Replace `map_zsmul_zpow`
@[simp]
lemma map_zsmul_eq_zpow (Ïˆ : AddChar G R) (n : â„¤) (a : G) : Ïˆ (n â€¢ a) = Ïˆ a ^ n :=
  map_zpow Ïˆ.toMonoidHom _ _

lemma map_neg_eq_inv (Ïˆ : AddChar G R) (x : G) : Ïˆ (-x) = (Ïˆ x)â»Â¹ :=
  eq_inv_of_mul_eq_one_left $ by simp [â† map_add_mul]

lemma neg_apply' (Ïˆ : AddChar G R) (x : G) : (-Ïˆ) x = (Ïˆ x)â»Â¹ :=
  map_neg_eq_inv _ _

lemma sub_apply' (Ïˆ Ï‡ : AddChar G R) (a : G) : (Ïˆ - Ï‡) a = Ïˆ a / Ï‡ a := by
  rw [sub_apply, map_neg_eq_inv, div_eq_mul_inv]

end DivisionCommMonoid

section IsROrC
variable [IsROrC R] {Ïˆâ‚ Ïˆâ‚‚ : AddChar G R}

lemma map_neg_eq_conj [Finite G] (Ïˆ : AddChar G R) (x : G) : Ïˆ (-x) = conj (Ïˆ x) := by
  rw [map_neg_eq_inv, IsROrC.inv_eq_conj $ norm_apply _ _]

lemma l2Inner_eq [Fintype G] (Ïˆâ‚ Ïˆâ‚‚ : AddChar G R) :
    âŸª(Ïˆâ‚ : G â†’ R), Ïˆâ‚‚âŸ«_[R] = if Ïˆâ‚ = Ïˆâ‚‚ then â†‘(card G) else 0 := by
  split_ifs with h
  Â· rw [h, AddChar.l2Inner_self]
  have : Ïˆâ‚â»Â¹ * Ïˆâ‚‚ â‰  1 := by rwa [Ne.def, inv_mul_eq_one]
  simp_rw [l2Inner_eq_sum, â†inv_apply_eq_conj]
  simpa [map_neg_eq_inv] using sum_eq_zero_iff_ne_zero.2 this

lemma l2Inner_eq_zero_iff_ne [Fintype G] : âŸª(Ïˆâ‚ : G â†’ R), Ïˆâ‚‚âŸ«_[R] = 0 â†” Ïˆâ‚ â‰  Ïˆâ‚‚ := by
  rw [l2Inner_eq, Ne.ite_eq_right_iff (Nat.cast_ne_zero.2 Fintype.card_ne_zero)]

lemma l2Inner_eq_card_iff_eq [Fintype G] : âŸª(Ïˆâ‚ : G â†’ R), Ïˆâ‚‚âŸ«_[R] = card G â†” Ïˆâ‚ = Ïˆâ‚‚ := by
  rw [l2Inner_eq, Ne.ite_eq_left_iff (Nat.cast_ne_zero.2 Fintype.card_ne_zero)]

variable (G R)

protected lemma linearIndependent [Finite G] : LinearIndependent R ((â‡‘) : AddChar G R â†’ G â†’ R) := by
  cases nonempty_fintype G
  exact linearIndependent_of_ne_zero_of_l2Inner_eq_zero AddChar.coe_ne_zero fun Ïˆâ‚ Ïˆâ‚‚ â†¦
    l2Inner_eq_zero_iff_ne.2

noncomputable instance instFintype [Finite G] : Fintype (AddChar G R) :=
  @Fintype.ofFinite _ (AddChar.linearIndependent G R).finite

@[simp] lemma card_addChar_le [Fintype G] : card (AddChar G R) â‰¤ card G := by
  simpa only [FiniteDimensional.finrank_fintype_fun_eq_card] using
    (AddChar.linearIndependent G R).fintype_card_le_finrank

end IsROrC

end AddCommGroup

section DirectSum
variable {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [DecidableEq Î¹] [âˆ€ i, AddCommGroup (Ï€ i)] [CommMonoid R]

/-- Direct sum of additive characters. -/
protected def directSum (Ïˆ : âˆ€ i, AddChar (Ï€ i) R) : AddChar (â¨ i, Ï€ i) R :=
  AddChar.toAddMonoidHomEquiv'.symm
    (DirectSum.toAddMonoid fun i â†¦ toAddMonoidHomEquiv' (Ïˆ i) : (â¨ i, Ï€ i) â†’+ Additive R)

lemma directSum_injective :
    Injective (AddChar.directSum : (âˆ€ i, AddChar (Ï€ i) R) â†’ AddChar (â¨ i, Ï€ i) R) := by
  refine' AddChar.toAddMonoidHomEquiv'.symm.injective.comp $ DirectSum.toAddMonoid_injective.comp _
  rintro Ïˆ Ï‡ h
  simpa [Function.funext_iff] using h

end DirectSum
end AddChar
