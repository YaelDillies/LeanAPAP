import Mathlib.GroupTheory.FiniteAbelian
import LeanAPAP.Mathlib.Data.ZMod.Basic
import LeanAPAP.Prereqs.AddChar.Circle

/-!
# Pontryagin duality for finite abelian groups

This file proves the Pontryagin duality in case of finite abelian groups. This states that any
finite abelian group is canonically isomorphic to its double dual (the space of complex-valued
characters of its space of complex-valued characters).

We first prove it for `ZMod n` and then extend to all finite abelian groups using the
Structure Theorem.
-/

noncomputable section

open circle Circle Finset Function Multiplicative
open Fintype (card)
open scoped BigOperators DirectSum

variable {Œ± : Type*} [AddCommGroup Œ±] {n : ‚Ñï} {a b : Œ±}

namespace AddChar

private def zmodAuxAux (n : ‚Ñï) : ‚Ñ§ ‚Üí+ Additive circle where
  toFun x := Additive.ofMul (e $ x / n)
  map_zero' := by dsimp; rw [Int.cast_zero, zero_div, ofMul_eq_zero, map_zero_one]
  map_add' x y := by rw [‚ÜêofMul_mul, Equiv.apply_eq_iff_eq, Int.cast_add, add_div, map_add_mul]

@[simp]
lemma zmodAuxAux_apply (n : ‚Ñï) (z : ‚Ñ§) : zmodAuxAux n z = Additive.ofMul (e $ z / n) := rfl

/-- The character sending `k : ZMod n` to `e ^ (2 * œÄ * i * k / n)`. -/
private def zmodAux (n : ‚Ñï) : AddChar (ZMod n) circle :=
  AddChar.toAddMonoidHomEquiv'.symm $ ZMod.lift n ‚ü®zmodAuxAux n, by
    obtain hn | hn := eq_or_ne (n : ‚Ñù) 0 <;> simp [hn, zmodAuxAux]‚ü©

--TODO: Heavily generalise. Ya√´l's attempts at generalising failed :(
@[simp] lemma aux (n : ‚Ñï) (h) :
    (‚ü®zmodAuxAux n, h‚ü© : {f : ‚Ñ§ ‚Üí+ Additive circle // f n = 0}) = zmodAuxAux n := rfl

@[simp] lemma zmodAux_apply (n : ‚Ñï) (z : ‚Ñ§) : zmodAux n z = e (z / n) := by simp [zmodAux]

-- probably an evil lemma
-- @[simp] lemma zmodAux_apply (n : ‚Ñï) (x : ZMod n) : zmodAux n x = e (x / n) :=
-- by simp [zmodAux]

lemma zmodAux_injective (hn : n ‚â† 0) : Injective (zmodAux n) := by
  replace hn : (n : ‚Ñù) ‚â† 0 := Nat.cast_ne_zero.2 hn
  simp [zmodAux, ZMod.lift_injective, CharP.intCast_eq_zero_iff _ n, e_eq_one, div_eq_iff hn,
    mul_comm _ (n : ‚Ñù), -forall_exists_index]
  norm_cast
  exact fun _ ‚Ü¶ id

/-- Indexing of the complex characters of `ZMod n`. `AddChar.zmod n x` is the character sending `y`
to `e ^ (2 * œÄ * i * x * y / n)`. -/
def zmod (n : ‚Ñï) (x : ZMod n) : AddChar (ZMod n) circle :=
  (zmodAux n).compAddMonoidHom $ AddMonoidHom.mulLeft x

@[simp] lemma zmod_apply (n : ‚Ñï) (x y : ‚Ñ§) :
    (zmod n x) (y : ZMod n) = e (x * y / n) := by
  simp [zmod, ‚ÜêInt.cast_mul x y, -Int.cast_mul]

@[simp] lemma zmod_zero (n : ‚Ñï) : zmod n 0 = 1 := by
  refine DFunLike.ext _ _ ?_
  rw [ZMod.intCast_surjective.forall]
  rintro y
  simpa using zmod_apply n 0 y

@[simp] lemma zmod_add (n : ‚Ñï) : ‚àÄ x y : ZMod n, zmod n (x + y) = zmod n x * zmod n y := by
  simp only [DFunLike.ext_iff, ZMod.intCast_surjective.forall, ‚ÜêInt.cast_add, AddChar.mul_apply,
    zmod_apply]
  simp [add_mul, add_div, map_add_mul]

-- probably an evil lemma
-- @[simp] lemma zmod_apply (n : ‚Ñï) (x y : ZMod n) : zmod n x y = e (x * y / n) :=
-- by simp [addChar.zmod, ZMod.coe_mul]

lemma zmod_injective (hn : n ‚â† 0) : Injective (zmod n) := by
  simp_rw [Injective, ZMod.intCast_surjective.forall]
  rintro x y h
  replace hn : (n : ‚Ñù) ‚â† 0 := by positivity
  simpa only [Int.cast_one, mul_one, one_mul, e_inj, AddCommGroup.div_modEq_div hn,
    AddCommGroup.intCast_modEq_intCast', AddCommGroup.modEq_iff_int_modEq,
    CharP.intCast_eq_intCast (ZMod n) n] using (zmod_apply _ _ _).symm.trans $
    (DFunLike.congr_fun h ((1 : ‚Ñ§) : ZMod n)).trans $ zmod_apply _ _ _

@[simp] lemma zmod_inj (hn : n ‚â† 0) {x y : ZMod n} : zmod n x = zmod n y ‚Üî x = y :=
  (zmod_injective hn).eq_iff

def zmodHom (n : ‚Ñï) : AddChar (ZMod n) (AddChar (ZMod n) circle) where
  toFun := zmod n
  map_zero_one' := by simp
  map_add_mul' := by simp

def mkZModAux {Œπ : Type} [DecidableEq Œπ] (n : Œπ ‚Üí ‚Ñï) (u : ‚àÄ i, ZMod (n i)) :
    AddChar (‚®Å i, ZMod (n i)) circle :=
  AddChar.directSum fun i ‚Ü¶ zmod (n i) (u i)

lemma mkZModAux_injective {Œπ : Type} [DecidableEq Œπ] {n : Œπ ‚Üí ‚Ñï} (hn : ‚àÄ i, n i ‚â† 0) :
    Injective (mkZModAux n) :=
  AddChar.directSum_injective.comp fun f g h ‚Ü¶ by simpa [Function.funext_iff, hn] using h

/-- The circle-valued characters of a finite abelian group are the same as its complex-valued
characters. -/
def circleEquivComplex [Finite Œ±] : AddChar Œ± circle ‚âÉ+ AddChar Œ± ‚ÑÇ where
  toFun œà := toMonoidHomEquiv'.symm $ circle.subtype.comp œà.toMonoidHom
  invFun œà :=
    { toFun := fun a ‚Ü¶ (‚ü®œà a, mem_circle_iff_abs.2 $ œà.norm_apply _‚ü© : circle)
      map_zero_one' := by simp
      map_add_mul' := fun a b ‚Ü¶ by ext : 1; simp [map_add_mul] }
  left_inv œà := by ext : 1; simp
  right_inv œà := by ext : 1; simp
  map_add' œà œá := rfl

@[simp] lemma card_eq [Fintype Œ±] : card (AddChar Œ± ‚ÑÇ) = card Œ± := by
  obtain ‚ü®Œπ, _, n, hn, ‚ü®e‚ü©‚ü© := AddCommGroup.equiv_directSum_zmod_of_finite' Œ±
  skip
  classical
  have hn' : ‚àÄ i, n i ‚â† 0 := fun i ‚Ü¶ by have := hn i; positivity
  let f : Œ± ‚Üí AddChar Œ± ‚ÑÇ := fun a ‚Ü¶
    circle.subtype.compAddChar ((mkZModAux n $ e a).compAddMonoidHom e)
  have hf : Injective f := circleEquivComplex.injective.comp
    ((compAddMonoidHom_injective_left _ e.surjective).comp $ (mkZModAux_injective hn').comp $
      DFunLike.coe_injective.comp $ e.injective.comp Additive.ofMul.injective)
  exact (card_addChar_le _ _).antisymm (Fintype.card_le_of_injective _ hf)

/-- `ZMod n` is (noncanonically) isomorphic to its group of characters. -/
def zmodAddEquiv (hn : n ‚â† 0) : ZMod n ‚âÉ+ AddChar (ZMod n) ‚ÑÇ := by
  haveI : NeZero n := ‚ü®hn‚ü©
  refine' AddEquiv.ofBijective
    (circleEquivComplex.toAddMonoidHom.comp $ AddChar.toAddMonoidHom $ zmodHom n) _
  rw [Fintype.bijective_iff_injective_and_card, card_eq]
  exact ‚ü®circleEquivComplex.injective.comp $ zmod_injective hn, rfl‚ü©

@[simp] lemma zmodAddEquiv_apply [NeZero n] (x : ZMod n) :
    zmodAddEquiv (NeZero.ne _) x = circleEquivComplex (zmod n x) := rfl

section Finite
variable (Œ±) [Finite Œ±]

/-- Complex-valued characters of a finite abelian group `Œ±` form a basis of `Œ± ‚Üí ‚ÑÇ`. -/
def complexBasis : Basis (AddChar Œ± ‚ÑÇ) ‚ÑÇ (Œ± ‚Üí ‚ÑÇ) :=
  basisOfLinearIndependentOfCardEqFinrank (AddChar.linearIndependent _ _) $ by
    cases nonempty_fintype Œ±; rw [card_eq, FiniteDimensional.finrank_fintype_fun_eq_card]

@[simp, norm_cast]
lemma coe_complexBasis : ‚áë(complexBasis Œ±) = ((‚áë) : AddChar Œ± ‚ÑÇ ‚Üí Œ± ‚Üí ‚ÑÇ) := by
  rw [complexBasis, coe_basisOfLinearIndependentOfCardEqFinrank]

variable {Œ±}

@[simp]
lemma complexBasis_apply (œà : AddChar Œ± ‚ÑÇ) : complexBasis Œ± œà = œà := by rw [coe_complexBasis]

lemma exists_apply_ne_zero : (‚àÉ œà : AddChar Œ± ‚ÑÇ, œà a ‚â† 1) ‚Üî a ‚â† 0 := by
  refine' ‚ü®_, fun ha ‚Ü¶ _‚ü©
  ¬∑ rintro ‚ü®œà, hœà‚ü© rfl
    exact hœà œà.map_zero_one
  classical
  by_contra! h
  let f : Œ± ‚Üí ‚ÑÇ := fun b ‚Ü¶ if a = b then 1 else 0
  have h‚ÇÄ := congr_fun ((complexBasis Œ±).sum_repr f) 0
  have h‚ÇÅ := congr_fun ((complexBasis Œ±).sum_repr f) a
  simp only [complexBasis_apply, Fintype.sum_apply, Pi.smul_apply, h, smul_eq_mul, mul_one,
    map_zero_one, if_pos rfl, if_neg ha, f] at h‚ÇÄ h‚ÇÅ
  exact one_ne_zero (h‚ÇÅ.symm.trans h‚ÇÄ)

lemma forall_apply_eq_zero : (‚àÄ œà : AddChar Œ± ‚ÑÇ, œà a = 1) ‚Üî a = 0 := by
  simpa using exists_apply_ne_zero.not

lemma doubleDualEmb_injective : Injective (doubleDualEmb : Œ± ‚Üí AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) :=
  doubleDualEmb.ker_eq_bot_iff.1 $ eq_bot_iff.2 fun a ha ‚Ü¶
    forall_apply_eq_zero.1 fun œà ‚Ü¶ by simpa using DFunLike.congr_fun ha (Additive.ofMul œà)

lemma doubleDualEmb_bijective : Bijective (doubleDualEmb : Œ± ‚Üí AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) := by
  cases nonempty_fintype Œ±
  exact (Fintype.bijective_iff_injective_and_card _).2
    ‚ü®doubleDualEmb_injective, card_eq.symm.trans card_eq.symm‚ü©

@[simp]
lemma doubleDualEmb_inj : (doubleDualEmb a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) = doubleDualEmb b ‚Üî a = b :=
  doubleDualEmb_injective.eq_iff

@[simp] lemma doubleDualEmb_eq_zero : (doubleDualEmb a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) = 0 ‚Üî a = 0 := by
  rw [‚Üêmap_zero doubleDualEmb, doubleDualEmb_inj]

lemma doubleDualEmb_ne_zero : (doubleDualEmb a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) ‚â† 0 ‚Üî a ‚â† 0 :=
  doubleDualEmb_eq_zero.not

/-- The double dual isomorphism. -/
def doubleDualEquiv : Œ± ‚âÉ+ AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ :=
  AddEquiv.ofBijective _ doubleDualEmb_bijective

@[simp]
lemma coe_doubleDualEquiv : ‚áë(doubleDualEquiv : Œ± ‚âÉ+ AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) = doubleDualEmb := rfl

@[simp] lemma doubleDualEmb_doubleDualEquiv_symm_apply (a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) :
    doubleDualEmb (doubleDualEquiv.symm a) = a :=
  doubleDualEquiv.apply_symm_apply _

@[simp] lemma doubleDualEquiv_symm_doubleDualEmb_apply (a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ) :
    doubleDualEquiv.symm (doubleDualEmb a) = a := doubleDualEquiv.symm_apply_apply _

end Finite

lemma sum_apply_eq_ite [Fintype Œ±] [DecidableEq Œ±] (a : Œ±) :
    ‚àë œà : AddChar Œ± ‚ÑÇ, œà a = if a = 0 then (Fintype.card Œ± : ‚ÑÇ) else 0 := by
  simpa using sum_eq_ite (doubleDualEmb a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ)

lemma expect_apply_eq_ite [Fintype Œ±] [DecidableEq Œ±] (a : Œ±) :
    ùîº œà : AddChar Œ± ‚ÑÇ, œà a = if a = 0 then 1 else 0 := by
  simpa using expect_eq_ite (doubleDualEmb a : AddChar (AddChar Œ± ‚ÑÇ) ‚ÑÇ)

lemma sum_apply_eq_zero_iff_ne_zero [Finite Œ±] : ‚àë œà : AddChar Œ± ‚ÑÇ, œà a = 0 ‚Üî a ‚â† 0 := by
  classical
  cases nonempty_fintype Œ±
  rw [sum_apply_eq_ite, Ne.ite_eq_right_iff]
  exact Nat.cast_ne_zero.2 Fintype.card_ne_zero

lemma sum_apply_ne_zero_iff_eq_zero [Finite Œ±] : ‚àë œà : AddChar Œ± ‚ÑÇ, œà a ‚â† 0 ‚Üî a = 0 :=
  sum_apply_eq_zero_iff_ne_zero.not_left

lemma expect_apply_eq_zero_iff_ne_zero [Finite Œ±] : ùîº œà : AddChar Œ± ‚ÑÇ, œà a = 0 ‚Üî a ‚â† 0 := by
  classical
  cases nonempty_fintype Œ±
  rw [expect_apply_eq_ite, one_ne_zero.ite_eq_right_iff]

lemma expect_apply_ne_zero_iff_eq_zero [Finite Œ±] : ùîº œà : AddChar Œ± ‚ÑÇ, œà a ‚â† 0 ‚Üî a = 0 :=
  expect_apply_eq_zero_iff_ne_zero.not_left

end AddChar
