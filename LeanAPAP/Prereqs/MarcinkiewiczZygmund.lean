/-
Copyright (c) 2023 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.MeanInequalitiesPow
import Mathlib.Data.Nat.Choose.Multinomial
import Mathlib.Tactic.Positivity.Finset

/-!
# The Marcinkiewicz-Zygmund inequality

This file proves the Marcinkiewicz-Zygmund inequality.
-/

open Finset Fintype Nat Real
variable {Î¹ : Type*} {A : Finset Î¹} {m n : â„•}

local notation:70 s:70 " ^^ " n:71 => Fintype.piFinset fun _ : Fin n â†¦ s

private lemma step_one (hA : A.Nonempty) (f : Î¹ â†’ â„) (a : Fin n â†’ Î¹)
    (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    |âˆ‘ i, f (a i)| ^ (m + 1) â‰¤
      (âˆ‘ b in A ^^ n, |âˆ‘ i, (f (a i) - f (b i))| ^ (m + 1)) / A.card ^ n := by
  let B := A ^^ n
  calc
    |âˆ‘ i, f (a i)| ^ (m + 1)
      = |âˆ‘ i, (f (a i) - (âˆ‘ b in B, f (b i)) / B.card)| ^ (m + 1) := by
      simp only [hf, sub_zero, zero_div]
    _ = |(âˆ‘ b in B, âˆ‘ i, (f (a i) - f (b i))) / B.card| ^ (m + 1) := by
      simp only [sum_sub_distrib]
      rw [sum_const, sub_div, sum_comm, sum_div, nsmul_eq_mul, card_piFinset, prod_const,
        Finset.card_univ, Fintype.card_fin, Nat.cast_pow, mul_div_cancel_leftâ‚€]
      positivity
    _ = |âˆ‘ b in B, âˆ‘ i, (f (a i) - f (b i))| ^ (m + 1) / B.card ^ (m + 1) := by
      rw [abs_div, div_pow, Nat.abs_cast]
    _ â‰¤ (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))|) ^ (m + 1) / B.card ^ (m + 1) := by
      gcongr; exact IsAbsoluteValue.abv_sum _ _ _
    _ = (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))|) ^ (m + 1) / B.card ^ m / B.card := by
      rw [div_div, â† _root_.pow_succ]
    _ â‰¤ (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))| ^ (m + 1)) / B.card := by
      gcongr; exact pow_sum_div_card_le_sum_pow (fun _ _ â†¦ abs_nonneg _) _
    _ = _ := by simp [B]

private lemma step_one' (hA : A.Nonempty) (f : Î¹ â†’ â„) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) (m : â„•)
    (a : Fin n â†’ Î¹) :
    |âˆ‘ i, f (a i)| ^ m â‰¤ (âˆ‘ b in A ^^ n, |âˆ‘ i, (f (a i) - f (b i))| ^ m) / A.card ^ n := by
  cases m
  Â· simp only [_root_.pow_zero, sum_const, prod_const, Nat.smul_one_eq_cast, Finset.card_fin,
      card_piFinset, â† Nat.cast_pow]
    rw [div_self]
    rw [Nat.cast_ne_zero, â† pos_iff_ne_zero]
    exact pow_pos (Finset.card_pos.2 hA) _
  exact step_one hA f a hf

-- works with this
-- private lemma step_two_aux' {Î² Î³ : Type*} [AddCommMonoid Î²] [CommRing Î³]
--   (f : (Fin n â†’ Î¹) â†’ (Fin n â†’ Î³)) (Îµ : Fin n â†’ Î³)
--   (hÎµ : âˆ€ i, Îµ i = -1 âˆ¨ Îµ i = 1) (g : (Fin n â†’ Î³) â†’ Î²) :
--   âˆ‘ a b in A ^^ n, g (Îµ * (f a - f b)) = âˆ‘ a b in A ^^ n, g (f a - f b) :=
-- feels like could generalise more...
-- the key point is that you combine the double sums into a single sum, and do a pair swap
-- when the corresponding Îµ is -1
-- but the order here is a bit subtle (ie this explanation is an oversimplification)
private lemma step_two_aux (A : Finset Î¹) (f : Î¹ â†’ â„) (Îµ : Fin n â†’ â„)
    (hÎµ : Îµ âˆˆ ({-1, 1} : Finset â„)^^n) (g : (Fin n â†’ â„) â†’ â„) :
    âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, g (Îµ * (f âˆ˜ a - f âˆ˜ b)) =
      âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, g (f âˆ˜ a - f âˆ˜ b) := by
  rw [â† sum_product', â† sum_product']
  let swapper : (Fin n â†’ Î¹) Ã— (Fin n â†’ Î¹) â†’ (Fin n â†’ Î¹) Ã— (Fin n â†’ Î¹) := by
    intro xy
    exact (fun i â†¦ if Îµ i = 1 then xy.1 i else xy.2 i, fun i â†¦ if Îµ i = 1 then xy.2 i else xy.1 i)
  have hâ‚ : âˆ€ a âˆˆ (A ^^ n) Ã—Ë¢ (A ^^ n), swapper a âˆˆ (A ^^ n) Ã—Ë¢ (A ^^ n) := by
    simp only [mem_product, and_imp, mem_piFinset, â† forall_and]
    intro a h i
    split_ifs
    Â· exact h i
    Â· exact (h i).symm
  have hâ‚‚ : âˆ€ a âˆˆ (A ^^ n) Ã—Ë¢ (A ^^ n), swapper (swapper a) = a := fun a _ â†¦ by
    ext <;> simp only <;> split_ifs <;> rfl
  refine sum_nbij' swapper swapper hâ‚ hâ‚ hâ‚‚ hâ‚‚ ?_
  Â· rintro âŸ¨a, bâŸ© _
    congr with i : 1
    simp only [Pi.mul_apply, Pi.sub_apply, Function.comp_apply]
    simp only [mem_piFinset, mem_insert, mem_singleton] at hÎµ
    split_ifs with h
    Â· simp [h]
    rw [(hÎµ i).resolve_right h]
    ring

private lemma step_two (f : Î¹ â†’ â„) :
    âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, (âˆ‘ i, (f (a i) - f (b i))) ^ (2 * m) =
      2â»Â¹ ^ n * âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n,
        âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, (âˆ‘ i, Îµ i * (f (a i) - f (b i))) ^ (2 * m) := by
  let B := A ^^ n
  have : âˆ€ Îµ âˆˆ ({-1, 1} : Finset â„)^^n,
    âˆ‘ a in B, âˆ‘ b in B, (âˆ‘ i, Îµ i * (f (a i) - f (b i))) ^ (2 * m) =
      âˆ‘ a in B, âˆ‘ b in B, (âˆ‘ i, (f (a i) - f (b i))) ^ (2 * m) :=
    fun Îµ hÎµ â†¦ step_two_aux A f _ hÎµ fun z : Fin n â†’ â„ â†¦ univ.sum z ^ (2 * m)
  rw [Finset.sum_congr rfl this, sum_const, card_piFinset_const, card_pair, nsmul_eq_mul,
    Nat.cast_pow, Nat.cast_two, inv_pow, inv_mul_cancel_leftâ‚€]
  Â· positivity
  Â· norm_num

private lemma step_three (f : Î¹ â†’ â„) :
    âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n,
      âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, (âˆ‘ i, Îµ i * (f (a i) - f (b i))) ^ (2 * m) =
      âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, âˆ‘ k in piAntidiag univ (2 * m),
          (multinomial univ k * âˆ t, (f (a t) - f (b t)) ^ k t) *
            âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n, âˆ t, Îµ t ^ k t := by
  simp only [@sum_comm _ _ (Fin n â†’ â„) _ _ (A ^^ n), â† Complex.abs_pow, sum_pow_eq_sum_piAntidiag]
  refine sum_congr rfl fun a _ â†¦ ?_
  refine sum_congr rfl fun b _ â†¦ ?_
  simp only [mul_pow, prod_mul_distrib, @sum_comm _ _ (Fin n â†’ â„), â† mul_sum, â† sum_mul]
  refine sum_congr rfl fun k _ â†¦ ?_
  rw [â† mul_assoc, mul_right_comm]

private lemma step_four {k : Fin n â†’ â„•} :
    âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n, âˆ t, Îµ t ^ k t = 2 ^ n * ite (âˆ€ i, Even (k i)) 1 0 := by
  have := sum_prod_piFinset ({-1, 1} : Finset â„) fun i fi â†¦ fi ^ k i
  rw [this, â† Fintype.prod_boole]
  have : (2 : â„) ^ n = âˆ i : Fin n, 2 := by simp
  rw [this, â† prod_mul_distrib]
  refine prod_congr rfl fun t _ â†¦ ?_
  rw [sum_pair, one_pow, mul_boole]
  swap
  Â· norm_num
  split_ifs with h
  Â· rw [h.neg_one_pow, one_add_one_eq_two]
  rw [Nat.not_even_iff_odd] at h
  simp [h.neg_one_pow]

-- double_multinomial
private lemma step_six {f : Î¹ â†’ â„} {a b : Fin n â†’ Î¹} :
    âˆ‘ k : Fin n â†’ â„• in piAntidiag univ m,
        (multinomial univ fun a â†¦ 2 * k a : â„) * âˆ i, (f (a i) - f (b i)) ^ (2 * k i) â‰¤
      m ^ m * (âˆ‘ i, (f (a i) - f (b i)) ^ 2) ^ m := by
  rw [sum_pow_eq_sum_piAntidiag, mul_sum]
  convert sum_le_sum fun k hk â†¦ _
  rw [mem_piAntidiag] at hk
  simp only [â† mul_assoc, pow_mul]
  gcongr
  norm_cast
  refine multinomial_two_mul_le_mul_multinomial.trans ?_
  rw [hk.1]

private lemma step_seven {f : Î¹ â†’ â„} {a b : Fin n â†’ Î¹} :
    m ^ m * (âˆ‘ i, (f (a i) - f (b i)) ^ 2 : â„) ^ m â‰¤
      m ^ m * 2 ^ m * (âˆ‘ i, (f (a i) ^ 2 + f (b i) ^ 2)) ^ m := by
  rw [â† mul_pow, â† mul_pow, â† mul_pow, mul_assoc, mul_sum _ _ (2 : â„)]
  gcongr with i
  exact add_sq_le.trans_eq (by simp)

private lemma step_eight {f : Î¹ â†’ â„} {a b : Fin n â†’ Î¹} :
    m ^ m * 2 ^ m * (âˆ‘ i, (f (a i) ^ 2 + f (b i) ^ 2)) ^ m â‰¤
      m ^ m * 2 ^ (m + (m - 1)) *
        ((âˆ‘ i, f (a i) ^ 2) ^ m + (âˆ‘ i, f (b i) ^ 2) ^ m) := by
  rw [pow_add, â† mul_assoc _ _ (2 ^ _), mul_assoc _ (2 ^ (m - 1)), sum_add_distrib]
  gcongr
  refine add_pow_le ?_ ?_ m <;> positivity

private lemma end_step {f : Î¹ â†’ â„} (hm : 1 â‰¤ m) (hA : A.Nonempty) :
    (âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, âˆ‘ k in piAntidiag univ m,
      â†‘(multinomial univ fun i â†¦ 2 * k i) * âˆ t, (f (a t) - f (b t)) ^ (2 * k t)) / A.card ^ n
        â‰¤ (4 * m) ^ m * âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i) ^ 2) ^ m := by
  let B := A ^^ n
  calc
    (âˆ‘ a in B, âˆ‘ b in B, âˆ‘ k : Fin n â†’ â„• in piAntidiag univ m,
      (multinomial univ fun i â†¦ 2 * k i : â„) * âˆ t, (f (a t) - f (b t)) ^ (2 * k t)) / A.card ^ n
    _ â‰¤ (âˆ‘ a in B, âˆ‘ b in B, m ^ m * 2 ^ (m + (m - 1)) *
          ((âˆ‘ i, f (a i) ^ 2) ^ m + (âˆ‘ i, f (b i) ^ 2) ^ m) : â„) / A.card ^ n := by
      gcongr; exact step_six.trans $ step_seven.trans step_eight
    _ = _ := by
      simp only [mul_add, sum_add_distrib, sum_const, nsmul_eq_mul, â† mul_sum]
      rw [â† mul_add, â† two_mul, â† mul_assoc 2, â† mul_assoc 2, mul_right_comm 2, â† _root_.pow_succ',
        add_assoc, Nat.sub_add_cancel hm, pow_add, â† mul_pow, â† mul_pow, card_piFinset, prod_const,
        Finset.card_univ, Fintype.card_fin, Nat.cast_pow, mul_div_cancel_leftâ‚€]
      norm_num
      positivity

namespace Real

/-- The **Marcinkiewicz-Zygmund inequality** for real-valued functions, with a slightly better
constant than `Real.marcinkiewicz_zygmund`. -/
theorem marcinkiewicz_zygmund' (m : â„•) (f : Î¹ â†’ â„) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i)) ^ (2 * m) â‰¤
      (4 * m) ^ m * âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i) ^ 2) ^ m := by
  obtain rfl | hm := m.eq_zero_or_pos
  Â· simp
  have hm' : 1 â‰¤ m := by rwa [Nat.succ_le_iff]
  obtain rfl | hA := A.eq_empty_or_nonempty
  Â· cases n <;> cases m <;> simp
  let B := A ^^ n
  calc
    âˆ‘ a in B, (âˆ‘ i, f (a i)) ^ (2 * m)
      â‰¤ âˆ‘ a in A ^^ n, (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))| ^ (2 * m))/ A.card ^ n := by
      gcongr; simpa [pow_mul, sq_abs] using step_one' hA f hf (2 * m) _
    _ â‰¤ _ := ?_
  rw [â† sum_div]
  simp only [pow_mul, sq_abs]
  simp only [â† pow_mul]
  rw [step_two, step_three, mul_comm, inv_pow, â† div_eq_mul_inv, div_div]
  simp only [step_four, mul_ite, mul_zero, mul_one, â† sum_filter, â† sum_mul]
  rw [mul_comm, mul_div_mul_left]
  swap; Â· positivity
  simpa only [even_iff_two_dvd, â† map_nsmul_piAntidiag_univ _ two_ne_zero, sum_map,
    Function.Embedding.coeFn_mk] using end_step hm' hA

/-- The **Marcinkiewicz-Zygmund inequality** for real-valued functions, with a slightly easier to
bound constant than `Real.marcinkiewicz_zygmund'`.

Note that `RCLike.marcinkiewicz_zygmund` is another version that works for both `â„` and `â„‚` at the
expense of a slightly worse constant. -/
theorem marcinkiewicz_zygmund (hm : m â‰  0) (f : Î¹ â†’ â„) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i)) ^ (2 * m) â‰¤
      (4 * m) ^ m * n ^ (m - 1) * âˆ‘ a in A ^^ n, âˆ‘ i, f (a i) ^ (2 * m) := by
  obtain _ | m := m
  Â· simp at hm
  obtain rfl | hn := n.eq_zero_or_pos
  Â· simp
  calc
    âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i)) ^ (2 * (m + 1))
      â‰¤ (4 * â†‘(m + 1)) ^ (m + 1) * âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i) ^ 2) ^ (m + 1) :=
      marcinkiewicz_zygmund' _ f hf
    _ â‰¤ (4 * â†‘(m + 1)) ^ (m + 1) * (âˆ‘ a in A ^^ n, n ^ m * âˆ‘ i, f (a i) ^ (2 * (m + 1))) := ?_
    _ â‰¤ (4 * â†‘(m + 1)) ^ (m + 1) * n ^ m * âˆ‘ a in A ^^ n, âˆ‘ i, f (a i) ^ (2 * (m + 1)) := by
      simp_rw [mul_assoc, mul_sum]; rfl
  gcongr with a
  rw [â† div_le_iffâ‚€']
  have := pow_sum_div_card_le_sum_pow (f := fun i â†¦ f (a i) ^ 2) (s := univ) (fun i _ â†¦ ?_) m
  simpa only [Finset.card_fin, pow_mul] using this
  all_goals { positivity }

end Real

namespace RCLike
variable {ğ•œ : Type*} [RCLike ğ•œ]

/-- The **Marcinkiewicz-Zygmund inequality** for real- or complex-valued functions. -/
lemma marcinkiewicz_zygmund (hm : m â‰  0) (f : Î¹ â†’ ğ•œ) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, â€–âˆ‘ i, f (a i)â€– ^ (2 * m) â‰¤
      (8 * m) ^ m * n ^ (m - 1) * âˆ‘ a in A ^^ n, âˆ‘ i, â€–f (a i)â€– ^ (2 * m) := by
  let fâ‚ x : â„ := re (f x)
  let fâ‚‚ x : â„ := im (f x)
  let B := A ^^ n
  have hfâ‚ i : âˆ‘ a in B, fâ‚ (a i) = 0 := by rw [â† map_sum, hf, map_zero]
  have hfâ‚‚ i : âˆ‘ a in B, fâ‚‚ (a i) = 0 := by rw [â† map_sum, hf, map_zero]
  have hâ‚ := Real.marcinkiewicz_zygmund hm _ hfâ‚
  have hâ‚‚ := Real.marcinkiewicz_zygmund hm _ hfâ‚‚
  simp only [pow_mul, RCLike.norm_sq_eq_def]
  simp only [â† sq, map_sum, map_sum]
  calc
    âˆ‘ a in B, ((âˆ‘ i, re (f (a i))) ^ 2 + (âˆ‘ i, im (f (a i))) ^ 2) ^ m â‰¤
        âˆ‘ a in B,
          2 ^ (m - 1) * (((âˆ‘ i, re (f (a i))) ^ 2) ^ m + ((âˆ‘ i, im (f (a i))) ^ 2) ^ m) := by
      gcongr with a; apply add_pow_le <;> positivity
    _ = 2 ^ (m - 1) * (âˆ‘ a in B, (âˆ‘ i, re (f (a i))) ^ (2 * m) +
          âˆ‘ a in B, (âˆ‘ i, im (f (a i))) ^ (2 * m)) := by
      simp only [â† sum_add_distrib, mul_sum, pow_mul]
    _ â‰¤ 2 ^ (m - 1) * ((4 * m) ^ m * n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, re (f (a i)) ^ (2 * m) + (4 * m) ^ m * n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, im (f (a i)) ^ (2 * m)) := by gcongr
    _ = 2 ^ (m - 1) * ((4 * m) ^ m * n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, (re (f (a i)) ^ (2 * m) + im (f (a i)) ^ (2 * m))) := by
      simp_rw [sum_add_distrib, mul_add]
    _ â‰¤ 2 ^ (m - 1) * ((4 * m) ^ m * n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, 2 * (re (f (a i)) ^ 2 + im (f (a i)) ^ 2) ^ m) := by
      simp_rw [pow_mul]; gcongr; apply pow_add_pow_le' <;> positivity
    _ = (8 * m) ^ m * n ^ (m - 1) * âˆ‘ a in B, âˆ‘ i, (re (f (a i)) ^ 2 + im (f (a i)) ^ 2) ^ m := by
      simp_rw [â† mul_sum, show (8 : â„) = 2 * 4 by norm_num, mul_pow, â† pow_sub_one_mul hm (2 : â„)]
      ring

end RCLike
