import Mathlib.Algebra.Order.Star.Conjneg
import Mathlib.Analysis.RCLike.Inner
import Mathlib.Data.Real.StarOrdered
import LeanAPAP.Prereqs.Convolution.Discrete.Defs
import LeanAPAP.Prereqs.LpNorm.Discrete.Basic

/-!
# Norm of a convolution

This file characterises the L1-norm of the convolution of two functions and proves the Young
convolution inequality.
-/

open Finset Function MeasureTheory RCLike Real
open scoped ComplexConjugate ENNReal NNReal Pointwise translate

variable {G ğ•œ : Type*} [Fintype G] [DecidableEq G] [AddCommGroup G]

section RCLike
variable [RCLike ğ•œ] {p : â„â‰¥0âˆ}

lemma conv_eq_wInner_one (f g : G â†’ ğ•œ) (a : G) : (f âˆ— g) a = âŸªconj f, Ï„ a fun x â†¦ g (-x)âŸ«_[ğ•œ] := by
  simp [wInner_one_eq_sum, conv_eq_sum_sub', mul_comm]

lemma dconv_eq_wInner_one (f g : G â†’ ğ•œ) (a : G) : (f â—‹ g) a = conj âŸªf, Ï„ a gâŸ«_[ğ•œ] := by
  simp [wInner_one_eq_sum, dconv_eq_sum_sub', map_sum, mul_comm]

lemma wInner_one_dconv (f g h : G â†’ ğ•œ) : âŸªf, g â—‹ hâŸ«_[ğ•œ] = âŸªconj g, conj f âˆ— conj hâŸ«_[ğ•œ] := by
  calc
    _ = âˆ‘ b, âˆ‘ a, g a * conj (h b) * conj (f (a - b)) := by
      simp_rw [wInner_one_eq_sum, inner_apply, sum_dconv_mul]
      exact sum_comm
    _ = âˆ‘ b, âˆ‘ a, conj (f a) * conj (h b) * g (a + b) := by
      simp_rw [â† Fintype.sum_prod_type']
      exact Fintype.sum_equiv ((Equiv.refl _).prodShear Equiv.subRight) _ _
        (by simp [mul_rotate, mul_right_comm])
    _ = _ := by
      simp_rw [wInner_one_eq_sum, inner_apply, sum_conv_mul, Pi.conj_apply, RCLike.conj_conj]
      exact sum_comm

lemma wInner_one_conv (f g h : G â†’ ğ•œ) : âŸªf, g âˆ— hâŸ«_[ğ•œ] = âŸªconj g, conj f â—‹ conj hâŸ«_[ğ•œ] := by
  simp_rw [wInner_one_dconv, RCLike.conj_conj]

lemma dconv_wInner_one (f g h : G â†’ ğ•œ) : âŸªf â—‹ g, hâŸ«_[ğ•œ] = âŸªconj h âˆ— conj g, conj fâŸ«_[ğ•œ] := by
  rw [â† conj_wInner_symm, wInner_one_dconv, conj_wInner_symm]

lemma conv_wInner_one (f g h : G â†’ ğ•œ) : âŸªf âˆ— g, hâŸ«_[ğ•œ] = âŸªconj h â—‹ conj g, conj fâŸ«_[ğ•œ] := by
  rw [â† conj_wInner_symm, wInner_one_conv, conj_wInner_symm]

lemma dconv_wInner_one_eq_wInner_one_conv (f g h : G â†’ ğ•œ) : âŸªf â—‹ g, hâŸ«_[ğ•œ] = âŸªf, h âˆ— gâŸ«_[ğ•œ] := by
  rw [dconv_wInner_one]; simp [wInner_one_eq_sum, mul_comm]

lemma wInner_one_dconv_eq_conv_wInner_one (f g h : G â†’ ğ•œ) : âŸªf, h â—‹ gâŸ«_[ğ•œ] = âŸªf âˆ— g, hâŸ«_[ğ•œ] := by
  rw [wInner_one_dconv]; simp [wInner_one_eq_sum, mul_comm]

variable [MeasurableSpace G] [DiscreteMeasurableSpace G]

@[simp] lemma dLpNorm_trivChar (hp : p â‰  0) : â€–(trivChar : G â†’ ğ•œ)â€–_[p] = 1 := by
  obtain _ | p := p
  Â· simp only [ENNReal.none_eq_top, dLinftyNorm_eq_iSup_nnnorm, trivChar_apply, apply_ite,
      nnnorm_one, nnnorm_zero]
    exact IsLUB.ciSup_eq âŸ¨by aesop (add simp mem_upperBounds), fun x hx â†¦ hx âŸ¨0, if_pos rflâŸ©âŸ©
  Â· simp at hp
    simp [dLpNorm_eq_sum_nnnorm hp, apply_ite, hp]

/-- A special case of **Young's convolution inequality**. -/
lemma dLpNorm_conv_le {p : â„â‰¥0} (hp : 1 â‰¤ p) (f g : G â†’ ğ•œ) : â€–f âˆ— gâ€–_[p] â‰¤ â€–fâ€–_[p] * â€–gâ€–_[1] := by
  obtain rfl | hp := hp.eq_or_lt
  Â· simp_rw [ENNReal.coe_one, dL1Norm_eq_sum_nnnorm, sum_mul_sum, conv_eq_sum_sub']
    calc
      âˆ‘ x, â€–âˆ‘ y, f y * g (x - y)â€–â‚Š â‰¤ âˆ‘ x, âˆ‘ y, â€–f y * g (x - y)â€–â‚Š :=
        sum_le_sum fun x _ â†¦ nnnorm_sum_le _ _
      _ = _ := ?_
    rw [sum_comm]
    simp_rw [nnnorm_mul]
    exact sum_congr rfl fun x _ â†¦ Fintype.sum_equiv (Equiv.subRight x) _ _ fun _ â†¦ rfl
  have hpâ‚€ := zero_lt_one.trans hp
  rw [â† NNReal.rpow_le_rpow_iff hpâ‚€, NNReal.mul_rpow]
  dsimp
  simp_rw [dLpNorm_rpow_eq_sum_nnnorm hpâ‚€.ne', conv_eq_sum_sub']
  have hpconj : (p : â„).HolderConjugate (1 - (p : â„)â»Â¹)â»Â¹ :=
    âŸ¨by simp, mod_cast hpâ‚€, by simpa using inv_lt_one_of_one_ltâ‚€ hpâŸ©
  have (x : G) : â€–âˆ‘ y, f y * g (x - y)â€–â‚Š ^ (p : â„) â‰¤
      (âˆ‘ y, â€–f yâ€–â‚Š ^ (p : â„) * â€–g (x - y)â€–â‚Š) * (âˆ‘ y, â€–g (x - y)â€–â‚Š) ^ (p - 1 : â„) := by
    rw [â† NNReal.le_rpow_inv_iff_of_pos, NNReal.mul_rpow, â† NNReal.rpow_mul, sub_one_mul,
      mul_inv_cancelâ‚€]
    any_goals positivity
    calc
      _ â‰¤ âˆ‘ y, â€–f y * g (x - y)â€–â‚Š := nnnorm_sum_le _ _
      _ = âˆ‘ y, â€–f yâ€–â‚Š * â€–g (x - y)â€–â‚Š ^ (p : â„)â»Â¹ * â€–g (x - y)â€–â‚Š ^ (1 - (p : â„)â»Â¹) := ?_
      _ â‰¤ _ := NNReal.inner_le_Lp_mul_Lq _ _ _ hpconj
      _ = _ := ?_
    Â· congr with t
      rw [nnnorm_mul, mul_assoc, â† NNReal.rpow_add', add_sub_cancel, NNReal.rpow_one]
      simp
    Â· have : 1 - (p : â„)â»Â¹ â‰  0 := sub_ne_zero.2 (inv_ne_one.2 <| NNReal.coe_ne_one.2 hp.ne').symm
      simp [NNReal.mul_rpow, hpâ‚€.ne', this]

  calc
    âˆ‘ x, â€–âˆ‘ y, f y * g (x - y)â€–â‚Š ^ (p : â„) â‰¤
        âˆ‘ x, (âˆ‘ y, â€–f yâ€–â‚Š ^ (p : â„) * â€–g (x - y)â€–â‚Š) * (âˆ‘ y, â€–g (x - y)â€–â‚Š) ^ (p - 1 : â„) :=
      sum_le_sum fun i _ â†¦ this _
    _ = _ := ?_
  have hg : âˆ€ x, âˆ‘ y, â€–g (x - y)â€–â‚Š = â€–gâ€–_[1] := by
    simp_rw [dL1Norm_eq_sum_nnnorm]
    exact fun x â†¦ Fintype.sum_equiv (Equiv.subLeft _) _ _ fun _ â†¦ rfl
  have hg' : âˆ€ y, âˆ‘ x, â€–g (x - y)â€–â‚Š = â€–gâ€–_[1] := by
    simp_rw [dL1Norm_eq_sum_nnnorm]
    exact fun x â†¦ Fintype.sum_equiv (Equiv.subRight _) _ _ fun _ â†¦ rfl
  simp_rw [hg]
  rw [â† sum_mul, sum_comm]
  simp_rw [â† mul_sum, hg']
  rw [â† sum_mul, mul_assoc, â† NNReal.rpow_one_add', add_sub_cancel]
  Â· rw [add_sub_cancel]
    positivity

/-- A special case of **Young's convolution inequality**. -/
lemma dLpNorm_dconv_le {p : â„â‰¥0} (hp : 1 â‰¤ p) (f g : G â†’ ğ•œ) : â€–f â—‹ gâ€–_[p] â‰¤ â€–fâ€–_[p] * â€–gâ€–_[1] := by
  simpa only [conv_conjneg, dLpNorm_conjneg] using dLpNorm_conv_le hp f (conjneg g)

end RCLike

section Real
variable [MeasurableSpace G] [DiscreteMeasurableSpace G] {f g : G â†’ â„} {n : â„•}

--TODO: Include `f : G â†’ â„‚`
lemma dL1Norm_conv (hf : 0 â‰¤ f) (hg : 0 â‰¤ g) : â€–f âˆ— gâ€–_[1] = â€–fâ€–_[1] * â€–gâ€–_[1] := by
  ext
  have : âˆ€ x, 0 â‰¤ âˆ‘ y, f y * g (x - y) := fun x â†¦ sum_nonneg fun y _ â†¦ mul_nonneg (hf _) (hg _)
  simp [dL1Norm_eq_sum_nnnorm, â† sum_conv, conv_eq_sum_sub', nnnorm_of_nonneg (this _),
    nnnorm_of_nonneg (hf _), nnnorm_of_nonneg (hg _)]

lemma dL1Norm_dconv (hf : 0 â‰¤ f) (hg : 0 â‰¤ g) : â€–f â—‹ gâ€–_[1] = â€–fâ€–_[1] * â€–gâ€–_[1] := by
  simpa using dL1Norm_conv hf (conjneg_nonneg.2 hg)

end Real
