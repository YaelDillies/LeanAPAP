import Mathlib.Algebra.Star.Conjneg
import Mathlib.Analysis.Complex.Basic
import LeanAPAP.Prereqs.Function.Translate

/-!
# Convolution

This file defines several versions of the discrete convolution of functions.

## Main declarations

* `conv`: Discrete convolution of two functions
* `dconv`: Discrete difference convolution of two functions
* `iterConv`: Iterated convolution of a function

## Notation

* `f âˆ— g`: Convolution
* `f â—‹ g`: Difference convolution
* `f âˆ—^ n`: Iterated convolution

## Notes

Some lemmas could technically be generalised to a non-commutative semiring domain. Doesn't seem very
useful given that the codomain in applications is either `â„`, `â„â‰¥0` or `â„‚`.

Similarly we could drop the commutativity assumption on the domain, but this is unneeded at this
point in time.

## TODO

Multiplicativise? Probably ugly and not very useful.
-/

open Finset Fintype Function
open scoped ComplexConjugate NNReal Pointwise NNRat

variable {G H R S : Type*} [DecidableEq G] [AddCommGroup G]

/-! ### Trivial character -/

section CommSemiring
variable [CommSemiring R]

/-- The trivial character. -/
def trivChar : G â†’ R := fun a â†¦ if a = 0 then 1 else 0

@[simp] lemma trivChar_apply (a : G) : (trivChar a : R) = if a = 0 then 1 else 0 := rfl

variable [StarRing R]

@[simp] lemma conj_trivChar : conj (trivChar : G â†’ R) = trivChar := by ext; simp
@[simp] lemma conjneg_trivChar : conjneg (trivChar : G â†’ R) = trivChar := by ext; simp

@[simp] lemma isSelfAdjoint_trivChar : IsSelfAdjoint (trivChar : G â†’ R) := conj_trivChar

end CommSemiring

variable [Fintype G]

/-! ### Convolution -/

section CommSemiring
variable [CommSemiring R] {f g : G â†’ R}

/-- Convolution -/
def conv (f g : G â†’ R) : G â†’ R := fun a â†¦ âˆ‘ x : G Ã— G with x.1 + x.2 = a , f x.1 * g x.2

infixl:71 " âˆ— " => conv

lemma conv_apply (f g : G â†’ R) (a : G) :
    (f âˆ— g) a = âˆ‘ x : G Ã— G with x.1 + x.2 = a, f x.1 * g x.2 := rfl

@[simp] lemma conv_zero (f : G â†’ R) : f âˆ— 0 = 0 := by ext; simp [conv_apply]
@[simp] lemma zero_conv (f : G â†’ R) : 0 âˆ— f = 0 := by ext; simp [conv_apply]

lemma conv_add (f g h : G â†’ R) : f âˆ— (g + h) = f âˆ— g + f âˆ— h := by
  ext; simp [conv_apply, mul_add, sum_add_distrib]

lemma add_conv (f g h : G â†’ R) : (f + g) âˆ— h = f âˆ— h + g âˆ— h := by
  ext; simp [conv_apply, add_mul, sum_add_distrib]

lemma smul_conv [DistribSMul H R] [IsScalarTower H R R] (c : H) (f g : G â†’ R) :
    c â€¢ f âˆ— g = c â€¢ (f âˆ— g) := by ext a; simp [conv_apply, smul_sum, smul_mul_assoc]

lemma conv_smul [DistribSMul H R] [SMulCommClass H R R] (c : H) (f g : G â†’ R) :
    f âˆ— c â€¢ g = c â€¢ (f âˆ— g) := by ext a; simp [conv_apply, smul_sum, mul_smul_comm]

alias smul_conv_assoc := smul_conv
alias smul_conv_left_comm := conv_smul

@[simp] lemma translate_conv (a : G) (f g : G â†’ R) : Ï„ a f âˆ— g = Ï„ a (f âˆ— g) :=
  funext fun b â†¦ sum_equiv ((Equiv.subRight a).prodCongr $ Equiv.refl _)
    (by simp [sub_add_eq_add_sub]) (by simp)

@[simp] lemma conv_translate (a : G) (f g : G â†’ R) : f âˆ— Ï„ a g = Ï„ a (f âˆ— g) :=
  funext fun b â†¦ sum_equiv ((Equiv.refl _).prodCongr $ Equiv.subRight a)
    (by simp [â† add_sub_assoc]) (by simp)

lemma conv_comm (f g : G â†’ R) : f âˆ— g = g âˆ— f :=
  funext fun a â†¦ sum_equiv (Equiv.prodComm _ _) (by simp [add_comm]) $ by simp [mul_comm]

lemma mul_smul_conv_comm [Monoid H] [DistribMulAction H R] [IsScalarTower H R R]
    [SMulCommClass H R R] (c d : H) (f g : G â†’ R) : (c * d) â€¢ (f âˆ— g) = c â€¢ f âˆ— d â€¢ g := by
  rw [smul_conv, conv_smul, mul_smul]

lemma conv_assoc (f g h : G â†’ R) : f âˆ— g âˆ— h = f âˆ— (g âˆ— h) := by
  ext a
  simp only [sum_mul, mul_sum, conv_apply, sum_sigma']
  apply sum_nbij' (fun âŸ¨(_b, c), (d, e)âŸ© â†¦ âŸ¨(d, e + c), (e, c)âŸ©)
    (fun âŸ¨(b, _c), (d, e)âŸ© â†¦ âŸ¨(b + d, e), (b, d)âŸ©) <;> aesop (add simp [add_assoc, mul_assoc])

lemma conv_right_comm (f g h : G â†’ R) : f âˆ— g âˆ— h = f âˆ— h âˆ— g := by
  rw [conv_assoc, conv_assoc, conv_comm g]

lemma conv_left_comm (f g h : G â†’ R) : f âˆ— (g âˆ— h) = g âˆ— (f âˆ— h) := by
  rw [â† conv_assoc, â† conv_assoc, conv_comm g]

lemma conv_rotate (f g h : G â†’ R) : f âˆ— g âˆ— h = g âˆ— h âˆ— f := by rw [conv_assoc, conv_comm]
lemma conv_rotate' (f g h : G â†’ R) : f âˆ— (g âˆ— h) = g âˆ— (h âˆ— f) := by rw [conv_comm, â† conv_assoc]

lemma conv_conv_conv_comm (f g h i : G â†’ R) : f âˆ— g âˆ— (h âˆ— i) = f âˆ— h âˆ— (g âˆ— i) := by
  rw [conv_assoc, conv_assoc, conv_left_comm g]

lemma map_conv [CommSemiring S] (m : R â†’+* S) (f g : G â†’ R) (a : G) :
    m ((f âˆ— g) a) = (m âˆ˜ f âˆ— m âˆ˜ g) a := by simp [conv_apply, map_sum, map_mul]

lemma comp_conv [CommSemiring S] (m : R â†’+* S) (f g : G â†’ R) : m âˆ˜ (f âˆ— g) = m âˆ˜ f âˆ— m âˆ˜ g :=
  funext $ map_conv _ _ _

lemma conv_eq_sum_sub (f g : G â†’ R) (a : G) : (f âˆ— g) a = âˆ‘ t, f (a - t) * g t := by
  rw [conv_apply]; apply sum_nbij' Prod.snd (fun b â†¦ (a - b, b)) <;> aesop

lemma conv_eq_sum_add (f g : G â†’ R) (a : G) : (f âˆ— g) a = âˆ‘ t, f (a + t) * g (-t) :=
  (conv_eq_sum_sub _ _ _).trans $ Fintype.sum_equiv (Equiv.neg _) _ _ fun t â†¦ by
    simp only [sub_eq_add_neg, Equiv.neg_apply, neg_neg]

lemma conv_eq_sum_sub' (f g : G â†’ R) (a : G) : (f âˆ— g) a = âˆ‘ t, f t * g (a - t) := by
  rw [conv_comm, conv_eq_sum_sub]; simp_rw [mul_comm]

lemma conv_eq_sum_add' (f g : G â†’ R) (a : G) : (f âˆ— g) a = âˆ‘ t, f (-t) * g (a + t) := by
  rw [conv_comm, conv_eq_sum_add]; simp_rw [mul_comm]

lemma conv_apply_add (f g : G â†’ R) (a b : G) : (f âˆ— g) (a + b) = âˆ‘ t, f (a + t) * g (b - t) :=
  (conv_eq_sum_sub _ _ _).trans $ Fintype.sum_equiv (Equiv.subLeft b) _ _ fun t â†¦ by
    simp [add_sub_assoc, â† sub_add]

lemma sum_conv_mul (f g h : G â†’ R) : âˆ‘ a, (f âˆ— g) a * h a = âˆ‘ a, âˆ‘ b, f a * g b * h (a + b) := by
  simp_rw [conv_eq_sum_sub', sum_mul]
  rw [sum_comm]
  exact sum_congr rfl fun x _ â†¦ Fintype.sum_equiv (Equiv.subRight x) _ _ fun y â†¦ by simp

lemma sum_conv (f g : G â†’ R) : âˆ‘ a, (f âˆ— g) a = (âˆ‘ a, f a) * âˆ‘ a, g a := by
  simpa only [Fintype.sum_mul_sum, Pi.one_apply, mul_one] using sum_conv_mul f g 1

@[simp] lemma conv_const (f : G â†’ R) (b : R) : f âˆ— const _ b = const _ ((âˆ‘ x, f x) * b) := by
  ext; simp [conv_eq_sum_sub', sum_mul]

@[simp] lemma const_conv (b : R) (f : G â†’ R) : const _ b âˆ— f = const _ (b * âˆ‘ x, f x) := by
  ext; simp [conv_eq_sum_sub, mul_sum]

@[simp] lemma conv_trivChar (f : G â†’ R) : f âˆ— trivChar = f := by ext a; simp [conv_eq_sum_sub]
@[simp] lemma trivChar_conv (f : G â†’ R) : trivChar âˆ— f = f := by rw [conv_comm, conv_trivChar]

lemma support_conv_subset (f g : G â†’ R) : support (f âˆ— g) âŠ† support f + support g := by
  rintro a ha
  obtain âŸ¨x, hx, hâŸ© := exists_ne_zero_of_sum_ne_zero ha
  exact âŸ¨_, left_ne_zero_of_mul h, _, right_ne_zero_of_mul h, (mem_filter.1 hx).2âŸ©

/-! ### Difference convolution -/

variable [StarRing R]

/-- Difference convolution -/
def dconv (f g : G â†’ R) : G â†’ R := fun a â†¦ âˆ‘ x : G Ã— G with x.1 - x.2 = a, f x.1 * conj g x.2

infixl:71 " â—‹ " => dconv

lemma dconv_apply (f g : G â†’ R) (a : G) :
    (f â—‹ g) a = âˆ‘ x : G Ã— G with x.1 - x.2 = a , f x.1 * conj g x.2 := rfl

@[simp] lemma dconv_zero (f : G â†’ R) : f â—‹ 0 = 0 := by ext; simp [dconv_apply]
@[simp] lemma zero_dconv (f : G â†’ R) : 0 â—‹ f = 0 := by ext; simp [dconv_apply]

lemma dconv_add (f g h : G â†’ R) : f â—‹ (g + h) = f â—‹ g + f â—‹ h := by
  ext; simp [dconv_apply, mul_add, sum_add_distrib]

lemma add_dconv (f g h : G â†’ R) : (f + g) â—‹ h = f â—‹ h + g â—‹ h := by
  ext; simp [dconv_apply, add_mul, sum_add_distrib]

lemma smul_dconv [DistribSMul H R] [IsScalarTower H R R] (c : H) (f g : G â†’ R) :
    c â€¢ f â—‹ g = c â€¢ (f â—‹ g) := by ext; simp [dconv_apply, smul_sum, smul_mul_assoc]

lemma dconv_smul [Star H] [DistribSMul H R] [SMulCommClass H R R] [StarModule H R] (c : H)
    (f g : G â†’ R) : f â—‹ c â€¢ g = star c â€¢ (f â—‹ g) := by
  ext; simp [dconv_apply, smul_sum, mul_smul_comm, starRingEnd_apply, star_smul]

@[simp] lemma translate_dconv (a : G) (f g : G â†’ R) : Ï„ a f â—‹ g = Ï„ a (f â—‹ g) :=
  funext fun b â†¦ sum_equiv ((Equiv.subRight a).prodCongr $ Equiv.refl _)
    (by simp [sub_right_comm _ a]) (by simp)

@[simp] lemma dconv_translate (a : G) (f g : G â†’ R) : f â—‹ Ï„ a g = Ï„ (-a) (f â—‹ g) :=
  funext fun b â†¦ sum_equiv ((Equiv.refl _).prodCongr $ Equiv.subRight a)
    (by simp [sub_sub_eq_add_sub, â† sub_add_eq_add_sub]) (by simp)

@[simp] lemma conv_conjneg (f g : G â†’ R) : f âˆ— conjneg g = f â—‹ g :=
  funext fun a â†¦ sum_equiv ((Equiv.refl _).prodCongr $ Equiv.neg _) (by simp) (by simp)

@[simp] lemma dconv_conjneg (f g : G â†’ R) : f â—‹ conjneg g = f âˆ— g := by
  rw [â† conv_conjneg, conjneg_conjneg]

@[simp] lemma conj_conv_apply (f g : G â†’ R) (a : G) : conj ((f âˆ— g) a) = (conj f âˆ— conj g) a := by
  simp only [Pi.conj_apply, conv_apply, map_sum, map_mul]

@[simp] lemma conj_dconv_apply (f g : G â†’ R) (a : G) : conj ((f â—‹ g) a) = (conj f â—‹ conj g) a := by
  simp_rw [â† conv_conjneg, conj_conv_apply, conjneg_conj]

@[simp] lemma conj_conv (f g : G â†’ R) : conj (f âˆ— g) = conj f âˆ— conj g :=
  funext <| conj_conv_apply _ _

@[simp] lemma conj_dconv (f g : G â†’ R) : conj (f â—‹ g) = conj f â—‹ conj g :=
  funext <| conj_dconv_apply _ _

lemma IsSelfAdjoint.conv (hf : IsSelfAdjoint f) (hg : IsSelfAdjoint g) : IsSelfAdjoint (f âˆ— g) :=
  (conj_conv _ _).trans $ congr_argâ‚‚ _ hf hg

lemma IsSelfAdjoint.dconv (hf : IsSelfAdjoint f) (hg : IsSelfAdjoint g) : IsSelfAdjoint (f â—‹ g) :=
  (conj_dconv _ _).trans $ congr_argâ‚‚ _ hf hg

@[simp]lemma conjneg_conv (f g : G â†’ R) : conjneg (f âˆ— g) = conjneg f âˆ— conjneg g := by
  funext a
  simp only [conv_apply, conjneg_apply, map_sum, map_mul]
  exact sum_equiv (Equiv.neg _) (by simp [â† neg_eq_iff_eq_neg, add_comm]) (by simp)

@[simp] lemma conjneg_dconv (f g : G â†’ R) : conjneg (f â—‹ g) = g â—‹ f := by
  simp_rw [â† conv_conjneg, conjneg_conv, conjneg_conjneg, conv_comm]
alias smul_dconv_assoc := smul_dconv
alias smul_dconv_left_comm := dconv_smul

lemma dconv_right_comm (f g h : G â†’ R) : f â—‹ g â—‹ h = f â—‹ h â—‹ g := by
  simp_rw [â† conv_conjneg, conv_right_comm]

lemma conv_dconv_assoc (f g h : G â†’ R) : f âˆ— g â—‹ h = f âˆ— (g â—‹ h) := by
  simp_rw [â† conv_conjneg, conv_assoc]

lemma conv_dconv_left_comm (f g h : G â†’ R) : f âˆ— (g â—‹ h) = g âˆ— (f â—‹ h) := by
  simp_rw [â† conv_conjneg, conv_left_comm]

lemma conv_dconv_right_comm (f g h : G â†’ R) : f âˆ— g â—‹ h = f â—‹ h âˆ— g := by
  simp_rw [â† conv_conjneg, conv_right_comm]

lemma conv_dconv_conv_comm (f g h i : G â†’ R) : f âˆ— g â—‹ (h âˆ— i) = f â—‹ h âˆ— (g â—‹ i) := by
  simp_rw [â† conv_conjneg, conjneg_conv, conv_conv_conv_comm]

lemma dconv_conv_dconv_comm (f g h i : G â†’ R) : f â—‹ g âˆ— (h â—‹ i) = f âˆ— h â—‹ (g âˆ— i) := by
  simp_rw [â† conv_conjneg, conjneg_conv, conv_conv_conv_comm]

lemma dconv_dconv_dconv_comm (f g h i : G â†’ R) : f â—‹ g â—‹ (h â—‹ i) = f â—‹ h â—‹ (g â—‹ i) := by
  simp_rw [â† conv_conjneg, conjneg_conv, conv_conv_conv_comm]

--TODO: Can we generalise to star ring homs?
lemma map_dconv (f g : G â†’ â„â‰¥0) (a : G) : (â†‘((f â—‹ g) a) : â„) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := by
  simp_rw [dconv_apply, NNReal.coe_sum, NNReal.coe_mul, starRingEnd_apply, star_trivial,
    Function.comp_apply]

lemma comp_dconv (f g : G â†’ â„â‰¥0) : ((â†‘) âˆ˜ (f â—‹ g) : G â†’ â„) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g :=
  funext $ map_dconv _ _

lemma dconv_eq_sum_sub (f g : G â†’ R) (a : G) : (f â—‹ g) a = âˆ‘ t, f (a - t) * conj (g (-t)) := by
  simp [â† conv_conjneg, conv_eq_sum_sub]

lemma dconv_eq_sum_add (f g : G â†’ R) (a : G) : (f â—‹ g) a = âˆ‘ t, f (a + t) * conj (g t) := by
  simp [â† conv_conjneg, conv_eq_sum_add]

lemma dconv_eq_sum_sub' (f g : G â†’ R) (a : G) : (f â—‹ g) a = âˆ‘ t, f t * conj (g (t - a)) := by
  simp [â† conv_conjneg, conv_eq_sum_sub']

lemma dconv_eq_sum_add' (f g : G â†’ R) (a : G) : (f â—‹ g) a = âˆ‘ t, f (-t) * conj g (-(a + t)) := by
  simp [â† conv_conjneg, conv_eq_sum_add']

lemma dconv_apply_neg (f g : G â†’ R) (a : G) : (f â—‹ g) (-a) = conj ((g â—‹ f) a) := by
  rw [â† conjneg_dconv f, conjneg_apply, Complex.conj_conj]

lemma dconv_apply_sub (f g : G â†’ R) (a b : G) :
    (f â—‹ g) (a - b) = âˆ‘ t, f (a + t) * conj (g (b + t)) := by
  simp [â† conv_conjneg, sub_eq_add_neg, conv_apply_add, add_comm]

lemma sum_dconv_mul (f g h : G â†’ R) :
    âˆ‘ a, (f â—‹ g) a * h a = âˆ‘ a, âˆ‘ b, f a * conj (g b) * h (a - b) := by
  simp_rw [dconv_eq_sum_sub', sum_mul]
  rw [sum_comm]
  exact Fintype.sum_congr _ _ fun x â†¦ Fintype.sum_equiv (Equiv.subLeft x) _ _ fun y â†¦ by simp

lemma sum_dconv (f g : G â†’ R) : âˆ‘ a, (f â—‹ g) a = (âˆ‘ a, f a) * âˆ‘ a, conj (g a) := by
  simpa only [Fintype.sum_mul_sum, Pi.one_apply, mul_one] using sum_dconv_mul f g 1

@[simp] lemma dconv_const (f : G â†’ R) (b : R) : f â—‹ const _ b = const _ ((âˆ‘ x, f x) * conj b) := by
  ext; simp [dconv_eq_sum_sub', sum_mul]

@[simp] lemma const_dconv (b : R) (f : G â†’ R) : const _ b â—‹ f = const _ (b * âˆ‘ x, conj (f x)) := by
  ext; simp [dconv_eq_sum_add, mul_sum]

@[simp] lemma dconv_trivChar (f : G â†’ R) : f â—‹ trivChar = f := by ext a; simp [dconv_eq_sum_add]
@[simp] lemma trivChar_dconv (f : G â†’ R) : trivChar â—‹ f = conjneg f := by
  rw [â† conv_conjneg, trivChar_conv]

lemma support_dconv_subset (f g : G â†’ R) : support (f â—‹ g) âŠ† support f - support g := by
  simpa [sub_eq_add_neg] using support_conv_subset f (conjneg g)

end CommSemiring

section CommRing
variable [CommRing R]

@[simp] lemma conv_neg (f g : G â†’ R) : f âˆ— -g = -(f âˆ— g) := by ext; simp [conv_apply]
@[simp] lemma neg_conv (f g : G â†’ R) : -f âˆ— g = -(f âˆ— g) := by ext; simp [conv_apply]

lemma conv_sub (f g h : G â†’ R) : f âˆ— (g - h) = f âˆ— g - f âˆ— h := by
  simp only [sub_eq_add_neg, conv_add, conv_neg]

lemma sub_conv (f g h : G â†’ R) : (f - g) âˆ— h = f âˆ— h - g âˆ— h := by
  simp only [sub_eq_add_neg, add_conv, neg_conv]

variable [StarRing R]

@[simp] lemma dconv_neg (f g : G â†’ R) : f â—‹ -g = -(f â—‹ g) := by ext; simp [dconv_apply]
@[simp] lemma neg_dconv (f g : G â†’ R) : -f â—‹ g = -(f â—‹ g) := by ext; simp [dconv_apply]

lemma dconv_sub (f g h : G â†’ R) : f â—‹ (g - h) = f â—‹ g - f â—‹ h := by
  simp only [sub_eq_add_neg, dconv_add, dconv_neg]

lemma sub_dconv (f g h : G â†’ R) : (f - g) â—‹ h = f â—‹ h - g â—‹ h := by
  simp only [sub_eq_add_neg, add_dconv, neg_dconv]

end CommRing

namespace RCLike
variable {ğ•œ : Type} [RCLike ğ•œ] (f g : G â†’ â„) (a : G)

@[simp, norm_cast]
lemma coe_conv : (â†‘((f âˆ— g) a) : ğ•œ) = ((â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g) a :=
  map_conv (algebraMap â„ ğ•œ) _ _ _

@[simp, norm_cast]
lemma coe_dconv : (â†‘((f â—‹ g) a) : ğ•œ) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := by simp [dconv_apply, coe_sum]

@[simp] lemma coe_comp_conv : ((â†‘) : â„ â†’ ğ•œ) âˆ˜ (f âˆ— g) = (â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g := funext $ coe_conv _ _
@[simp] lemma coe_comp_dconv : ((â†‘) : â„ â†’ ğ•œ) âˆ˜ (f â—‹ g) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g := funext $ coe_dconv _ _

end RCLike

namespace Complex
variable (f g : G â†’ â„) (n : â„•) (a : G)

@[simp, norm_cast]
lemma coe_conv : (â†‘((f âˆ— g) a) : â„‚) = ((â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g) a := RCLike.coe_conv _ _ _

@[simp, norm_cast]
lemma coe_dconv : (â†‘((f â—‹ g) a) : â„‚) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := RCLike.coe_dconv _ _ _

@[simp] lemma coe_comp_conv : ((â†‘) : â„ â†’ â„‚) âˆ˜ (f âˆ— g) = (â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g := funext $ coe_conv _ _
@[simp] lemma coe_comp_dconv : ((â†‘) : â„ â†’ â„‚) âˆ˜ (f â—‹ g) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g := funext $ coe_dconv _ _

end Complex

namespace NNReal
variable (f g : G â†’ â„â‰¥0) (a : G)

@[simp, norm_cast]
lemma coe_conv : (â†‘((f âˆ— g) a) : â„) = ((â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g) a := map_conv NNReal.toRealHom _ _ _

@[simp, norm_cast]
lemma coe_dconv : (â†‘((f â—‹ g) a) : â„) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := by simp [dconv_apply, coe_sum]

@[simp] lemma coe_comp_conv : ((â†‘) : _ â†’ â„) âˆ˜ (f âˆ— g) = (â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g := funext $ coe_conv _ _
@[simp] lemma coe_comp_dconv : ((â†‘) : _ â†’ â„) âˆ˜ (f â—‹ g) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g := funext $ coe_dconv _ _

end NNReal

/-! ### Iterated convolution -/

section CommSemiring
variable [CommSemiring R] {f g : G â†’ R} {n : â„•}

/-- Iterated convolution. -/
def iterConv (f : G â†’ R) : â„• â†’ G â†’ R
  | 0 => trivChar
  | n + 1 => iterConv f n âˆ— f

infixl:78 " âˆ—^ " => iterConv

@[simp] lemma iterConv_zero (f : G â†’ R) : f âˆ—^ 0 = trivChar := rfl
@[simp] lemma iterConv_one (f : G â†’ R) : f âˆ—^ 1 = f := trivChar_conv _

lemma iterConv_succ (f : G â†’ R) (n : â„•) : f âˆ—^ (n + 1) = f âˆ—^ n âˆ— f := rfl
lemma iterConv_succ' (f : G â†’ R) (n : â„•) : f âˆ—^ (n + 1) = f âˆ— f âˆ—^ n := conv_comm _ _

lemma iterConv_add (f : G â†’ R) (m : â„•) : âˆ€ n, f âˆ—^ (m + n) = f âˆ—^ m âˆ— f âˆ—^ n
  | 0 => by simp
  | n + 1 => by simp [â† add_assoc, iterConv_succ', iterConv_add, conv_left_comm]

lemma iterConv_mul (f : G â†’ R) (m : â„•) : âˆ€ n, f âˆ—^ (m * n) = f âˆ—^ m âˆ—^ n
  | 0 => rfl
  | n + 1 => by simp [mul_add_one, iterConv_succ, iterConv_add, iterConv_mul]

lemma iterConv_mul' (f : G â†’ R) (m n : â„•) : f âˆ—^ (m * n) = f âˆ—^ n âˆ—^ m := by
  rw [mul_comm, iterConv_mul]

lemma iterConv_conv_distrib (f g : G â†’ R) : âˆ€ n, (f âˆ— g) âˆ—^ n = f âˆ—^ n âˆ— g âˆ—^ n
  | 0 => (conv_trivChar _).symm
  | n + 1 => by simp_rw [iterConv_succ, iterConv_conv_distrib, conv_conv_conv_comm]

@[simp] lemma zero_iterConv : âˆ€ {n}, n â‰  0 â†’ (0 : G â†’ R) âˆ—^ n = 0
  | 0, hn => by cases hn rfl
  | n + 1, _ => conv_zero _

@[simp] lemma smul_iterConv [Monoid H] [DistribMulAction H R] [IsScalarTower H R R]
    [SMulCommClass H R R] (c : H) (f : G â†’ R) : âˆ€ n, (c â€¢ f) âˆ—^ n = c ^ n â€¢ f âˆ—^ n
  | 0 => by simp
  | n + 1 => by simp_rw [iterConv_succ, smul_iterConv _ _ n, pow_succ, mul_smul_conv_comm]

lemma comp_iterConv [CommSemiring S] (m : R â†’+* S) (f : G â†’ R) :
    âˆ€ n, m âˆ˜ (f âˆ—^ n) = m âˆ˜ f âˆ—^ n
  | 0 => by ext; simp
  | n + 1 => by simp [iterConv_succ, comp_conv, comp_iterConv]

lemma map_iterConv [CommSemiring S] (m : R â†’+* S) (f : G â†’ R) (a : G) (n : â„•) :
    m ((f âˆ—^ n) a) = (m âˆ˜ f âˆ—^ n) a := congr_fun (comp_iterConv m _ _) _

lemma sum_iterConv (f : G â†’ R) : âˆ€ n, âˆ‘ a, (f âˆ—^ n) a = (âˆ‘ a, f a) ^ n
  | 0 => by simp [filter_eq']
  | n + 1 => by simp only [iterConv_succ, sum_conv, sum_iterConv, pow_succ]

@[simp] lemma iterConv_trivChar : âˆ€ n, (trivChar : G â†’ R) âˆ—^ n = trivChar
  | 0 => rfl
  | _n + 1 => (conv_trivChar _).trans $ iterConv_trivChar _

lemma support_iterConv_subset (f : G â†’ R) : âˆ€ n, support (f âˆ—^ n) âŠ† n â€¢ support f
  | 0 => by
    simp only [iterConv_zero, zero_smul, support_subset_iff, Ne, ite_eq_right_iff, not_forall,
      exists_prop, Set.mem_zero, and_imp, forall_eq, eq_self_iff_true, imp_true_iff, trivChar_apply]
  | n + 1 =>
    (support_conv_subset _ _).trans $ Set.add_subset_add_right $ support_iterConv_subset _ _

variable [StarRing R]

lemma iterConv_dconv_distrib (f g : G â†’ R) : âˆ€ n, (f â—‹ g) âˆ—^ n = f âˆ—^ n â—‹ g âˆ—^ n
  | 0 => (dconv_trivChar _).symm
  | n + 1 => by simp_rw [iterConv_succ, iterConv_dconv_distrib, conv_dconv_conv_comm]

@[simp] lemma conj_iterConv (f : G â†’ R) : âˆ€ n, conj (f âˆ—^ n) = conj f âˆ—^ n
  | 0 => by ext; simp
  | n + 1 => by simp [iterConv_succ, conj_iterConv]

@[simp] lemma conj_iterConv_apply (f : G â†’ R) (n : â„•) (a : G) :
    conj ((f âˆ—^ n) a) = (conj f âˆ—^ n) a := congr_fun (conj_iterConv _ _) _

lemma IsSelfAdjoint.iterConv (hf : IsSelfAdjoint f) (n : â„•) : IsSelfAdjoint (f âˆ—^ n) :=
  (conj_iterConv _ _).trans $ congr_arg (Â· âˆ—^ n) hf

@[simp]
lemma conjneg_iterConv (f : G â†’ R) : âˆ€ n, conjneg (f âˆ—^ n) = conjneg f âˆ—^ n
  | 0 => by ext; simp
  | n + 1 => by simp [iterConv_succ, conjneg_iterConv]

end CommSemiring

namespace NNReal

@[simp, norm_cast]
lemma coe_iterConv (f : G â†’ â„â‰¥0) (n : â„•) (a : G) : (â†‘((f âˆ—^ n) a) : â„) = ((â†‘) âˆ˜ f âˆ—^ n) a :=
  map_iterConv NNReal.toRealHom _ _ _

end NNReal

namespace Complex

@[simp, norm_cast]
lemma coe_iterConv (f : G â†’ â„) (n : â„•) (a : G) : (â†‘((f âˆ—^ n) a) : â„‚) = ((â†‘) âˆ˜ f âˆ—^ n) a :=
  map_iterConv ofReal _ _ _

end Complex
