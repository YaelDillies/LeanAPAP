import Mathlib.Algebra.Module.Pi
import Mathlib.Analysis.Complex.Basic
import LeanAPAP.Mathlib.Data.Fintype.Pi
import LeanAPAP.Prereqs.Function.Indicator.Basic
import LeanAPAP.Prereqs.Convolution.Discrete.Defs

/-!
# Convolution

This file defines several versions of the discrete convolution of functions.

## Main declarations

* `conv`: Discrete convolution of two functions
* `dconv`: Discrete difference convolution of two functions
* `iterConv`: Iterated convolution of a function

## Notation

* `f âˆ— g`: Convolution
* `f â—‹ g`: Difference convolution
* `f âˆ—^ n`: Iterated convolution

## Notes

Some lemmas could technically be generalised to a non-commutative semiring domain. Doesn't seem very
useful given that the codomain in applications is either `â„`, `â„â‰¥0` or `â„‚`.

Similarly we could drop the commutativity assumption on the domain, but this is unneeded at this
point in time.

## TODO

Multiplicativise? Probably ugly and not very useful.
-/

open Finset Fintype Function
open scoped BigOperators ComplexConjugate NNReal Pointwise

variable {Î± Î² Î³ : Type*} [Fintype Î±] [DecidableEq Î±] [AddCommGroup Î±]

/-!
### Convolution of functions

In this section, we define the convolution `f âˆ— g` and difference convolution `f â—‹ g` of functions
`f g : Î± â†’ Î²`, and show how they interact.
-/

section CommSemiring
variable [CommSemiring Î²] [StarRing Î²] {f g : Î± â†’ Î²}

lemma indicate_conv_indicate_apply (s t : Finset Î±) (a : Î±) :
    (ğŸ­_[Î²] s âˆ— ğŸ­ t) a = ((s Ã—Ë¢ t).filter fun x : Î± Ã— Î± â†¦ x.1 + x.2 = a).card := by
  simp only [conv_apply, indicate_apply, â†ite_and, filter_comm, boole_mul, sum_boole]
  simp_rw [â†mem_product, filter_univ_mem]

lemma indicate_dconv_indicate_apply (s t : Finset Î±) (a : Î±) :
    (ğŸ­_[Î²] s â—‹ ğŸ­ t) a = ((s Ã—Ë¢ t).filter fun x : Î± Ã— Î± â†¦ x.1 - x.2 = a).card := by
  simp only [dconv_apply, indicate_apply, â†ite_and, filter_comm, boole_mul, sum_boole,
    apply_ite conj, map_one, map_zero, Pi.conj_apply]
  simp_rw [â†mem_product, filter_univ_mem]

lemma indicate_conv (s : Finset Î±) (f : Î± â†’ Î²) : ğŸ­ s âˆ— f = âˆ‘ a âˆˆ s, Ï„ a f := by
  ext; simp [conv_eq_sum_sub', indicate_apply]

lemma conv_indicate (f : Î± â†’ Î²) (s : Finset Î±) : f âˆ— ğŸ­ s = âˆ‘ a âˆˆ s, Ï„ a f := by
  ext; simp [conv_eq_sum_sub, indicate_apply]

lemma indicate_dconv (s : Finset Î±) (f : Î± â†’ Î²) : ğŸ­ s â—‹ f = âˆ‘ a âˆˆ s, Ï„ a (conjneg f) := by
  ext; simp [dconv_eq_sum_sub', indicate_apply]

lemma dconv_indicate (f : Î± â†’ Î²) (s : Finset Î±) : f â—‹ ğŸ­ s = âˆ‘ a âˆˆ s, Ï„ (-a) f := by
  ext; simp [dconv_eq_sum_add, indicate_apply]

end CommSemiring

section Semifield
variable [Semifield Î²] [CharZero Î²] [StarRing Î²]

@[simp] lemma mu_univ_conv_mu_univ : Î¼_[Î²] (univ : Finset Î±) âˆ— Î¼ univ = Î¼ univ := by
  ext; cases eq_or_ne (card Î± : Î²) 0 <;> simp [mu_apply, conv_eq_sum_add, card_univ, *]

@[simp] lemma mu_univ_dconv_mu_univ : Î¼_[Î²] (univ : Finset Î±) â—‹ Î¼ univ = Î¼ univ := by
  ext; cases eq_or_ne (card Î± : Î²) 0 <;> simp [mu_apply, dconv_eq_sum_add, card_univ, *]

lemma expect_conv (f g : Î± â†’ Î²) : ğ”¼ a, (f âˆ— g) a = (âˆ‘ a, f a) * ğ”¼ a, g a := by
  simp_rw [expect, sum_conv, mul_smul_comm]

lemma expect_dconv (f g : Î± â†’ Î²) : ğ”¼ a, (f â—‹ g) a = (âˆ‘ a, f a) * ğ”¼ a, conj (g a) := by
  simp_rw [expect, sum_dconv, mul_smul_comm]

lemma expect_conv' (f g : Î± â†’ Î²) : ğ”¼ a, (f âˆ— g) a = (ğ”¼ a, f a) * âˆ‘ a, g a := by
  simp_rw [expect, sum_conv, smul_mul_assoc]

lemma expect_dconv' (f g : Î± â†’ Î²) : ğ”¼ a, (f â—‹ g) a = (ğ”¼ a, f a) * âˆ‘ a, conj (g a) := by
  simp_rw [expect, sum_dconv, smul_mul_assoc]

lemma mu_conv (s : Finset Î±) (f : Î± â†’ Î²) : Î¼ s âˆ— f = (s.card : Î²)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ a f := by
  simp [mu, indicate_conv, smul_conv]

lemma conv_mu (f : Î± â†’ Î²) (s : Finset Î±) : f âˆ— Î¼ s = (s.card : Î²)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ a f := by
  simp [mu, conv_indicate, conv_smul]

lemma mu_dconv (s : Finset Î±) (f : Î± â†’ Î²) :
    Î¼ s â—‹ f = (s.card : Î²)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ a (conjneg f) := by
  simp [mu, indicate_dconv, smul_dconv]

lemma dconv_mu (f : Î± â†’ Î²) (s : Finset Î±) : f â—‹ Î¼ s = (s.card : Î²)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ (-a) f := by
  simp [mu, dconv_indicate, dconv_smul]

end Semifield

section Field
variable [Field Î²] [StarRing Î²] [CharZero Î²]

@[simp] lemma balance_conv (f g : Î± â†’ Î²) : balance (f âˆ— g) = balance f âˆ— balance g := by
  simpa [balance, conv_sub, sub_conv, expect_conv]
    using (mul_smul_comm _ _ _).trans (smul_mul_assoc _ _ _).symm

@[simp] lemma balance_dconv (f g : Î± â†’ Î²) : balance (f â—‹ g) = balance f â—‹ balance g := by
  simpa [balance, dconv_sub, sub_dconv, expect_dconv, map_expect]
    using (mul_smul_comm _ _ _).trans (smul_mul_assoc _ _ _).symm

end Field

/-! ### Iterated convolution -/

section CommSemiring
variable [CommSemiring Î²] [StarRing Î²] {f g : Î± â†’ Î²} {n : â„•}

lemma indicate_iterConv_apply (s : Finset Î±) (n : â„•) (a : Î±) :
    (ğŸ­_[Î²] s âˆ—^ n) a = ((s ^^ n).filter fun x : Fin n â†’ Î± â†¦ âˆ‘ i, x i = a).card := by
  induction' n with n ih generalizing a
  Â· simp [apply_ite card, eq_comm]
  simp_rw [iterConv_succ', conv_eq_sum_sub', ih, indicate_apply, boole_mul, sum_ite,
    filter_univ_mem, sum_const_zero, add_zero, â†Nat.cast_sum, â†Finset.card_sigma]
  congr 1
  refine card_equiv ((Equiv.sigmaEquivProd ..).trans (Equiv.piFinSucc ..).symm) ?_
  aesop (add simp [Fin.sum_cons, Fin.forall_fin_succ])

lemma indicate_iterConv_conv (s : Finset Î±) (n : â„•) (f : Î± â†’ Î²) :
    ğŸ­ s âˆ—^ n âˆ— f = âˆ‘ a âˆˆ s ^^ n, Ï„ (âˆ‘ i, a i) f := by
  ext b
  simp only [conv_eq_sum_sub', indicate_iterConv_apply, mem_piFinset, Finset.sum_apply,
    translate_apply, â† nsmul_eq_mul, â† sum_const, Finset.sum_fiberwise']

lemma conv_indicate_iterConv (f : Î± â†’ Î²) (s : Finset Î±) (n : â„•) :
    f âˆ— ğŸ­ s âˆ—^ n = âˆ‘ a âˆˆ s ^^ n, Ï„ (âˆ‘ i, a i) f := by
  ext b
  simp only [conv_eq_sum_sub, indicate_iterConv_apply, mem_piFinset, Finset.sum_apply,
    translate_apply, â† nsmul_eq_mul', â† sum_const, Finset.sum_fiberwise']

lemma indicate_iterConv_dconv (s : Finset Î±) (n : â„•) (f : Î± â†’ Î²) :
    ğŸ­ s âˆ—^ n â—‹ f = âˆ‘ a âˆˆ s ^^ n, Ï„ (âˆ‘ i, a i) (conjneg f) := by
  rw [â† conv_conjneg, indicate_iterConv_conv]

lemma dconv_indicate_iterConv (f : Î± â†’ Î²) (s : Finset Î±) (n : â„•) :
    f â—‹ ğŸ­ s âˆ—^ n = âˆ‘ a âˆˆ s ^^ n, Ï„ (-âˆ‘ i, a i) f := by
  simp [â† conv_conjneg, conjneg_iterConv, conv_indicate_iterConv, piFinset_neg]

end CommSemiring

section Semifield
variable [Semifield Î²] [CharZero Î²] [Module â„šâ‰¥0 Î²] [StarRing Î²]

lemma mu_iterConv_conv (s : Finset Î±) (n : â„•) (f : Î± â†’ Î²) :
    Î¼ s âˆ—^ n âˆ— f = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (âˆ‘ i, a i) f := by
  simp only [mu, smul_iterConv, inv_pow, smul_conv, indicate_iterConv_conv, expect,
    card_piFinsetConst, Nat.cast_pow]
  rw [â† nnratCast_smul_eq_nnqsmul Î²]
  push_cast
  rfl

lemma conv_mu_iterConv (f : Î± â†’ Î²) (s : Finset Î±) (n : â„•) :
    f âˆ— Î¼ s âˆ—^ n = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (âˆ‘ i, a i) f := by
  rw [conv_comm, mu_iterConv_conv]

lemma mu_iterConv_dconv (s : Finset Î±) (n : â„•) (f : Î± â†’ Î²) :
    Î¼ s âˆ—^ n â—‹ f = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (âˆ‘ i, a i) (conjneg f) := by
  rw [â† conv_conjneg, mu_iterConv_conv]

lemma dconv_mu_iterConv (f : Î± â†’ Î²) (s : Finset Î±) (n : â„•) :
    f â—‹ Î¼ s âˆ—^ n = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (-âˆ‘ i, a i) f := by
  simp_rw [â† conv_conjneg, conjneg_iterConv, conjneg_mu, conv_mu_iterConv, piFinset_neg,
    expect_neg_index, Pi.neg_apply, sum_neg_distrib]

end Semifield

section Field
variable [Field Î²] [StarRing Î²] [CharZero Î²]

@[simp] lemma balance_iterConv (f : Î± â†’ Î²) : âˆ€ {n}, n â‰  0 â†’ balance (f âˆ—^ n) = balance f âˆ—^ n
  | 0, h => by cases h rfl
  | 1, _ => by simp
  | n + 2, _ => by simp [iterConv_succ _ (n + 1), balance_iterConv _ n.succ_ne_zero]

end Field
