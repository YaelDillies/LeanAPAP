import Mathlib.Algebra.Module.Pi
import Mathlib.Analysis.Complex.Basic
import LeanAPAP.Prereqs.Function.Indicator.Basic
import LeanAPAP.Prereqs.Convolution.Discrete.Defs

/-!
# Convolution

This file defines several versions of the discrete convolution of functions.

## Main declarations

* `conv`: Discrete convolution of two functions
* `dconv`: Discrete difference convolution of two functions
* `iterConv`: Iterated convolution of a function

## Notation

* `f âˆ— g`: Convolution
* `f â—‹ g`: Difference convolution
* `f âˆ—^ n`: Iterated convolution

## Notes

Some lemmas could technically be generalised to a non-commutative semiring domain. Doesn't seem very
useful given that the codomain in applications is either `â„`, `â„â‰¥0` or `â„‚`.

Similarly we could drop the commutativity assumption on the domain, but this is unneeded at this
point in time.

## TODO

Multiplicativise? Probably ugly and not very useful.
-/

local notation:70 s:70 " ^^ " n:71 => Fintype.piFinset fun _ : Fin n â†¦ s

open Finset Fintype Function
open scoped BigOperators ComplexConjugate NNReal Pointwise

variable {G R Î³ : Type*} [Fintype G] [DecidableEq G] [AddCommGroup G]

/-!
### Convolution of functions

In this section, we define the convolution `f âˆ— g` and difference convolution `f â—‹ g` of functions
`f g : G â†’ R`, and show how they interact.
-/

section CommSemiring
variable [CommSemiring R] {f g : G â†’ R}

lemma indicate_conv_indicate_apply (s t : Finset G) (a : G) :
    (ğŸ­_[R] s âˆ— ğŸ­ t) a = ((s Ã—Ë¢ t).filter fun x : G Ã— G â†¦ x.1 + x.2 = a).card := by
  simp only [conv_apply, indicate_apply, â† ite_and, filter_comm, boole_mul, sum_boole]
  simp_rw [â† mem_product, filter_univ_mem]

lemma indicate_conv (s : Finset G) (f : G â†’ R) : ğŸ­ s âˆ— f = âˆ‘ a âˆˆ s, Ï„ a f := by
  ext; simp [conv_eq_sum_sub', indicate_apply]

lemma conv_indicate (f : G â†’ R) (s : Finset G) : f âˆ— ğŸ­ s = âˆ‘ a âˆˆ s, Ï„ a f := by
  ext; simp [conv_eq_sum_sub, indicate_apply]

variable [StarRing R]

lemma indicate_dconv_indicate_apply (s t : Finset G) (a : G) :
    (ğŸ­_[R] s â—‹ ğŸ­ t) a = ((s Ã—Ë¢ t).filter fun x : G Ã— G â†¦ x.1 - x.2 = a).card := by
  simp only [dconv_apply, indicate_apply, â† ite_and, filter_comm, boole_mul, sum_boole,
    apply_ite conj, map_one, map_zero, Pi.conj_apply]
  simp_rw [â† mem_product, filter_univ_mem]

lemma indicate_dconv (s : Finset G) (f : G â†’ R) : ğŸ­ s â—‹ f = âˆ‘ a âˆˆ s, Ï„ a (conjneg f) := by
  ext; simp [dconv_eq_sum_sub', indicate_apply]

lemma dconv_indicate (f : G â†’ R) (s : Finset G) : f â—‹ ğŸ­ s = âˆ‘ a âˆˆ s, Ï„ (-a) f := by
  ext; simp [dconv_eq_sum_add, indicate_apply]

end CommSemiring

section Semifield
variable [Semifield R]

@[simp] lemma mu_univ_conv_mu_univ : Î¼_[R] (univ : Finset G) âˆ— Î¼ univ = Î¼ univ := by
  ext; cases eq_or_ne (card G : R) 0 <;> simp [mu_apply, conv_eq_sum_add, card_univ, *]

lemma mu_conv (s : Finset G) (f : G â†’ R) : Î¼ s âˆ— f = (s.card : R)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ a f := by
  simp [mu, indicate_conv, smul_conv]

lemma conv_mu (f : G â†’ R) (s : Finset G) : f âˆ— Î¼ s = (s.card : R)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ a f := by
  simp [mu, conv_indicate, conv_smul]

variable [StarRing R]

@[simp] lemma mu_univ_dconv_mu_univ : Î¼_[R] (univ : Finset G) â—‹ Î¼ univ = Î¼ univ := by
  ext; cases eq_or_ne (card G : R) 0 <;> simp [mu_apply, dconv_eq_sum_add, card_univ, *]

lemma mu_dconv (s : Finset G) (f : G â†’ R) :
    Î¼ s â—‹ f = (s.card : R)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ a (conjneg f) := by
  simp [mu, indicate_dconv, smul_dconv]

lemma dconv_mu (f : G â†’ R) (s : Finset G) : f â—‹ Î¼ s = (s.card : R)â»Â¹ â€¢ âˆ‘ a âˆˆ s, Ï„ (-a) f := by
  simp [mu, dconv_indicate, dconv_smul]

end Semifield

section Semifield
variable [Semifield R] [CharZero R]

lemma expect_conv (f g : G â†’ R) : ğ”¼ a, (f âˆ— g) a = (âˆ‘ a, f a) * ğ”¼ a, g a := by
  simp_rw [expect, sum_conv, mul_smul_comm]

lemma expect_conv' (f g : G â†’ R) : ğ”¼ a, (f âˆ— g) a = (ğ”¼ a, f a) * âˆ‘ a, g a := by
  simp_rw [expect, sum_conv, smul_mul_assoc]

variable [StarRing R]

lemma expect_dconv (f g : G â†’ R) : ğ”¼ a, (f â—‹ g) a = (âˆ‘ a, f a) * ğ”¼ a, conj (g a) := by
  simp_rw [expect, sum_dconv, mul_smul_comm]

lemma expect_dconv' (f g : G â†’ R) : ğ”¼ a, (f â—‹ g) a = (ğ”¼ a, f a) * âˆ‘ a, conj (g a) := by
  simp_rw [expect, sum_dconv, smul_mul_assoc]

end Semifield

section Field
variable [Field R] [CharZero R]

@[simp] lemma balance_conv (f g : G â†’ R) : balance (f âˆ— g) = balance f âˆ— balance g := by
  simpa [balance, conv_sub, sub_conv, expect_conv]
    using (mul_smul_comm _ _ _).trans (smul_mul_assoc _ _ _).symm

variable [StarRing R]

@[simp] lemma balance_dconv (f g : G â†’ R) : balance (f â—‹ g) = balance f â—‹ balance g := by
  simpa [balance, dconv_sub, sub_dconv, expect_dconv, map_expect]
    using (mul_smul_comm _ _ _).trans (smul_mul_assoc _ _ _).symm

end Field

/-! ### Iterated convolution -/

section CommSemiring
variable [CommSemiring R] {f g : G â†’ R} {n : â„•}

lemma indicate_iterConv_apply (s : Finset G) (n : â„•) (a : G) :
    (ğŸ­_[R] s âˆ—^ n) a = ((s ^^ n).filter fun x : Fin n â†’ G â†¦ âˆ‘ i, x i = a).card := by
  induction' n with n ih generalizing a
  Â· simp [apply_ite card, eq_comm]
  simp_rw [iterConv_succ', conv_eq_sum_sub', ih, indicate_apply, boole_mul, sum_ite,
    filter_univ_mem, sum_const_zero, add_zero, â† Nat.cast_sum, â† Finset.card_sigma]
  congr 1
  refine card_equiv ((Equiv.sigmaEquivProd ..).trans <| Fin.consEquiv fun _ â†¦ G) ?_
  aesop (add simp [Fin.sum_cons, Fin.forall_fin_succ])

lemma indicate_iterConv_conv (s : Finset G) (n : â„•) (f : G â†’ R) :
    ğŸ­ s âˆ—^ n âˆ— f = âˆ‘ a âˆˆ s ^^ n, Ï„ (âˆ‘ i, a i) f := by
  ext b
  simp only [conv_eq_sum_sub', indicate_iterConv_apply, mem_piFinset, Finset.sum_apply,
    translate_apply, â† nsmul_eq_mul, â† sum_const, Finset.sum_fiberwise']

lemma conv_indicate_iterConv (f : G â†’ R) (s : Finset G) (n : â„•) :
    f âˆ— ğŸ­ s âˆ—^ n = âˆ‘ a âˆˆ s ^^ n, Ï„ (âˆ‘ i, a i) f := by
  ext b
  simp only [conv_eq_sum_sub, indicate_iterConv_apply, mem_piFinset, Finset.sum_apply,
    translate_apply, â† nsmul_eq_mul', â† sum_const, Finset.sum_fiberwise']

variable [StarRing R]

lemma indicate_iterConv_dconv (s : Finset G) (n : â„•) (f : G â†’ R) :
    ğŸ­ s âˆ—^ n â—‹ f = âˆ‘ a âˆˆ s ^^ n, Ï„ (âˆ‘ i, a i) (conjneg f) := by
  rw [â† conv_conjneg, indicate_iterConv_conv]

lemma dconv_indicate_iterConv (f : G â†’ R) (s : Finset G) (n : â„•) :
    f â—‹ ğŸ­ s âˆ—^ n = âˆ‘ a âˆˆ s ^^ n, Ï„ (-âˆ‘ i, a i) f := by
  simp [â† conv_conjneg, conjneg_iterConv, conv_indicate_iterConv, piFinset_neg]

end CommSemiring

section Semifield
variable [Semifield R] [CharZero R]

lemma mu_iterConv_conv (s : Finset G) (n : â„•) (f : G â†’ R) :
    Î¼ s âˆ—^ n âˆ— f = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (âˆ‘ i, a i) f := by
  simp only [mu, smul_iterConv, inv_pow, smul_conv, indicate_iterConv_conv, expect,
    card_piFinset_const, Nat.cast_pow]
  rw [â† NNRat.cast_smul_eq_nnqsmul R]
  push_cast
  rfl

lemma conv_mu_iterConv (f : G â†’ R) (s : Finset G) (n : â„•) :
    f âˆ— Î¼ s âˆ—^ n = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (âˆ‘ i, a i) f := by
  rw [conv_comm, mu_iterConv_conv]

variable [StarRing R]

lemma mu_iterConv_dconv (s : Finset G) (n : â„•) (f : G â†’ R) :
    Î¼ s âˆ—^ n â—‹ f = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (âˆ‘ i, a i) (conjneg f) := by
  rw [â† conv_conjneg, mu_iterConv_conv]

lemma dconv_mu_iterConv (f : G â†’ R) (s : Finset G) (n : â„•) :
    f â—‹ Î¼ s âˆ—^ n = ğ”¼ a âˆˆ piFinset (fun _ : Fin n â†¦ s), Ï„ (-âˆ‘ i, a i) f := by
  simp_rw [â† conv_conjneg, conjneg_iterConv, conjneg_mu, conv_mu_iterConv, piFinset_neg,
    expect_neg_index, Pi.neg_apply, sum_neg_distrib]

end Semifield

section Field
variable [Field R] [CharZero R]

@[simp] lemma balance_iterConv (f : G â†’ R) : âˆ€ {n}, n â‰  0 â†’ balance (f âˆ—^ n) = balance f âˆ—^ n
  | 0, h => by cases h rfl
  | 1, _ => by simp
  | n + 2, _ => by simp [iterConv_succ _ (n + 1), balance_iterConv _ n.succ_ne_zero]

end Field
