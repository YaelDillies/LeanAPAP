import LeanAPAP.Prereqs.Discrete.Convolution.Compact
import LeanAPAP.Prereqs.Discrete.DFT.Basic
import LeanAPAP.Prereqs.Expect.Complex

/-!
# Discrete Fourier transform in the compact normalisation

This file defines the discrete Fourier transform in the compact normalisation and shows the
Parseval-Plancherel identity and Fourier inversion formula for it.
-/

noncomputable section

open AddChar Finset Function
open Fintype (card)
open scoped BigOps ComplexConjugate ComplexOrder

variable {Î± Î³ : Type*} [AddCommGroup Î±] [Fintype Î±] {f : Î± â†’ â„‚} {Ïˆ : AddChar Î± â„‚} {n : â„•}

/-- The discrete Fourier transform. -/
def cft (f : Î± â†’ â„‚) : AddChar Î± â„‚ â†’ â„‚ := fun Ïˆ â†¦ âŸªÏˆ, fâŸ«â‚™_[â„‚]

lemma cft_apply (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : cft f Ïˆ = âŸªÏˆ, fâŸ«â‚™_[â„‚] := rfl

@[simp] lemma cft_zero : cft (0 : Î± â†’ â„‚) = 0 := by ext; simp [cft_apply]

@[simp] lemma cft_add (f g : Î± â†’ â„‚) : cft (f + g) = cft f + cft g := by
  ext; simp [nl2Inner_add_right, cft_apply]

@[simp] lemma cft_neg (f : Î± â†’ â„‚) : cft (-f) = - cft f := by ext; simp [cft_apply]

@[simp] lemma cft_sub (f g : Î± â†’ â„‚) : cft (f - g) = cft f - cft g := by
  ext; simp [nl2Inner_sub_right, cft_apply]

@[simp] lemma cft_const (a : â„‚) (hÏˆ : Ïˆ â‰  0) : cft (const Î± a) Ïˆ = 0 := by
  simp only [cft_apply, nl2Inner_eq_expect, const_apply, â†expect_mul, â†map_expect,
    expect_eq_zero_iff_ne_zero.2 hÏˆ, map_zero, zero_mul]

@[simp] lemma cft_smul [DistribSMul Î³ â„‚] [Star Î³] [StarModule Î³ â„‚] [IsScalarTower Î³ â„‚ â„‚]
    [SMulCommClass Î³ â„‚ â„‚] (c : Î³) (f : Î± â†’ â„‚) : cft (c â€¢ f) = c â€¢ cft f := by
  ext; simp [nl2Inner_smul_right, cft_apply]

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
@[simp] lemma l2Inner_cft (f g : Î± â†’ â„‚) : âŸªcft f, cft gâŸ«_[â„‚] = âŸªf, gâŸ«â‚™_[â„‚] := by
  classical
  unfold cft
  simp_rw [l2Inner_eq_sum, nl2Inner_eq_expect, map_expect, map_mul, starRingEnd_self_apply, expect_mul,
    mul_expect, â† expect_sum_comm, mul_mul_mul_comm _ (conj $ f _), â† sum_mul, â†
    AddChar.inv_apply_eq_conj, â†map_neg_eq_inv, â†map_add_mul, AddChar.sum_apply_eq_ite]
  simp [add_neg_eq_zero, card_univ, mul_div_cancel_left, Fintype.card_ne_zero]

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
@[simp] lemma l2Norm_cft (f : Î± â†’ â„‚) : â€–cft fâ€–_[2] = â€–fâ€–â‚™_[2] :=
  (sq_eq_sq lpNorm_nonneg nlpNorm_nonneg).1 $ Complex.ofReal_injective $ by
    push_cast; simpa only [nl2Inner_self, l2Inner_self] using l2Inner_cft f f

/-- **Fourier inversion** for the discrete Fourier transform. -/
lemma cft_inversion (f : Î± â†’ â„‚) (a : Î±) : âˆ‘ Ïˆ, cft f Ïˆ * Ïˆ a = f a := by
  classical simp_rw [cft, nl2Inner_eq_expect, expect_mul, â† expect_sum_comm, mul_right_comm _ (f _),
    â† sum_mul, â†AddChar.inv_apply_eq_conj, inv_mul_eq_div, â†map_sub_eq_div, AddChar.sum_apply_eq_ite, sub_eq_zero, ite_mul, zero_mul, Fintype.expect_ite_eq]
  simp [add_neg_eq_zero, card_univ, mul_div_cancel_left, Fintype.card_ne_zero]

lemma dft_cft_doubleDualEmb (f : Î± â†’ â„‚) (a : Î±) : dft (cft f) (doubleDualEmb a) = f (-a) := by
  simp only [â† cft_inversion f (-a), mul_comm (conj _), dft_apply, l2Inner_eq_sum, map_neg_eq_inv,
    AddChar.inv_apply_eq_conj, doubleDualEmb_apply]

lemma cft_dft_doubleDualEmb (f : Î± â†’ â„‚) (a : Î±) : cft (dft f) (doubleDualEmb a) = f (-a) := by
  simp only [â† dft_inversion f (-a), mul_comm (conj _), cft_apply, nl2Inner_eq_expect,
    map_neg_eq_inv, AddChar.inv_apply_eq_conj, doubleDualEmb_apply]

lemma dft_cft (f : Î± â†’ â„‚) : dft (cft f) = f âˆ˜ doubleDualEquiv.symm âˆ˜ Neg.neg :=
  funext fun a â†¦ by simp_rw [Function.comp_apply, map_neg, â†dft_cft_doubleDualEmb,
      doubleDualEmb_doubleDualEquiv_symm_apply]

lemma cft_dft (f : Î± â†’ â„‚) : cft (dft f) = f âˆ˜ doubleDualEquiv.symm âˆ˜ Neg.neg :=
  funext fun a â†¦ by simp_rw [Function.comp_apply, map_neg, â†cft_dft_doubleDualEmb,
      doubleDualEmb_doubleDualEquiv_symm_apply]

lemma cft_injective : Injective (cft : (Î± â†’ â„‚) â†’ AddChar Î± â„‚ â†’ â„‚) := fun f g h â†¦
  funext fun a â†¦ (cft_inversion _ _).symm.trans $ by rw [h, cft_inversion]

lemma cft_inv (Ïˆ : AddChar Î± â„‚) (hf : IsSelfAdjoint f) : cft f Ïˆâ»Â¹ = conj (cft f Ïˆ) := by
  simp_rw [cft_apply, nl2Inner_eq_expect, map_expect, AddChar.inv_apply', map_mul,
    AddChar.inv_apply_eq_conj, Complex.conj_conj, (hf.apply _).conj_eq]

@[simp]
lemma cft_conj (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : cft (conj f) Ïˆ = conj (cft f Ïˆâ»Â¹) := by
  simp only [cft_apply, nl2Inner_eq_expect, map_expect, map_mul, â†inv_apply', â†inv_apply_eq_conj,
    inv_inv, Pi.conj_apply]

lemma cft_conjneg_apply (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : cft (conjneg f) Ïˆ = conj (cft f Ïˆ) := by
  simp only [cft_apply, nl2Inner_eq_expect, conjneg_apply, map_expect, map_mul, IsROrC.conj_conj]
  refine Fintype.expect_equiv (Equiv.neg _) _ _ fun i â†¦ ?_
  simp only [Equiv.neg_apply, â†inv_apply_eq_conj, â†inv_apply', inv_apply]

@[simp]
lemma cft_conjneg (f : Î± â†’ â„‚) : cft (conjneg f) = conj (cft f) := funext $ cft_conjneg_apply _

@[simp] lemma cft_balance (f : Î± â†’ â„‚) (hÏˆ : Ïˆ â‰  0) : cft (balance f) Ïˆ = cft f Ïˆ := by
  simp only [balance, Pi.sub_apply, cft_sub, cft_const _ hÏˆ, sub_zero]

lemma cft_dilate (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) (hn : n.Coprime (card Î±)) :
    cft (dilate f n) Ïˆ = cft f (Ïˆ ^ n) := by
  simp_rw [cft_apply, nl2Inner_eq_expect, dilate]
  refine' expect_nbij' ((nâ»Â¹ : ZMod (card Î±)).val â€¢ Â·) _ (fun x _ â†¦ _) (n â€¢ Â·) _ _ _ <;>
    simp only [pow_apply, â†map_nsmul_pow, mem_univ, nsmul_zmod_val_inv_nsmul hn,
      zmod_val_inv_nsmul_nsmul hn, eq_self_iff_true, forall_const]

@[simp] lemma cft_trivNChar [DecidableEq Î±] : cft (trivNChar : Î± â†’ â„‚) = 1 := by
  ext; simp [trivChar_apply, cft_apply, nl2Inner_eq_expect, â†map_expect, card_univ]

@[simp] lemma cft_one : cft (1 : Î± â†’ â„‚) = trivChar :=
  dft_injective $ by classical rw [dft_trivChar, dft_cft, Pi.one_comp]

variable [DecidableEq Î±]

@[simp] lemma cft_indicate_zero (s : Finset Î±) : cft (ğŸ­ s) 0 = s.dens := by
  simp only [cft_apply, nl2Inner_eq_expect, expect_indicate, AddChar.zero_apply, map_one, one_mul,
    dens]

lemma cft_nconv_apply (f g : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : cft (f âˆ—â‚™ g) Ïˆ = cft f Ïˆ * cft g Ïˆ := by
  simp_rw [cft, nl2Inner_eq_expect, nconv_eq_expect_sub', mul_expect, expect_mul, â†expect_product',
    univ_product_univ]
  refine'
    expect_nbij' (fun x â†¦ (x.1 - x.2, x.2)) (by simp) (fun x _ â†¦ _) (fun x â†¦ (x.1 + x.2, x.2))
      (by simp) (by simp) (by simp)
  rw [mul_mul_mul_comm, â†map_mul, â†map_add_mul, add_sub_cancel'_right]

lemma cft_ndconv_apply (f g : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) :
    cft (f â—‹â‚™ g) Ïˆ = cft f Ïˆ * conj (cft g Ïˆ) := by
  rw [â†nconv_conjneg, cft_nconv_apply, cft_conjneg_apply]

@[simp] lemma cft_nconv (f g : Î± â†’ â„‚) : cft (f âˆ—â‚™ g) = cft f * cft g :=
  funext $ cft_nconv_apply _ _

@[simp]
lemma cft_ndconv (f g : Î± â†’ â„‚) : cft (f â—‹â‚™ g) = cft f * conj (cft g) :=
  funext $ cft_ndconv_apply _ _

@[simp] lemma cft_iterNConv (f : Î± â†’ â„‚) : âˆ€ n, cft (f âˆ—^â‚™ n) = cft f ^ n
  | 0 => cft_trivNChar
  | n + 1 => by simp [iterNConv_succ, pow_succ, cft_iterNConv]

@[simp] lemma cft_iterNConv_apply (f : Î± â†’ â„‚) (n : â„•) (Ïˆ : AddChar Î± â„‚) :
    cft (f âˆ—^â‚™ n) Ïˆ = cft f Ïˆ ^ n := congr_fun (cft_iterNConv _ _) _

lemma nlpNorm_nconv_le_nlpNorm_ndconv (hnâ‚€ : n â‰  0) (hn : Even n) (f : Î± â†’ â„‚) :
    â€–f âˆ—â‚™ fâ€–â‚™_[n] â‰¤ â€–f â—‹â‚™ fâ€–â‚™_[n] := by
  cases isEmpty_or_nonempty Î±
  Â· rw [Subsingleton.elim (f âˆ—â‚™ f) (f â—‹â‚™ f)]
  refine le_of_pow_le_pow_left hnâ‚€ (by positivity) ?_
  obtain âŸ¨n, rflâŸ© := hn.two_dvd
  simp_rw [nlpNorm_pow_eq_expect hnâ‚€, â† cft_inversion (f âˆ—â‚™ f), â† cft_inversion (f â—‹â‚™ f),
    cft_nconv, cft_ndconv, Pi.mul_apply]
  rw [â†Real.norm_of_nonneg (expect_nonneg fun i _ â†¦ ?_), â†Complex.norm_real]
  rw [Complex.ofReal_expect (univ : Finset Î±)]
  any_goals positivity
  simp_rw [pow_mul', â†norm_pow _ n, Complex.ofReal_pow, â†Complex.conj_mul', map_pow, map_sum,
    map_mul, Fintype.sum_pow, Fintype.sum_mul_sum]
  sorry
  -- simp only [@expect_comm _ _ Î±, â†mul_expect, prod_mul_prod_comm]
  -- refine' (norm_expect_le _ _).trans_eq (Complex.ofReal_injective _)
  -- simp only [norm_mul, norm_prod, IsROrC.norm_conj, â†pow_mul]
  -- push_cast
  -- have : âˆ€ f g : Fin n â†’ AddChar Î± â„‚, 0 â‰¤ âˆ‘ a, âˆ i, conj (f i a) * g i a := by
  --   rintro f g
  --   suffices : âˆ‘ a, âˆ i, conj (f i a) * g i a = if âˆ‘ i, (g i - f i) = 0 then (card Î± : â„‚) else 0
  --   Â· rw [this]
  --     split_ifs <;> positivity
  --   simp_rw [â†AddChar.expect_eq_ite, AddChar.expect_apply, AddChar.sub_apply, AddChar.map_neg_eq_inv,
  --     AddChar.inv_apply_eq_conj, mul_comm]
  -- simp only [IsROrC.ofReal_pow, pow_mul, â†Complex.conj_mul', map_expect, map_mul, Complex.conj_conj,
  --   Pi.conj_apply, mul_pow, Fintype.expect_pow, â†sq, Fintype.expect_mul_expect]
  -- conv_lhs =>
  --   arg 2
  --   ext
  --   rw [â†Complex.eq_coe_norm_of_nonneg (this _ _)]
  -- simp only [@expect_comm _ _ Î±, mul_expect, map_prod, map_mul, IsROrC.conj_conj, â†prod_mul_distrib]
  -- refine' expect_congr rfl fun x _ â†¦ expect_congr rfl fun a _ â†¦ prod_congr rfl fun i _ â†¦ _
  -- ring

--TODO: Can we unify with `nlpNorm_nconv_le_nlpNorm_ndconv`?
lemma nlpNorm_nconv_le_nlpNorm_ndconv' (hnâ‚€ : n â‰  0) (hn : Even n) (f : Î± â†’ â„) :
    â€–f âˆ—â‚™ fâ€–â‚™_[n] â‰¤ â€–f â—‹â‚™ fâ€–â‚™_[n] := by
  simpa only [â†Complex.coe_comp_nconv, â†Complex.coe_comp_ndconv, Complex.nlpNorm_coe_comp] using
    nlpNorm_nconv_le_nlpNorm_ndconv hnâ‚€ hn ((â†‘) âˆ˜ f)
