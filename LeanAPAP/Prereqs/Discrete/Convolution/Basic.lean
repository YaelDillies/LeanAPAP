import Mathlib.Analysis.Complex.Basic
import LeanAPAP.Mathlib.Algebra.BigOperators.Basic
import LeanAPAP.Mathlib.Data.Fintype.Basic
import LeanAPAP.Mathlib.Data.Real.NNReal
import LeanAPAP.Prereqs.Indicator

/-!
# Convolution

This file defines several versions of the discrete convolution of functions.

## Main declarations

* `conv`: Discrete convolution of two functions
* `dconv`: Discrete difference convolution of two functions
* `iterConv`: Iterated convolution of a function

## Notation

* `f âˆ— g`: Convolution
* `f â—‹ g`: Difference convolution
* `f âˆ—^ n`: Iterated convolution

## Notes

Some lemmas could technically be generalised to a non-commutative semiring domain. Doesn't seem very
useful given that the codomain in applications is either `â„`, `â„â‰¥0` or `â„‚`.

Similarly we could drop the commutativity assumption on the domain, but this is unneeded at this
point in time.

## TODO

Multiplicativise? Probably ugly and not very useful.
-/

open Finset Fintype Function
open scoped BigOps ComplexConjugate NNReal Pointwise

variable {Î± Î² Î³ : Type*} [Fintype Î±] [DecidableEq Î±] [AddCommGroup Î±]

/-!
### Convolution of functions

In this section, we define the convolution `f âˆ— g` and difference convolution `f â—‹ g` of functions
`f g : Î± â†’ Î²`, and show how they interact.
-/

section CommSemiring
variable [CommSemiring Î²] [StarRing Î²] {f g : Î± â†’ Î²}

/-- Convolution -/
def conv (f g : Î± â†’ Î²) : Î± â†’ Î² := fun a â†¦ âˆ‘ x : Î± Ã— Î± with x.1 + x.2 = a , f x.1 * g x.2

/-- Difference convolution -/
def dconv (f g : Î± â†’ Î²) : Î± â†’ Î² := fun a â†¦ âˆ‘ x : Î± Ã— Î± with x.1 - x.2 = a, f x.1 * conj g x.2

/-- The trivial character. -/
def trivChar : Î± â†’ Î² := fun a â†¦ if a = 0 then 1 else 0

infixl:71 " âˆ— " => conv
infixl:71 " â—‹ " => dconv

lemma conv_apply (f g : Î± â†’ Î²) (a : Î±) :
    (f âˆ— g) a = âˆ‘ x : Î± Ã— Î± with x.1 + x.2 = a, f x.1 * g x.2 := rfl

lemma dconv_apply (f g : Î± â†’ Î²) (a : Î±) :
    (f â—‹ g) a = âˆ‘ x : Î± Ã— Î± with x.1 - x.2 = a , f x.1 * conj g x.2 := rfl

@[simp] lemma trivChar_apply (a : Î±) : (trivChar a : Î²) = if a = 0 then 1 else 0 := rfl

@[simp] lemma conv_conjneg (f g : Î± â†’ Î²) : f âˆ— conjneg g = f â—‹ g :=
  funext fun a â†¦ sum_equiv ((Equiv.refl _).prodCongr $ Equiv.neg _) (by simp) (by simp)

@[simp] lemma dconv_conjneg (f g : Î± â†’ Î²) : f â—‹ conjneg g = f âˆ— g := by
  rw [â†conv_conjneg, conjneg_conjneg]

@[simp] lemma translate_conv (a : Î±) (f g : Î± â†’ Î²) : Ï„ a f âˆ— g = Ï„ a (f âˆ— g) :=
  funext fun b â†¦ sum_equiv ((Equiv.subRight a).prodCongr $ Equiv.refl _)
    (by simp [sub_add_eq_add_sub]) (by simp)

@[simp] lemma translate_dconv (a : Î±) (f g : Î± â†’ Î²) : Ï„ a f â—‹ g = Ï„ a (f â—‹ g) :=
  funext fun b â†¦ sum_equiv ((Equiv.subRight a).prodCongr $ Equiv.refl _)
    (by simp [sub_right_comm _ a]) (by simp)

@[simp] lemma conv_translate (a : Î±) (f g : Î± â†’ Î²) : f âˆ— Ï„ a g = Ï„ a (f âˆ— g) :=
  funext fun b â†¦ sum_equiv ((Equiv.refl _).prodCongr $ Equiv.subRight a)
    (by simp [â† add_sub_assoc]) (by simp)

@[simp] lemma dconv_translate (a : Î±) (f g : Î± â†’ Î²) : f â—‹ Ï„ a g = Ï„ (-a) (f â—‹ g) :=
  funext fun b â†¦ sum_equiv ((Equiv.refl _).prodCongr $ Equiv.subRight a)
    (by simp [sub_sub_eq_add_sub, â† sub_add_eq_add_sub]) (by simp)

lemma conv_comm (f g : Î± â†’ Î²) : f âˆ— g = g âˆ— f :=
  funext fun a â†¦ sum_nbij' Prod.swap
    (fun x hx â†¦ by simpa [add_comm] using hx) (fun x _ â†¦ mul_comm _ _)
    Prod.swap (fun x hx â†¦ by simpa [add_comm] using hx) (fun x _ â†¦ x.swap_swap)
    fun x _ â†¦ x.swap_swap

@[simp] lemma conj_conv (f g : Î± â†’ Î²) : conj (f âˆ— g) = conj f âˆ— conj g :=
  funext fun a â†¦ by simp only [Pi.conj_apply, conv_apply, map_sum, map_mul]

@[simp] lemma conj_dconv (f g : Î± â†’ Î²) : conj (f â—‹ g) = conj f â—‹ conj g := by
  simp_rw [â† conv_conjneg, conj_conv, conjneg_conj]

@[simp] lemma conj_trivChar : conj (trivChar : Î± â†’ Î²) = trivChar := by ext; simp

lemma IsSelfAdjoint.conv (hf : IsSelfAdjoint f) (hg : IsSelfAdjoint g) : IsSelfAdjoint (f âˆ— g) :=
  (conj_conv _ _).trans $ congr_argâ‚‚ _ hf hg

lemma IsSelfAdjoint.dconv (hf : IsSelfAdjoint f) (hg : IsSelfAdjoint g) : IsSelfAdjoint (f â—‹ g) :=
  (conj_dconv _ _).trans $ congr_argâ‚‚ _ hf hg

@[simp] lemma isSelfAdjoint_trivChar : IsSelfAdjoint (trivChar : Î± â†’ Î²) := conj_trivChar

@[simp]lemma conjneg_conv (f g : Î± â†’ Î²) : conjneg (f âˆ— g) = conjneg f âˆ— conjneg g := by
  funext a
  simp only [conv_apply, conjneg_apply, map_sum, map_mul]
  convert Equiv.sum_comp_finset (Equiv.neg (Î± Ã— Î±)) _ rfl using 2
  rw [â†Equiv.coe_toEmbedding, â†map_eq_image (Equiv.neg (Î± Ã— Î±)).symm.toEmbedding, map_filter]
  simp [Function.comp, â†neg_eq_iff_eq_neg, add_comm]

@[simp] lemma conjneg_dconv (f g : Î± â†’ Î²) : conjneg (f â—‹ g) = g â—‹ f := by
  simp_rw [â†conv_conjneg, conjneg_conv, conjneg_conjneg, conv_comm]

@[simp] lemma conjneg_trivChar : conjneg (trivChar : Î± â†’ Î²) = trivChar := by ext; simp

lemma conv_assoc (f g h : Î± â†’ Î²) : f âˆ— g âˆ— h = f âˆ— (g âˆ— h) := by
  ext a
  simp only [sum_mul, mul_sum, conv_apply, sum_sigma']
  refine' sum_bij' (fun x _ â†¦ âŸ¨(x.2.1, x.2.2 + x.1.2), (x.2.2, x.1.2)âŸ©) _ _
    (fun x _ â†¦ âŸ¨(x.1.1 + x.2.1, x.2.2), (x.1.1, x.2.1)âŸ©) _ _ _ <;>
    simp only [mem_sigma, mem_filter, mem_univ, true_and_iff, Sigma.forall, Prod.forall, and_imp,
      heq_iff_eq] <;>
    rintro b c d e rfl rfl <;>
    simp only [add_assoc, mul_assoc, Prod.mk.eta, eq_self_iff_true, and_self_iff]

lemma conv_right_comm (f g h : Î± â†’ Î²) : f âˆ— g âˆ— h = f âˆ— h âˆ— g := by
  rw [conv_assoc, conv_assoc, conv_comm g]

lemma conv_left_comm (f g h : Î± â†’ Î²) : f âˆ— (g âˆ— h) = g âˆ— (f âˆ— h) := by
  rw [â†conv_assoc, â†conv_assoc, conv_comm g]

lemma conv_conv_conv_comm (f g h i : Î± â†’ Î²) : f âˆ— g âˆ— (h âˆ— i) = f âˆ— h âˆ— (g âˆ— i) := by
  rw [conv_assoc, conv_assoc, conv_left_comm g]

lemma conv_dconv_conv_comm (f g h i : Î± â†’ Î²) : f âˆ— g â—‹ (h âˆ— i) = f â—‹ h âˆ— (g â—‹ i) := by
  simp_rw [â†conv_conjneg, conjneg_conv, conv_conv_conv_comm]

lemma dconv_conv_dconv_comm (f g h i : Î± â†’ Î²) : f â—‹ g âˆ— (h â—‹ i) = f âˆ— h â—‹ (g âˆ— i) := by
  simp_rw [â†conv_conjneg, conjneg_conv, conv_conv_conv_comm]

lemma dconv_dconv_dconv_comm (f g h i : Î± â†’ Î²) : f â—‹ g â—‹ (h â—‹ i) = f â—‹ h â—‹ (g â—‹ i) := by
  simp_rw [â†conv_conjneg, conjneg_conv, conv_conv_conv_comm]

@[simp] lemma conv_zero (f : Î± â†’ Î²) : f âˆ— 0 = 0 := by ext; simp [conv_apply]
@[simp] lemma zero_conv (f : Î± â†’ Î²) : 0 âˆ— f = 0 := by ext; simp [conv_apply]
@[simp] lemma dconv_zero (f : Î± â†’ Î²) : f â—‹ 0 = 0 := by simp [â†conv_conjneg]
@[simp] lemma zero_dconv (f : Î± â†’ Î²) : 0 â—‹ f = 0 := by rw [â†conv_conjneg]; simp [-conv_conjneg]

lemma conv_add (f g h : Î± â†’ Î²) : f âˆ— (g + h) = f âˆ— g + f âˆ— h := by
  ext; simp only [conv_apply, mul_add, sum_add_distrib, Pi.add_apply]

lemma add_conv (f g h : Î± â†’ Î²) : (f + g) âˆ— h = f âˆ— h + g âˆ— h := by
  ext; simp only [conv_apply, add_mul, sum_add_distrib, Pi.add_apply]

lemma dconv_add (f g h : Î± â†’ Î²) : f â—‹ (g + h) = f â—‹ g + f â—‹ h := by
  simp_rw [â†conv_conjneg, conjneg_add, conv_add]

lemma add_dconv (f g h : Î± â†’ Î²) : (f + g) â—‹ h = f â—‹ h + g â—‹ h := by
  simp_rw [â†conv_conjneg, add_conv]

lemma smul_conv [DistribSMul Î³ Î²] [IsScalarTower Î³ Î² Î²] (c : Î³) (f g : Î± â†’ Î²) :
    c â€¢ f âˆ— g = c â€¢ (f âˆ— g) := by
  ext a; simp only [Pi.smul_apply, smul_sum, conv_apply, smul_mul_assoc]

lemma smul_dconv [DistribSMul Î³ Î²] [IsScalarTower Î³ Î² Î²] (c : Î³) (f g : Î± â†’ Î²) :
    c â€¢ f â—‹ g = c â€¢ (f â—‹ g) := by
  ext a; simp only [Pi.smul_apply, smul_sum, dconv_apply, smul_mul_assoc]

lemma conv_smul [DistribSMul Î³ Î²] [SMulCommClass Î³ Î² Î²] (c : Î³) (f g : Î± â†’ Î²) :
    f âˆ— c â€¢ g = c â€¢ (f âˆ— g) := by
  ext a; simp only [Pi.smul_apply, smul_sum, conv_apply, mul_smul_comm]

lemma dconv_smul [Star Î³] [DistribSMul Î³ Î²] [SMulCommClass Î³ Î² Î²] [StarModule Î³ Î²] (c : Î³)
    (f g : Î± â†’ Î²) : f â—‹ c â€¢ g = star c â€¢ (f â—‹ g) := by
  ext a;
    simp only [Pi.smul_apply, smul_sum, dconv_apply, mul_smul_comm, starRingEnd_apply,
      star_smul]

alias smul_conv_assoc := smul_conv
alias smul_dconv_assoc := smul_dconv
alias smul_conv_left_comm := conv_smul
alias smul_dconv_left_comm := dconv_smul

lemma mul_smul_conv_comm [Monoid Î³] [DistribMulAction Î³ Î²] [IsScalarTower Î³ Î² Î²]
    [SMulCommClass Î³ Î² Î²] (c d : Î³) (f g : Î± â†’ Î²) : (c * d) â€¢ (f âˆ— g) = c â€¢ f âˆ— d â€¢ g := by
  rw [smul_conv, conv_smul, mul_smul]

lemma map_conv {Î³} [CommSemiring Î³] [StarRing Î³] (m : Î² â†’+* Î³) (f g : Î± â†’ Î²) (a : Î±) :
    m ((f âˆ— g) a) = (m âˆ˜ f âˆ— m âˆ˜ g) a := by
  simp_rw [conv_apply, map_sum, map_mul, Function.comp_apply]

lemma comp_conv {Î³} [CommSemiring Î³] [StarRing Î³] (m : Î² â†’+* Î³) (f g : Î± â†’ Î²) :
    m âˆ˜ (f âˆ— g) = m âˆ˜ f âˆ— m âˆ˜ g :=
  funext $ map_conv _ _ _

--TODO: Can we generalise to star ring homs?
lemma map_dconv (f g : Î± â†’ â„â‰¥0) (a : Î±) : (â†‘((f â—‹ g) a) : â„) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := by
  simp_rw [dconv_apply, NNReal.coe_sum, NNReal.coe_mul, starRingEnd_apply, star_trivial,
    Function.comp_apply]

lemma conv_eq_sum_sub (f g : Î± â†’ Î²) (a : Î±) : (f âˆ— g) a = âˆ‘ t, f (a - t) * g t := by
  rw [conv_apply]
  refine' sum_bij (fun x _ â†¦ x.2) (fun x _ â†¦ mem_univ _) _ _ fun b _ â†¦
    âŸ¨(a - b, b), mem_filter.2 âŸ¨mem_univ _, sub_add_cancel _ _âŸ©, rflâŸ© <;>
      simp only [mem_filter, mem_univ, true_and_iff, Prod.forall]
  Â· rintro b c rfl
    rw [add_sub_cancel]
  Â· rintro b c d e h rfl rfl
    simpa [Prod.ext_iff] using h

lemma conv_eq_sum_add (f g : Î± â†’ Î²) (a : Î±) : (f âˆ— g) a = âˆ‘ t, f (a + t) * g (-t) :=
  (conv_eq_sum_sub _ _ _).trans $ Fintype.sum_equiv (Equiv.neg _) _ _ fun t â†¦ by
    simp only [sub_eq_add_neg, Equiv.neg_apply, neg_neg]

lemma dconv_eq_sum_add (f g : Î± â†’ Î²) (a : Î±) : (f â—‹ g) a = âˆ‘ t, f (a + t) * conj (g t) := by
  simp [â†conv_conjneg, conv_eq_sum_add]

lemma conv_eq_sum_sub' (f g : Î± â†’ Î²) (a : Î±) : (f âˆ— g) a = âˆ‘ t, f t * g (a - t) := by
  rw [conv_comm, conv_eq_sum_sub]; simp_rw [mul_comm]

lemma dconv_eq_sum_sub (f g : Î± â†’ Î²) (a : Î±) : (f â—‹ g) a = âˆ‘ t, f t * conj (g (t - a)) := by
  simp [â†conv_conjneg, conv_eq_sum_sub']

lemma conv_eq_sum_add' (f g : Î± â†’ Î²) (a : Î±) : (f âˆ— g) a = âˆ‘ t, f (-t) * g (a + t) := by
  rw [conv_comm, conv_eq_sum_add]; simp_rw [mul_comm]

lemma conv_apply_add (f g : Î± â†’ Î²) (a b : Î±) : (f âˆ— g) (a + b) = âˆ‘ t, f (a + t) * g (b - t) :=
  (conv_eq_sum_sub _ _ _).trans $ Fintype.sum_equiv (Equiv.subLeft b) _ _ fun t â†¦ by
    simp [add_sub_assoc, â†sub_add]

lemma dconv_apply_neg (f g : Î± â†’ Î²) (a : Î±) : (f â—‹ g) (-a) = conj ((g â—‹ f) a) := by
  rw [â†conjneg_dconv f, conjneg_apply, Complex.conj_conj]

lemma dconv_apply_sub (f g : Î± â†’ Î²) (a b : Î±) :
    (f â—‹ g) (a - b) = âˆ‘ t, f (a + t) * conj (g (b + t)) := by
  simp [â†conv_conjneg, sub_eq_add_neg, conv_apply_add, add_comm]

lemma sum_conv_mul (f g h : Î± â†’ Î²) : âˆ‘ a, (f âˆ— g) a * h a = âˆ‘ a, âˆ‘ b, f a * g b * h (a + b) := by
  simp_rw [conv_eq_sum_sub', sum_mul]
  rw [sum_comm]
  exact sum_congr rfl fun x _ â†¦ Fintype.sum_equiv (Equiv.subRight x) _ _ fun y â†¦ by simp

lemma sum_dconv_mul (f g h : Î± â†’ Î²) :
    âˆ‘ a, (f â—‹ g) a * h a = âˆ‘ a, âˆ‘ b, f a * conj (g b) * h (a - b) := by
  simp_rw [dconv_eq_sum_sub, sum_mul]
  rw [sum_comm]
  exact Fintype.sum_congr _ _ fun x â†¦ Fintype.sum_equiv (Equiv.subLeft x) _ _ fun y â†¦ by simp

lemma sum_conv (f g : Î± â†’ Î²) : âˆ‘ a, (f âˆ— g) a = (âˆ‘ a, f a) * âˆ‘ a, g a := by
  simpa only [sum_mul_sum, sum_product, Pi.one_apply, mul_one] using sum_conv_mul f g 1

lemma sum_dconv (f g : Î± â†’ Î²) : âˆ‘ a, (f â—‹ g) a = (âˆ‘ a, f a) * âˆ‘ a, conj (g a) := by
  simpa only [sum_mul_sum, sum_product, Pi.one_apply, mul_one] using sum_dconv_mul f g 1

@[simp] lemma conv_const (f : Î± â†’ Î²) (b : Î²) : f âˆ— const _ b = const _ ((âˆ‘ x, f x) * b) := by
  ext; simp [conv_eq_sum_sub', sum_mul]

@[simp] lemma const_conv (b : Î²) (f : Î± â†’ Î²) : const _ b âˆ— f = const _ (b * âˆ‘ x, f x) := by
  ext; simp [conv_eq_sum_sub, mul_sum]

@[simp] lemma dconv_const (f : Î± â†’ Î²) (b : Î²) : f â—‹ const _ b = const _ ((âˆ‘ x, f x) * conj b) := by
  ext; simp [dconv_eq_sum_sub, sum_mul]

@[simp] lemma const_dconv (b : Î²) (f : Î± â†’ Î²) : const _ b â—‹ f = const _ (b * âˆ‘ x, conj (f x)) := by
  ext; simp [dconv_eq_sum_add, mul_sum]

@[simp] lemma conv_trivChar (f : Î± â†’ Î²) : f âˆ— trivChar = f := by ext a; simp [conv_eq_sum_sub]
@[simp] lemma trivChar_conv (f : Î± â†’ Î²) : trivChar âˆ— f = f := by rw [conv_comm, conv_trivChar]
@[simp] lemma dconv_trivChar (f : Î± â†’ Î²) : f â—‹ trivChar = f := by ext a; simp [dconv_eq_sum_add]
@[simp] lemma trivChar_dconv (f : Î± â†’ Î²) : trivChar â—‹ f = conjneg f := by
  rw [â†conv_conjneg, trivChar_conv]

lemma support_conv_subset (f g : Î± â†’ Î²) : support (f âˆ— g) âŠ† support f + support g := by
  rintro a ha
  obtain âŸ¨x, hx, hâŸ© := exists_ne_zero_of_sum_ne_zero ha
  exact âŸ¨x.1, x.2, left_ne_zero_of_mul h, right_ne_zero_of_mul h, (mem_filter.1 hx).2âŸ©

lemma support_dconv_subset (f g : Î± â†’ Î²) : support (f â—‹ g) âŠ† support f - support g := by
  simpa [sub_eq_add_neg] using support_conv_subset f (conjneg g)

lemma indicate_conv_indicate_apply (s t : Finset Î±) (a : Î±) :
    (ğŸ­_[Î²] s âˆ— ğŸ­ t) a = ((s Ã—Ë¢ t).filter fun x : Î± Ã— Î± â†¦ x.1 + x.2 = a).card := by
  simp only [conv_apply, indicate_apply, â†ite_and, filter_comm, boole_mul, sum_boole]
  simp_rw [â†mem_product, filter_mem_univ]

lemma indicate_dconv_indicate_apply (s t : Finset Î±) (a : Î±) :
    (ğŸ­_[Î²] s â—‹ ğŸ­ t) a = ((s Ã—Ë¢ t).filter fun x : Î± Ã— Î± â†¦ x.1 - x.2 = a).card := by
  simp only [dconv_apply, indicate_apply, â†ite_and, filter_comm, boole_mul, sum_boole,
    apply_ite conj, map_one, map_zero, Pi.conj_apply]
  simp_rw [â†mem_product, filter_mem_univ]

end CommSemiring

section CommRing
variable [CommRing Î²] [StarRing Î²]

@[simp] lemma conv_neg (f g : Î± â†’ Î²) : f âˆ— -g = -(f âˆ— g) := by ext; simp [conv_apply]
@[simp] lemma neg_conv (f g : Î± â†’ Î²) : -f âˆ— g = -(f âˆ— g) := by ext; simp [conv_apply]
@[simp] lemma dconv_neg (f g : Î± â†’ Î²) : f â—‹ -g = -(f â—‹ g) := by ext; simp [dconv_apply]
@[simp] lemma neg_dconv (f g : Î± â†’ Î²) : -f â—‹ g = -(f â—‹ g) := by ext; simp [dconv_apply]

lemma conv_sub (f g h : Î± â†’ Î²) : f âˆ— (g - h) = f âˆ— g - f âˆ— h := by
  simp only [sub_eq_add_neg, conv_add, conv_neg]

lemma sub_conv (f g h : Î± â†’ Î²) : (f - g) âˆ— h = f âˆ— h - g âˆ— h := by
  simp only [sub_eq_add_neg, add_conv, neg_conv]

lemma dconv_sub (f g h : Î± â†’ Î²) : f â—‹ (g - h) = f â—‹ g - f â—‹ h := by
  simp only [sub_eq_add_neg, dconv_add, dconv_neg]

lemma sub_dconv (f g h : Î± â†’ Î²) : (f - g) â—‹ h = f â—‹ h - g â—‹ h := by
  simp only [sub_eq_add_neg, add_dconv, neg_dconv]

end CommRing

section Semifield
variable [Semifield Î²] [StarRing Î²]

@[simp] lemma mu_univ_conv_mu_univ : Î¼_[Î²] (univ : Finset Î±) âˆ— Î¼ univ = Î¼ univ := by
  ext; cases eq_or_ne (card Î± : Î²) 0 <;> simp [mu_apply, conv_eq_sum_add, card_univ, *]

@[simp] lemma mu_univ_dconv_mu_univ : Î¼_[Î²] (univ : Finset Î±) â—‹ Î¼ univ = Î¼ univ := by
  ext; cases eq_or_ne (card Î± : Î²) 0 <;> simp [mu_apply, dconv_eq_sum_add, card_univ, *]

lemma expect_conv (f g : Î± â†’ Î²) : ğ”¼ a, (f âˆ— g) a = (âˆ‘ a, f a) * ğ”¼ a, g a := by
  simp_rw [expect, sum_conv, mul_div_assoc]

lemma expect_dconv (f g : Î± â†’ Î²) : ğ”¼ a, (f â—‹ g) a = (âˆ‘ a, f a) * ğ”¼ a, conj (g a) := by
  simp_rw [expect, sum_dconv, mul_div_assoc]

lemma expect_conv' (f g : Î± â†’ Î²) : ğ”¼ a, (f âˆ— g) a = (ğ”¼ a, f a) * âˆ‘ a, g a := by
  simp_rw [expect, sum_conv, mul_div_right_comm]

lemma expect_dconv' (f g : Î± â†’ Î²) : ğ”¼ a, (f â—‹ g) a = (ğ”¼ a, f a) * âˆ‘ a, conj (g a) := by
  simp_rw [expect, sum_dconv, mul_div_right_comm]

end Semifield

section Field
variable [Field Î²] [StarRing Î²] [CharZero Î²]

@[simp] lemma balance_conv (f g : Î± â†’ Î²) : balance (f âˆ— g) = balance f âˆ— balance g := by
  simpa [balance, conv_sub, sub_conv, expect_conv] using (mul_comm_div _ _ _).symm

@[simp] lemma balance_dconv (f g : Î± â†’ Î²) : balance (f â—‹ g) = balance f â—‹ balance g := by
  simpa [balance, dconv_sub, sub_dconv, expect_dconv, map_expect] using (mul_comm_div _ _ _).symm

end Field

namespace IsROrC
variable {ğ•œ : Type} [IsROrC ğ•œ] (f g : Î± â†’ â„) (a : Î±)

@[simp, norm_cast]
lemma coe_conv : (â†‘((f âˆ— g) a) : ğ•œ) = ((â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g) a :=
  map_conv (algebraMap â„ ğ•œ) _ _ _

@[simp, norm_cast]
lemma coe_dconv : (â†‘((f â—‹ g) a) : ğ•œ) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := by simp [dconv_apply, coe_sum]

@[simp] lemma coe_comp_conv : ((â†‘) : â„ â†’ ğ•œ) âˆ˜ (f âˆ— g) = (â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g := funext $ coe_conv _ _
@[simp] lemma coe_comp_dconv : ((â†‘) : â„ â†’ ğ•œ) âˆ˜ (f â—‹ g) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g := funext $ coe_dconv _ _

end IsROrC

namespace Complex
variable (f g : Î± â†’ â„) (n : â„•) (a : Î±)

@[simp, norm_cast]
lemma coe_conv : (â†‘((f âˆ— g) a) : â„‚) = ((â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g) a := IsROrC.coe_conv _ _ _

@[simp, norm_cast]
lemma coe_dconv : (â†‘((f â—‹ g) a) : â„‚) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := IsROrC.coe_dconv _ _ _

@[simp] lemma coe_comp_conv : ((â†‘) : â„ â†’ â„‚) âˆ˜ (f âˆ— g) = (â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g := funext $ coe_conv _ _
@[simp] lemma coe_comp_dconv : ((â†‘) : â„ â†’ â„‚) âˆ˜ (f â—‹ g) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g := funext $ coe_dconv _ _

end Complex

namespace NNReal
variable (f g : Î± â†’ â„â‰¥0) (a : Î±)

@[simp, norm_cast]
lemma coe_conv : (â†‘((f âˆ— g) a) : â„) = ((â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g) a := map_conv NNReal.toRealHom _ _ _

@[simp, norm_cast]
lemma coe_dconv : (â†‘((f â—‹ g) a) : â„) = ((â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g) a := by simp [dconv_apply, coe_sum]

@[simp] lemma coe_comp_conv : ((â†‘) : _ â†’ â„) âˆ˜ (f âˆ— g) = (â†‘) âˆ˜ f âˆ— (â†‘) âˆ˜ g := funext $ coe_conv _ _
@[simp] lemma coe_comp_dconv : ((â†‘) : _ â†’ â„) âˆ˜ (f â—‹ g) = (â†‘) âˆ˜ f â—‹ (â†‘) âˆ˜ g := funext $ coe_dconv _ _

end NNReal

/-! ### Iterated convolution -/

section CommSemiring
variable [CommSemiring Î²] [StarRing Î²] {f g : Î± â†’ Î²} {n : â„•}

/-- Iterated convolution. -/
def iterConv (f : Î± â†’ Î²) : â„• â†’ Î± â†’ Î²
  | 0 => trivChar
  | n + 1 => f âˆ— iterConv f n

infixl:78 " âˆ—^ " => iterConv

@[simp] lemma iterConv_zero (f : Î± â†’ Î²) : f âˆ—^ 0 = trivChar := rfl
@[simp] lemma iterConv_one (f : Î± â†’ Î²) : f âˆ—^ 1 = f := conv_trivChar _

lemma iterConv_succ (f : Î± â†’ Î²) (n : â„•) : f âˆ—^ (n + 1) = f âˆ— f âˆ—^ n := rfl
lemma iterConv_succ' (f : Î± â†’ Î²) (n : â„•) : f âˆ—^ (n + 1) = f âˆ—^ n âˆ— f := conv_comm _ _

lemma iterConv_add (f : Î± â†’ Î²) (m : â„•) : âˆ€ n, f âˆ—^ (m + n) = f âˆ—^ m âˆ— f âˆ—^ n
  | 0 => by simp
  | n + 1 => by simp [â†add_assoc, iterConv_succ, iterConv_add, conv_left_comm]

lemma iterConv_mul (f : Î± â†’ Î²) (m : â„•) : âˆ€ n, f âˆ—^ (m * n) = f âˆ—^ m âˆ—^ n
  | 0 => rfl
  | n + 1 => by simp [mul_add_one, iterConv_succ', iterConv_add, iterConv_mul]

lemma iterConv_mul' (f : Î± â†’ Î²) (m n : â„•) : f âˆ—^ (m * n) = f âˆ—^ n âˆ—^ m := by
  rw [mul_comm, iterConv_mul]

@[simp] lemma conj_iterConv (f : Î± â†’ Î²) : âˆ€ n, conj (f âˆ—^ n) = conj f âˆ—^ n
  | 0 => by ext; simp
  | n + 1 => by simp [iterConv_succ, conj_iterConv]

lemma IsSelfAdjoint.iterConv (hf : IsSelfAdjoint f) (n : â„•) : IsSelfAdjoint (f âˆ—^ n) :=
  (conj_iterConv _ _).trans $ congr_arg (Â· âˆ—^ n) hf

@[simp]
lemma conjneg_iterConv (f : Î± â†’ Î²) : âˆ€ n, conjneg (f âˆ—^ n) = conjneg f âˆ—^ n
  | 0 => by ext; simp
  | n + 1 => by simp [iterConv_succ, conjneg_iterConv]

lemma iterConv_conv_distrib (f g : Î± â†’ Î²) : âˆ€ n, (f âˆ— g) âˆ—^ n = f âˆ—^ n âˆ— g âˆ—^ n
  | 0 => (conv_trivChar _).symm
  | n + 1 => by simp_rw [iterConv_succ, iterConv_conv_distrib, conv_conv_conv_comm]

lemma iterConv_dconv_distrib (f g : Î± â†’ Î²) : âˆ€ n, (f â—‹ g) âˆ—^ n = f âˆ—^ n â—‹ g âˆ—^ n
  | 0 => (dconv_trivChar _).symm
  | n + 1 => by simp_rw [iterConv_succ, iterConv_dconv_distrib, conv_dconv_conv_comm]

@[simp] lemma zero_iterConv : âˆ€ {n}, n â‰  0 â†’ (0 : Î± â†’ Î²) âˆ—^ n = 0
  | 0, hn => by cases hn rfl
  | n + 1, _ => zero_conv _

@[simp] lemma smul_iterConv [Monoid Î³] [DistribMulAction Î³ Î²] [IsScalarTower Î³ Î² Î²]
    [SMulCommClass Î³ Î² Î²] (c : Î³) (f : Î± â†’ Î²) : âˆ€ n, (c â€¢ f) âˆ—^ n = c ^ n â€¢ f âˆ—^ n
  | 0 => by simp
  | n + 1 => by simp_rw [iterConv_succ, smul_iterConv _ _ n, pow_succ, mul_smul_conv_comm]

lemma comp_iterConv {Î³} [CommSemiring Î³] [StarRing Î³] (m : Î² â†’+* Î³) (f : Î± â†’ Î²) :
    âˆ€ n, m âˆ˜ (f âˆ—^ n) = m âˆ˜ f âˆ—^ n
  | 0 => by ext; simp
  | n + 1 => by simp [iterConv_succ, comp_conv, comp_iterConv]

lemma map_iterConv {Î³} [CommSemiring Î³] [StarRing Î³] (m : Î² â†’+* Î³) (f : Î± â†’ Î²) (a : Î±) (n : â„•) :
    m ((f âˆ—^ n) a) = (m âˆ˜ f âˆ—^ n) a :=
  congr_fun (comp_iterConv m _ _) _

lemma sum_iterConv (f : Î± â†’ Î²) : âˆ€ n, âˆ‘ a, (f âˆ—^ n) a = (âˆ‘ a, f a) ^ n
  | 0 => by simp [filter_eq']
  | n + 1 => by simp only [iterConv_succ, sum_conv, sum_iterConv, pow_succ]

@[simp] lemma iterConv_trivChar : âˆ€ n, (trivChar : Î± â†’ Î²) âˆ—^ n = trivChar
  | 0 => rfl
  | _n + 1 => (trivChar_conv _).trans $ iterConv_trivChar _

lemma support_iterConv_subset (f : Î± â†’ Î²) : âˆ€ n, support (f âˆ—^ n) âŠ† n â€¢ support f
  | 0 => by
    simp only [iterConv_zero, zero_smul, support_subset_iff, Ne.def, ite_eq_right_iff, not_forall,
      exists_prop, Set.mem_zero, and_imp, forall_eq, eq_self_iff_true, imp_true_iff, trivChar_apply]
  | n + 1 => (support_conv_subset _ _).trans $ Set.add_subset_add_left $ support_iterConv_subset _ _

lemma indicate_iterConv_apply (s : Finset Î±) (n : â„•) (a : Î±) :
    (ğŸ­_[â„] s âˆ—^ n) a = ((piFinset fun _i â†¦ s).filter fun x : Fin n â†’ Î± â†¦ âˆ‘ i, x i = a).card := by
  induction' n with n ih generalizing a
  Â· simp [apply_ite card, eq_comm]
  simp_rw [iterConv_succ, conv_eq_sum_sub', ih, indicate_apply, boole_mul, sum_ite, filter_mem_univ,
    sum_const_zero, add_zero, â†Nat.cast_sum, â†Finset.card_sigma, Nat.cast_inj]
  refine' Finset.card_congr (fun f _ â†¦ Fin.cons f.1 f.2) _ _ _
  Â· simp only [Fin.sum_cons, eq_sub_iff_add_eq', mem_sigma, mem_filter, mem_piFinset, and_imp]
    refine' fun bf hb hf ha â†¦ âŸ¨Fin.cases _ _, haâŸ©
    Â· exact hb
    Â· simpa only [Fin.cons_succ]
  Â· simp only [Sigma.ext_iff, Fin.cons_eq_cons, heq_iff_eq, imp_self, imp_true_iff, forall_const,
      Sigma.forall]
  Â· simp only [mem_filter, mem_piFinset, mem_sigma, exists_prop, Sigma.exists, and_imp,
      eq_sub_iff_add_eq', and_assoc]
    exact fun f hf ha â†¦
      âŸ¨f 0, Fin.tail f, hf _, fun _ â†¦ hf _, (Fin.sum_univ_succ _).symm.trans ha,
        Fin.cons_self_tail _âŸ©

end CommSemiring

section Field
variable [Field Î²] [StarRing Î²] [CharZero Î²]

@[simp] lemma balance_iterConv (f : Î± â†’ Î²) : âˆ€ {n}, n â‰  0 â†’ balance (f âˆ—^ n) = balance f âˆ—^ n
  | 0, h => by cases h rfl
  | 1, _ => by simp
  | n + 2, _ => by simp [iterConv_succ _ (n + 1), balance_iterConv _ n.succ_ne_zero]

end Field

namespace NNReal

@[simp, norm_cast]
lemma coe_iterConv (f : Î± â†’ â„â‰¥0) (n : â„•) (a : Î±) : (â†‘((f âˆ—^ n) a) : â„) = ((â†‘) âˆ˜ f âˆ—^ n) a :=
  map_iterConv NNReal.toRealHom _ _ _

end NNReal

namespace Complex

@[simp, norm_cast]
lemma coe_iterConv (f : Î± â†’ â„) (n : â„•) (a : Î±) : (â†‘((f âˆ—^ n) a) : â„‚) = ((â†‘) âˆ˜ f âˆ—^ n) a :=
  map_iterConv ofReal _ _ _

end Complex
