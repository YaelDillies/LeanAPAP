import LeanAPAP.Mathlib.Analysis.Complex.Basic
import LeanAPAP.Prereqs.AddChar.PontryaginDuality
import LeanAPAP.Prereqs.LpNorm.Compact
import LeanAPAP.Prereqs.Convolution.Discrete.Defs

/-!
# Discrete Fourier transform

This file defines the discrete Fourier transform and shows the Parseval-Plancherel identity and
Fourier inversion formula for it.
-/

open AddChar Finset Function
open Fintype (card)
open scoped BigOperators ComplexConjugate ComplexOrder

variable {Î± Î³ : Type*} [AddCommGroup Î±] [Fintype Î±] {f : Î± â†’ â„‚} {Ïˆ : AddChar Î± â„‚} {n : â„•}

/-- The discrete Fourier transform. -/
def dft (f : Î± â†’ â„‚) : AddChar Î± â„‚ â†’ â„‚ := fun Ïˆ â†¦ âŸªÏˆ, fâŸ«_[â„‚]

lemma dft_apply (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : dft f Ïˆ = âŸªÏˆ, fâŸ«_[â„‚] := rfl

@[simp] lemma dft_zero : dft (0 : Î± â†’ â„‚) = 0 := by ext; simp [dft_apply]

@[simp] lemma dft_add (f g : Î± â†’ â„‚) : dft (f + g) = dft f + dft g := by
  ext; simp [l2Inner_add_right, dft_apply]

@[simp] lemma dft_neg (f : Î± â†’ â„‚) : dft (-f) = - dft f := by ext; simp [dft_apply]

@[simp] lemma dft_sub (f g : Î± â†’ â„‚) : dft (f - g) = dft f - dft g := by
  ext; simp [l2Inner_sub_right, dft_apply]

@[simp] lemma dft_const (a : â„‚) (hÏˆ : Ïˆ â‰  0) : dft (const Î± a) Ïˆ = 0 := by
  simp only [dft_apply, l2Inner_eq_sum, const_apply, â†sum_mul, â†map_sum,
    sum_eq_zero_iff_ne_zero.2 hÏˆ, map_zero, zero_mul]

@[simp] lemma dft_smul [DistribSMul Î³ â„‚] [Star Î³] [StarModule Î³ â„‚] [SMulCommClass Î³ â„‚ â„‚] (c : Î³)
    (f : Î± â†’ â„‚) : dft (c â€¢ f) = c â€¢ dft f := by ext; simp [l2Inner_smul_right, dft_apply]

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
@[simp] lemma nl2Inner_dft (f g : Î± â†’ â„‚) : âŸªdft f, dft gâŸ«â‚™_[â„‚] = âŸªf, gâŸ«_[â„‚] := by
  classical
  unfold dft
  simp_rw [l2Inner_eq_sum, nl2Inner_eq_expect, map_sum, map_mul, starRingEnd_self_apply, sum_mul,
    mul_sum, expect_sum_comm, mul_mul_mul_comm _ (conj $ f _), â†expect_mul, â†
    AddChar.inv_apply_eq_conj, â†map_neg_eq_inv, â†map_add_eq_mul, AddChar.expect_apply_eq_ite,
    add_neg_eq_zero, boole_mul, Fintype.sum_ite_eq]

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
@[simp] lemma nl2Norm_dft (f : Î± â†’ â„‚) : â€–dft fâ€–â‚™_[2] = â€–fâ€–_[2] :=
  (sq_eq_sq nlpNorm_nonneg lpNorm_nonneg).1 $ Complex.ofReal_injective $ by
    push_cast; simpa only [nl2Inner_self, l2Inner_self] using nl2Inner_dft f f

/-- **Fourier inversion** for the discrete Fourier transform. -/
lemma dft_inversion (f : Î± â†’ â„‚) (a : Î±) : ğ”¼ Ïˆ, dft f Ïˆ * Ïˆ a = f a := by
  classical simp_rw [dft, l2Inner_eq_sum, sum_mul, expect_sum_comm, mul_right_comm _ (f _),
    â† expect_mul, â†AddChar.inv_apply_eq_conj, inv_mul_eq_div, â†map_sub_eq_div,
    AddChar.expect_apply_eq_ite, sub_eq_zero, boole_mul, Fintype.sum_ite_eq]

/-- **Fourier inversion** for the discrete Fourier transform. -/
lemma dft_inversion' (f : Î± â†’ â„‚) (a : Î±) : âˆ‘ Ïˆ : AddChar Î± â„‚, dft f Ïˆ * Ïˆ a = card Î± * f a := by
  rw [mul_comm, â† div_eq_iff, â† dft_inversion f, â† AddChar.card_eq,
    Fintype.expect_eq_sum_div_card (Î± := â„‚)]
  simp

lemma dft_dft_doubleDualEmb (f : Î± â†’ â„‚) (a : Î±) :
    dft (dft f) (doubleDualEmb a) = card Î± * f (-a) := by
  simp only [â†dft_inversion f (-a), mul_comm (conj _), dft_apply, l2Inner_eq_sum, map_neg_eq_inv,
    AddChar.inv_apply_eq_conj, doubleDualEmb_apply, â† Fintype.card_mul_expect, AddChar.card_eq]

lemma dft_dft (f : Î± â†’ â„‚) : dft (dft f) = card Î± * f âˆ˜ doubleDualEquiv.symm âˆ˜ Neg.neg :=
  funext fun a â†¦ by
    simp_rw [Pi.mul_apply, Function.comp_apply, map_neg, Pi.natCast_apply, â†dft_dft_doubleDualEmb,
      doubleDualEmb_doubleDualEquiv_symm_apply]

lemma dft_injective : Injective (dft : (Î± â†’ â„‚) â†’ AddChar Î± â„‚ â†’ â„‚) := fun f g h â†¦
  funext fun a â†¦ (dft_inversion _ _).symm.trans $ by rw [h, dft_inversion]

lemma dft_inv (Ïˆ : AddChar Î± â„‚) (hf : IsSelfAdjoint f) : dft f Ïˆâ»Â¹ = conj (dft f Ïˆ) := by
  simp_rw [dft_apply, l2Inner_eq_sum, map_sum, AddChar.inv_apply', map_mul,
    AddChar.inv_apply_eq_conj, Complex.conj_conj, (hf.apply _).conj_eq]

@[simp]
lemma dft_conj (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : dft (conj f) Ïˆ = conj (dft f Ïˆâ»Â¹) := by
  simp only [dft_apply, l2Inner_eq_sum, map_sum, map_mul, â†inv_apply', â†inv_apply_eq_conj,
    inv_inv, Pi.conj_apply]

lemma dft_conjneg_apply (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : dft (conjneg f) Ïˆ = conj (dft f Ïˆ) := by
  simp only [dft_apply, l2Inner_eq_sum, conjneg_apply, map_sum, map_mul, RCLike.conj_conj]
  refine Fintype.sum_equiv (Equiv.neg Î±) _ _ fun i â†¦ ?_
  simp only [Equiv.neg_apply, â†inv_apply_eq_conj, â†inv_apply', inv_apply]

@[simp]
lemma dft_conjneg (f : Î± â†’ â„‚) : dft (conjneg f) = conj (dft f) := funext $ dft_conjneg_apply _

@[simp] lemma dft_balance (f : Î± â†’ â„‚) (hÏˆ : Ïˆ â‰  0) : dft (balance f) Ïˆ = dft f Ïˆ := by
  simp only [balance, Pi.sub_apply, dft_sub, dft_const _ hÏˆ, sub_zero]

lemma dft_dilate (f : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) (hn : (card Î±).Coprime n) :
    dft (dilate f n) Ïˆ = dft f (Ïˆ ^ n) := by
  simp_rw [dft_apply, l2Inner_eq_sum, dilate]
  rw [â† Nat.card_eq_fintype_card] at hn
  refine (Fintype.sum_bijective _ hn.nsmul_right_bijective _ _  ?_).symm
  simp only [pow_apply, â† map_nsmul_eq_pow, zmod_val_inv_nsmul_nsmul hn, forall_const]

@[simp] lemma dft_trivChar [DecidableEq Î±] : dft (trivChar : Î± â†’ â„‚) = 1 := by
  ext; simp [trivChar_apply, dft_apply, l2Inner_eq_sum, â†map_sum]

@[simp] lemma dft_one : dft (1 : Î± â†’ â„‚) = card Î± â€¢ trivChar :=
  dft_injective $ by classical rw [dft_smul, dft_trivChar, dft_dft, Pi.one_comp, nsmul_eq_mul]

variable [DecidableEq Î±]

@[simp] lemma dft_indicate_zero (A : Finset Î±) : dft (ğŸ­ A) 0 = A.card := by
  simp only [dft_apply, l2Inner_eq_sum, sum_indicate, AddChar.zero_apply, map_one, one_mul]

lemma dft_conv_apply (f g : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) : dft (f âˆ— g) Ïˆ = dft f Ïˆ * dft g Ïˆ := by
  simp_rw [dft, l2Inner_eq_sum, conv_eq_sum_sub', mul_sum, sum_mul, â†sum_product',
    univ_product_univ]
  refine Fintype.sum_equiv ((Equiv.prodComm _ _).trans $
    ((Equiv.refl _).prodShear Equiv.subRight).trans $ Equiv.prodComm _ _)  _ _ fun (a, b) â†¦ ?_
  simp only [Equiv.trans_apply, Equiv.prodComm_apply, Equiv.prodShear_apply, Prod.fst_swap,
    Equiv.refl_apply, Prod.snd_swap, Equiv.subRight_apply, Prod.swap_prod_mk, Prod.forall]
  rw [mul_mul_mul_comm, â†map_mul, â†map_add_eq_mul, add_sub_cancel]

lemma dft_dconv_apply (f g : Î± â†’ â„‚) (Ïˆ : AddChar Î± â„‚) :
    dft (f â—‹ g) Ïˆ = dft f Ïˆ * conj (dft g Ïˆ) := by
  rw [â†conv_conjneg, dft_conv_apply, dft_conjneg_apply]

@[simp] lemma dft_conv (f g : Î± â†’ â„‚) : dft (f âˆ— g) = dft f * dft g := funext $ dft_conv_apply _ _

@[simp]
lemma dft_dconv (f g : Î± â†’ â„‚) : dft (f â—‹ g) = dft f * conj (dft g) := funext $ dft_dconv_apply _ _

@[simp] lemma dft_iterConv (f : Î± â†’ â„‚) : âˆ€ n, dft (f âˆ—^ n) = dft f ^ n
  | 0 => dft_trivChar
  | n + 1 => by simp [iterConv_succ, pow_succ, dft_iterConv]

@[simp] lemma dft_iterConv_apply (f : Î± â†’ â„‚) (n : â„•) (Ïˆ : AddChar Î± â„‚) :
    dft (f âˆ—^ n) Ïˆ = dft f Ïˆ ^ n := congr_fun (dft_iterConv _ _) _

lemma lpNorm_conv_le_lpNorm_dconv (hnâ‚€ : n â‰  0) (hn : Even n) (f : Î± â†’ â„‚) :
    â€–f âˆ— fâ€–_[n] â‰¤ â€–f â—‹ fâ€–_[n] := by
  refine le_of_pow_le_pow_left hnâ‚€ (by positivity) ?_
  obtain âŸ¨k, hnkâŸ© := hn.two_dvd
  calc â€–f âˆ— fâ€–_[n] ^ n
      = âˆ‘ x, â€–(f âˆ— f) xâ€– ^ n := lpNorm_pow_eq_sum hnâ‚€ _
    _ = âˆ‘ x, â€–(ğ”¼ Ïˆ, dft f Ïˆ ^ 2 * Ïˆ x)â€– ^ n := by
        simp_rw [â† norm_pow, â† dft_inversion (f âˆ— f), dft_conv_apply, sq]
    _ â‰¤ âˆ‘ x, â€–ğ”¼ Ïˆ, â€–dft f Ïˆâ€– ^ 2 * Ïˆ xâ€– ^ n := Complex.le_of_eq_sum_of_eq_sum_norm
          (fun Ïˆ : (Fin n â†’ AddChar Î± â„‚) Ã— (Fin n â†’ AddChar Î± â„‚) â†¦ conj (âˆ i, dft f (Ïˆ.1 i) ^ 2) *
            (âˆ i, dft f (Ïˆ.2 i) ^ 2) * âˆ‘ x, (âˆ‘ i, Ïˆ.1 i - âˆ‘ i, Ïˆ.2 i) x) univ (by positivity) ?_ ?_
    _ = âˆ‘ x, â€–(f â—‹ f) xâ€– ^ n := by
        simp_rw [â† norm_pow, â† dft_inversion (f â—‹ f), dft_dconv_apply, Complex.mul_conj']
    _ = â€–f â—‹ fâ€–_[n] ^ n := (lpNorm_pow_eq_sum hnâ‚€ _).symm
  Â· push_cast
    simp_rw [hnk, pow_mul, â† Complex.conj_mul', map_expect, mul_pow, expect_pow, expect_mul_expect]
    sorry
  sorry
--   simp_rw [lpNorm_pow_eq_sum hnâ‚€, mul_sum, â†mul_pow, â†nsmul_eq_mul, â†norm_nsmul, nsmul_eq_mul,
--     â† dft_inversion', dft_conv, dft_dconv, Pi.mul_apply]
--   rw [â†Real.norm_of_nonneg (sum_nonneg fun i _ â†¦ ?_), â†Complex.norm_real]
--   rw [Complex.ofReal_sum (univ : Finset Î±)]
--   any_goals positivity
--   simp_rw [pow_mul', â†norm_pow _ n, Complex.ofReal_pow, â†Complex.conj_mul', map_pow, map_sum,
--     map_mul, Fintype.sum_pow, Fintype.sum_mul_sum]
--   simp only [@sum_comm _ _ Î±, â†mul_sum, prod_mul_prod_comm]
--   refine (norm_sum_le _ _).trans_eq (Complex.ofReal_injective _)
--   simp only [norm_mul, norm_prod, RCLike.norm_conj, â†pow_mul]
--   push_cast
--   have : âˆ€ f g : Fin n â†’ AddChar Î± â„‚, 0 â‰¤ âˆ‘ a, âˆ i, conj (f i a) * g i a := by
--     rintro f g
--     suffices : âˆ‘ a, âˆ i, conj (f i a) * g i a = if âˆ‘ i, (g i - f i) = 0 then (card Î± : â„‚) else 0
--     Â· rw [this]
--       split_ifs <;> positivity
--     simp_rw [â†AddChar.sum_eq_ite, AddChar.sum_apply, AddChar.sub_apply, AddChar.map_neg_eq_inv,
--       AddChar.inv_apply_eq_conj, mul_comm]
--   simp only [RCLike.ofReal_pow, pow_mul, â†Complex.conj_mul', map_sum, map_mul, Complex.conj_conj,
--     Pi.conj_apply, mul_pow, Fintype.sum_pow, â†sq, Fintype.sum_mul_sum]
--   conv_lhs =>
--     arg 2
--     ext
--     rw [â†Complex.eq_coe_norm_of_nonneg (this _ _)]
--   simp only [@sum_comm _ _ Î±, mul_sum, map_prod, map_mul, RCLike.conj_conj, â†prod_mul_distrib]
--   refine sum_congr rfl fun x _ â†¦ sum_congr rfl fun a _ â†¦ prod_congr rfl fun i _ â†¦ _
--   ring

--TODO: Can we unify with `lpNorm_conv_le_lpNorm_dconv`?
lemma lpNorm_conv_le_lpNorm_dconv' (hnâ‚€ : n â‰  0) (hn : Even n) (f : Î± â†’ â„) :
    â€–f âˆ— fâ€–_[n] â‰¤ â€–f â—‹ fâ€–_[n] := by
  simpa only [â†Complex.coe_comp_conv, â†Complex.coe_comp_dconv, Complex.lpNorm_coe_comp] using
    lpNorm_conv_le_lpNorm_dconv hnâ‚€ hn ((â†‘) âˆ˜ f)
