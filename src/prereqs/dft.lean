import mathlib.number_theory.legendre_symbol.add_char.duality
import prereqs.convolution.basic

/-!
# Discrete Fourier transform

This file defines the discrete Fourier transform and shows the Parseval-Plancherel identity and
Fourier inversion formula for it.
-/

open add_char finset fintype (card) function.
open_locale big_operators complex_conjugate

variables {Î± : Type*} [add_comm_group Î±] [fintype Î±] {f : Î± â†’ â„‚} {Ïˆ : add_char Î± â„‚} {n : â„•}

/-- The discrete Fourier transform. -/
def dft (f : Î± â†’ â„‚) : add_char Î± â„‚ â†’ â„‚ := Î» Ïˆ, âŸªÏˆ, fâŸ«_[â„‚]

lemma dft_apply (f : Î± â†’ â„‚) (Ïˆ : add_char Î± â„‚) : dft f Ïˆ = âŸªÏˆ, fâŸ«_[â„‚] := rfl

@[simp] lemma dft_zero : dft (0 : Î± â†’ â„‚) = 0 := by ext; simp [dft_apply]

@[simp] lemma dft_add (f g : Î± â†’ â„‚) : dft (f + g) = dft f + dft g :=
by ext : 1; simp [L2inner_add_right, dft_apply]

@[simp] lemma dft_sub (f g : Î± â†’ â„‚) : dft (f - g) = dft f - dft g :=
by ext : 1; simp [L2inner_sub_right, dft_apply]

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
@[simp] lemma L2inner_dft (f g : Î± â†’ â„‚) : âŸªdft f, dft gâŸ«_[â„‚] = card Î± * âŸªf, gâŸ«_[â„‚] :=
begin
  classical,
  simp_rw [dft, L2inner_eq_sum, map_sum, map_mul, star_ring_end_self_apply, sum_mul,
    mul_sum, @sum_comm _ _ (add_char _ _), mul_mul_mul_comm _ (conj $ f _), â†sum_mul,
    â†add_char.inv_apply_eq_conj, â†map_neg_eq_inv, â†map_add_mul, add_char.sum_apply, add_neg_eq_zero,
    ite_mul, zero_mul, fintype.sum_ite_eq],
end

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
lemma L2norm_dft_sq (f : Î± â†’ â„‚) : â€–dft fâ€–_[2] ^ 2 = card Î± * â€–fâ€–_[2] ^ 2 :=
complex.of_real_injective $ by push_cast; simpa only [L2inner_self] using L2inner_dft f f

/-- **Parseval-Plancherel identity** for the discrete Fourier transform. -/
@[simp] lemma L2norm_dft (f : Î± â†’ â„‚) : â€–dft fâ€–_[2] = real.sqrt (card Î±) * â€–fâ€–_[2] :=
by simpa using congr_arg real.sqrt (L2norm_dft_sq f)

/-- **Fourier inversion** for the discrete Fourier transform. -/
lemma dft_inversion (f : Î± â†’ â„‚) (a : Î±) : âˆ‘ Ïˆ : add_char Î± â„‚, dft f Ïˆ * Ïˆ a = card Î± * f a :=
begin
  classical,
  simp_rw [dft, L2inner_eq_sum, sum_mul, @sum_comm _ Î±, mul_right_comm _ (f _), â†sum_mul,
    â†add_char.inv_apply_eq_conj, inv_mul_eq_div, â†map_sub_eq_div, add_char.sum_apply, sub_eq_zero,
    ite_mul, zero_mul, fintype.sum_ite_eq],
end

lemma dft_inv (Ïˆ : add_char Î± â„‚) (hf : is_self_adjoint f) : dft f Ïˆâ»Â¹ = conj (dft f Ïˆ) :=
by simp_rw [dft_apply, L2inner_eq_sum, map_sum, add_char.inv_apply', map_mul,
  add_char.inv_apply_eq_conj, complex.conj_conj, (hf.apply _).conj_eq]

@[simp] lemma dft_balance (f : Î± â†’ â„‚) (hÏˆ : Ïˆ â‰  1) : dft (balance f) Ïˆ = dft f Ïˆ :=
begin
  simp only [dft_apply, L2inner_eq_sum, balance, mul_sub, sum_sub_distrib],
  rw [â†sum_mul, â†map_sum, sum_eq_zero_iff_ne_one.2 hÏˆ, map_zero, zero_mul, sub_zero],
end

lemma dft_dilate (f : Î± â†’ â„‚) (Ïˆ : add_char Î± â„‚) (hn : n.coprime (card Î±)) :
  dft (dilate f n) Ïˆ = dft f (Ïˆ ^ n) :=
begin
  simp_rw [dft_apply, L2inner_eq_sum, dilate],
  refine sum_nbij' ((â€¢) (nâ»Â¹ : zmod (card Î±)).val) _ (Î» x hx, _) ((â€¢) n) _ _ _,
  { simp only [mem_univ, forall_const] },
  { rw [pow_apply, â†map_nsmul_pow, nsmul_zmod_val_inv_nsmul hn] },
  all_goals { simp only [hn, mem_univ, nsmul_zmod_val_inv_nsmul, zmod_val_inv_nsmul_nsmul,
    eq_self_iff_true, forall_const] },
end

variables [decidable_eq Î±]

@[simp] lemma dft_indicate_one (A : finset Î±) : dft (ğŸ­ A) 1 = A.card :=
begin
  rw [dft_apply, L2inner_eq_sum, â†sum_indicate],
  simp only [monoid_hom.one_apply, coe_one_unit_sphere, map_one, one_mul],
end

lemma dft_conv_apply (f g : Î± â†’ â„‚) (Ïˆ : add_char Î± â„‚) : dft (f âˆ— g) Ïˆ = dft f Ïˆ * dft g Ïˆ :=
begin
  simp_rw [dft, L2inner_eq_sum, conv_eq_sum_sub', mul_sum, sum_mul, â†sum_product',
    univ_product_univ],
  refine sum_nbij' (Î» x, (x.1 - x.2, x.2)) (by simp) (Î» x _, _) (Î» x, (x.1 + x.2, x.2))
    (by simp) (by simp) (by simp),
  rw [mul_mul_mul_comm, â†map_mul, â†map_add_mul, add_sub_cancel'_right],
end

@[simp] lemma dft_conv (f g : Î± â†’ â„‚) : dft (f âˆ— g) = dft f * dft g := funext $ dft_conv_apply _ _
